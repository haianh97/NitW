title: EnterLevel_A1D3
tags: 
colorID: 0
position: -1182,281
---
<<fadeOut 0.00001>>
<<lookOut Mae instant>>
Mae: o>
<<wait .5>>
<<irisIn .5>>
Mae: Looking alright.
[[God, this shirt really accentuates my roundness, doesn't it?|Roundness]]
[[I have the worst face. I have a nightmare face.|WorstFace]]
===
title: Roundness
tags: 
colorID: 0
position: -1543,539
---
<<closeOldSpeechBubbles>>
<<lookDown Mae>>
<<wait 1>>
<<newSpeechBubble>>
Mae:{locator=Left}I think the round zero doesn’t help. _o_
<<closeOldSpeechBubbles>>
<<newSpeechBubble>>
[[{locator=Right}It's cool, dude. Round is ok.|RoundIsOK]]
[[{locator=Right}I need to stop eating literally everything I see.|NeedToStopEatingEverything]]
===
title: NeedToStopEatingEverything
tags: 
colorID: 0
position: -1298,878
---
<<set $eat_everything_i_see to 1>>
<<closeOldSpeechBubbles>>
Mae: Ugh don't do this to yourself, Mae. :\
Mae: Not tonight.
Mae: Tomorrow you'll do lots of situps :)
Mae: and learn to like horrible things like kale
Mae: and everything will be great.
[[YouCanDoThis]]
===
title: RoundIsOK
tags: 
colorID: 0
position: -1667,925
---
<<set $body_posi to 1>>
<<closeOldSpeechBubbles>>
Mae: <o>
Mae: I'm being body-positive like the internet said.  :)
Mae: I'm a real live person
<<lookOut Mae>>
Mae: _o_
Mae: and no one can prove I'm not. :|
[[YouCanDoThis]]
===
title: WorstFace
tags: 
colorID: 0
position: -778,540
---
<<newSpeechBubble>>
<<rotateHead Mae -10 .5>>
Mae:{locator=Left}Geez. What a mess. _o_
<<closeOldSpeechBubbles>>
<<newSpeechBubble>>
[[{locator=Right}My big dumb eyes. My nightmare eyes.|MyNightmareEyes]]
[[{locator=Right}My messed up ear. My nightmare ear.|MyNightmareEar]]
===
title: MyNightmareEyes
tags: 
colorID: 0
position: -905,870
---
<<closeOldSpeechBubbles>>
<<dilate Mae .75 1>>
Mae: [speed=.5][shake=.03][color=DF3912]Nightmare Eyes[/all] would be an excellent horror movie name. o_o :|
Mae: You’ll see the Nightmare Eyes outside your window.
<<dilate Mae .5 .1>>
<<rotateHead Mae 10 1>>
Mae: [speed=.2][shake=.03][color=DF3912]and you'll know.[/all]
Mae: o_o
<<dilate Mae 1 3>>
<<set $nightmare_eyes to 1>>
[[YouCanDoThis]]
===
title: MyNightmareEar
tags: 
colorID: 0
position: -544,882
---
<<closeOldSpeechBubble>>
Mae: =_=
Mae: Oh yeah, well you should see the dog who did that.
<<set $dog_bite_dead to 1>>
Mae: Oh wait you can't, because he’s dead!
Mae: Because dogs don't live as long as people!
<<rotateHead Mae 10 1>>
<<dilate Mae .5 3>>
Mae: Put that in your pipe and smoke it, dog! \o/ o_o
Mae: [wave]with your skeleton lips![/wave]
<<dilate Mae 1 3>>
[[YouCanDoThis]]

===
title: YouCanDoThis
tags: 
colorID: 0
position: -1103,1444
---
<<rotateHead Mae 0 .7>>
<<lookOut Mae>>
Mae: o_o :| _o_
Mae: You can do this. People like you.
<<newSpeechBubble>>
[[{locator=Left}People don’t like you. Clearly.|PeopleDontLikeYou]]
[[{locator=Left}You don’t like people. Clearly.|YouDontLikePeople]]
===
title: PeopleDontLikeYou
tags: 
colorID: 0
position: -1279,1813
---
<<set $people_dont_like to 1>>
<<newSpeechBubble>>
<<closeOldSpeechBubbles>>
Mae:{locator=Right} No friends at college!
Mae: Not even one.
Mae: Not really their fault. :\
Mae: Something's...
Mae: ...
Mae: Whatever. =_= :|
[[ColeCheck]]
===
title: YouDontLikePeople
tags: 
colorID: 0
position: -922,1821
---
<<set $you_dont_like to 1>>
<<newSpeechBubble>>
<<closeOldSpeechBubbles>>
Mae:{locator=Right} Just because that online test said
Mae: that your best chance at being happy
Mae: is a situation where everyone already likes you
Mae: but they mostly leave you alone
Mae: except when they're delivering food to you...
Mae: that doesn't mean you can hide in your room
Mae: and wait for that to happen.
Mae: That's how hermits are made, Mae.
Mae: And they die alone in the middle of winter.
Mae: Waiting for pizza from friends they don't want to see.
[[ColeCheck]]
===
title: ColeCheck
tags: 
colorID: 0
position: -1038,2183
---
<<if $knows_cole_will_be_there is 1>>
	[[ColeWillBeThere]]
<<else>>
	[[WhatAreYouGonnaSay]]
<<endif>>

===
title: ColeWillBeThere
tags: 
colorID: 0
position: -1345,2517
---
Mae: Who cares if Cole is there?
Mae: Jerk. =_=
[[He’s probably up at school or whatever.|HesProbablyUpAtSchool]]
[[Prom was a mistake in like 10 different ways|PromWasAMistake]]
===
title: HesProbablyUpAtSchool
tags: 
colorID: 0
position: -1542,2876
---
Mae: Taking classes. Being all successful. o_o
<<lookUp Mae>>
Mae: Probably doesn’t even think about me.
Mae: Which is good because I honestly don’t care.
<<lookOut Mae>>
Mae: But I hope to any god who is listening that he isn’t there.
Mae: Please whatevergod.
<<set $whatever_god to 1>>
Mae: Make sure Cole isn’t there tonight.
[[Cole_2]]
===
title: PromWasAMistake
tags: 
colorID: 0
position: -1194,2850
---
<<lookRight Mae>>
Mae: That dress.
Mae: That picture under that doorway flower thing.
Mae: Less slowdancing than slowstanding.
Mae: Careful nontouching. No smooches.
<<set $no_smooches to 1>>
<<lookOut Mae>>
Mae: I'm so glad I'm not a teenager anymore.
[[Cole_2]]
===
title: Cole_2
tags: 
colorID: 0
position: -1331,3150
---
<<if $ate_tacos is 1>>
    <<newSpeechBubble>>
	Mae:{locator=Left} My breath tastes like tacos.
    <<closeAllSpeechBubbles>>
<<endif>>
Mae:{locator=Right} This is a disaster.
[[WhatAreYouGonnaSay]]

===
title: WhatAreYouGonnaSay
tags: 
colorID: 0
position: -1022,3404
---
<<closeAllSpeechBubbles>>
Mae:{locator=Left} Alright, what are you gonna say? o_o
Mae: You're a smooth talker. You're a smoothie. :)
Mae: o/
<<newSpeechBubble>>
<<dilate Mae .5 3>>
[[{locator=Right}[wave]"Hi, I’m Mae! How's it going?"[/wave]|HowsItGoing]]
[[{locator=Right}[wave]"Oh hey, I'm Mae! What's your name?"[/wave]|WhatsYourName]]
[[{locator=Right}[wave]"Hi, I'm Mae! I dropped out of college earlier this week!"[/wave]|IDroppedOut]]
===
title: HowsItGoing
tags: 
colorID: 0
position: -1289,3828
---
<<set $hows_it_going to 1>>
<<closeOldSpeechBubbles>>
Mae: "Really? That's fascinating!"
Mae: "That is so true! Well, see you later!"
<<dilate Mae 1 3>>
Mae: Ok good. That sounds normal. :| _o_
Mae: No one can prove you're not normal.
Mae: And really everyone looks normal in the dark.
Mae: I'm pretty sure that’s the only way sex works.
<<close>>
[[ThisIsHopeless]]
===
title: WhatsYourName
tags: 
colorID: 0
position: -1008,3826
---
<<set $whats_your_name to 1>>
<<closeOldSpeechBubbles>>
Mae: "Oh, I think I know you from somewhere..."
Mae: "It's probably from when we were lovers
Mae: and we beat up that street gang
Mae: with chains and garbage cans."
<<dilate Mae 1 3>>
[[ThisIsHopeless]]
===
title: IDroppedOut
tags: 
colorID: 0
position: -685,3796
---
<<set $dropout_week to 1>>
<<closeOldSpeechBubbles>>
Mae: "And now I'm in the woods!
Mae: drinking!" \o/
<<set $you_dont_like to 1>>
<<dilate Mae 1 3>>
Mae: _o_
Mae: geez this is yet another road to hermit-dom :\
[[ThisIsHopeless]]
===
title: ThisIsHopeless
tags: 
colorID: 0
position: -992,4160
---
<<closeAllSpeechBubbles>>
<<wait 2>>
Mae: =_= :( _o_
<<wait 2>>
Mae: This is hopeless.
Mae: Wheel me out to the curb for garbage day. 
<<close>>
<<wait 1>>
<<lookLeft Mae>>
Mae: Oh geez they’ll be here any minute! o_o :|
<<lookOut Mae>>
Mae: Good luck, kid.
<<newSpeechBubble>>
Mae:{locator=Left} I don’t need luck. =_= :)
<<closeOldSpeechBubble>>
Mae:{locator=Left} I make my own luck.
Mae:{locator=Left} With a luck machine. o_o :|
<<closeAllSpeechBubbles>>
<<fadeOut 1 wait>>
<<sectionTitle TheParty BeaCar>>
===
title: EnterLevel_A2D4
tags: 
colorID: 0
position: -129,271
---
<<fadeOut 0.00001>>
<<lookOut Mae instant>>
<<set $night to 1>>
Mae: o>
<<wait .5>>
<<irisIn .5>>
Mae: Aw yeah normal clothes
Mae: Aw yeah time to get spooky
<<close>>
<<irisOut .5 wait>>
Mae: _o_
<<set $harfest_costume to 1>>
<<playIdleAnimationPlayer>>
<<close>>
<<wait 1>>
<<irisIn .5 wait>>
Mae: Sweeeeeeeeeeet.
Mae: WITCHDAGGAH!!!!
<<close>>
<<set $mirror_done to 1>>
<<irisOut 1 wait>>
<<scene MaeRoom>>
===
