title: EnterLevel_A1D1
tags: 
colorID: 0
position: -211,528
---
[[OutsideDiner]]

===
title: OutsideDiner
tags: 
colorID: 0
position: -208,858
---
<<if $diner is 1>>
	Mae: It’s really great to be back.
	Gregg: Dude we can hang out every day!
    <<musicFadeOut 4>>
	Angus: Oh my god.
    <<alwaysLookAt Gregg SeveredArm>>
    <<alwaysLookAt Bea SeveredArm>>
	Bea: Holy crap.
	Mae: What?
    <<alwaysLookAt Mae SeveredArm>>
	Angus: Is that an arm?
	Bea: That’s an arm.
	Gregg: NOBODY MOVE.
	Gregg: LET’S POKE IT WITH A STICK.
	Mae: I CALL FIRST!!! \o/
	<<fadeOut 1 wait>>
	<<set $diner to 2>>
	<<scene PawsModeSeveredArm>>
<<elseif $diner is 2>>
    <<fadeOutMusic .01>>
    Mae: Hey Aunt Molly! We found an arm!
    Molly: Alright Mae, put the stick down and step away from the arm.
    Mae: Alright alright! Don’t taser me or anything.
    Molly: No promises.
    Gregg: Oh well I think it’s time for Angus and I to get home.
    Gregg: See you tomorrow, Mae!
    Molly: Alright I don’t want any of you walking home alone tonight
    Molly: buddy system. Something bad’s going on.
    Bea: I can drive Mae home, officer.
    Molly: Thank you, Bea.
    Mae: Yeah thanks, Bea! Let’s roll!
    <<unlockDoodle Arm>>
    <<if $found_tattoo>>
        <<unlockDoodle ArmTattoo>>
    <<endif>>
	<<fadeOut 1 wait>>
	<<set $diner to 3>>
	<<scene BeaCar>>
<<endif>>
===
title: Scout1
tags: 
colorID: 0
position: 605,1103
---
[[Scouts]]
===
title: Scout2
tags: 
colorID: 0
position: 911,1094
---
[[Scouts]]
===
title: Scouts
tags: 
colorID: 0
position: 764,1431
---
<<if $scouts_sequence is 0>>
    <<if $scouts is 0>>
        <<set $scouts += 1>>
        [[Scouts_00_00]]
    <<elseif $scouts is 1>>
        [[Scouts_00_01]]
    <<endif>>
<<elseif $scouts_sequence is 1>>
    <<if $scouts is 0>>
        <<set $scouts += 1>>
        [[Scouts_01_00]]
    <<elseif $scouts is 1>>
        <<set $scouts += 1>>
        <<set $scouts_done to 1>>
        [[Scouts_01_01]]
    <<endif>>
<<endif>>
===
title: Scouts_00_00
tags: 
colorID: 0
position: 408,1779
---
Scout1: Hey! Buy some cookies?
Scout2: Yeah, buy some cookies!
Mae: Huh. We always sold cookies in the spring.
Scout1: We have fall cookies now!
Scout2: Yeah, you not up on current events?
<<if $current_events is 1>>
    Mae: Geez! No! I’m not up on current events!
<<endif>>
<<set $current_events to 1>>
Scout1: Buy 9 boxes right now!
Scout2: Pleeeease!!!
Mae: I don't have any money.
Scout1: Then move along, pooreo!
===
title: Scouts_00_01
tags: 
colorID: 0
position: 691,1789
---
Scout1: Cookies! Getcha cookies here!
===
title: Bargoers_A1D1
tags: 
colorID: 2
position: -1084,1795
---
Bargoer1: It’s almost 5! Quittin' time!
Bargoer2: Kick back some brews and watch the Smelters!
<<set $bargoers_done to 1>>
===
title: Bargoers_A1D2
tags: 
colorID: 2
position: -842,1793
---
Bargoer1: Better take shelter! Here come the Smelters!
Bargoer2: No use hidin'. Smelters will find you.
<<set $bargoers_done to 1>>
===
title: LeftSideBlocker
tags: 
colorID: 0
position: 337,678
---
<<if $act is 1>>
    <<if $day is 1>>
        Mae: Don’t think I’m gonna find Gregg out by the abandoned Food Donkey.
        Mae: Better head Snack Falcon-wards.
    <<elseif $day is 2>>
        Mae: I just want my computer. I need the internet.
        Mae: It's more important than whatever's out there.
    <<elseif $day is 3>>
        Mae: Ugh. not hiking out to the edge of town today.
        Mae: got stuff to do.
    <<endif>>
<<elseif $act is 2>>
    <<if $day is 1>>
        [[LeftSideBlocker_A2D1]]
    <<endif>>
<<endif>>
===
title: Bargoers_A2D1
tags: 
colorID: 3
position: -1090,2039
---
Bargoer2: How 'bout them Smelters?
Bargoer1: Kicked some Blue Bear ass last night!
Bargoer2: Good game, good game.
Bargoer1: Gonna be a hell of a season, bud.
<<set $bargoers_done to 1>>
===
title: Scouts_01_00
tags: 
colorID: 0
position: 964,1771
---
Scout1: HEY! BUY 9 BOXES OF FALL COOKIES PLEASE
Mae: No money here!
Scout2: Then mooooove along, peasant!
Scout1: Hey. Listen. We're scouts, ok?
Scout2: Yeah, and?
Scout1: Scouts don't call people peasants, ok?
Scout2: Yeah, you're right.
Scout1: Even if they are peasants.
Scout2: Scouts are nice to peasants.
Mae: You two are learning valuable lessons.
===
title: Bargoers_A1D3
tags: 
colorID: 2
position: -619,1793
---
Bargoer1: Smelters! Smelters! Comin' for you!
Bargoer2: Whatchoo gonna do? Whatchoo gonna do?
<<set $bargoers_done to 1>>
===
title: Scouts_01_01
tags: 
colorID: 0
position: 1233,1776
---
Scout2: Cookies! Help the scouts! Keep us off the streets!
===
title: InitLevel
tags: 
colorID: 0
position: -660,418
---
<<if $act is 1 and $day is 1>>
    <<if $diner is 1>>
    	<<warp Player MaeDiner>>
    	<<show Gregg>>
    	<<show Bea>>
    	<<show Angus>>
    	<<warp Gregg GreggDiner>>
    	<<warp Bea BeaDiner>>
    	<<warp Angus AngusDiner>>
        <<play TowneCentreMusic>>
    <<elseif $diner is 2>>
    	<<warp Player MaeArm>>
    	<<show Gregg>>
    	<<show Bea>>
    	<<show Angus>>
    	<<warp Gregg GreggArm>>
    	<<warp Bea BeaArm>>
    	<<warp Angus AngusArm>>
        <<show Molly>>
        <<flip Molly -1>>
        <<warp Molly MollyArm>>
        <<lookAt Molly Bea instant>>
    <<endif>>
<<endif>>

<<if $act is 2 and $day is 1>>
    <<hide Scout1>>
    <<hide Scout2>>
<<endif>>

<<if $act is 2 and $day is 2>>
    <<show CityCouncil>>
<<else>>
    <<hide CityCouncil>>
<<endif>>

<<if $demo2>>
    <<show Pastor>>
    <<show Bruce>>
    <<setCanInteract Bruce 0>>
<<else>>
    <<hide Pastor>>
    <<hide Bruce>>
<<endif>>

<<if $act is 3 and $day is 1 and $rooftop_hangout>>
    <<cameraLookAt CameraRooftopHangout>>
    <<warp Mae RooftopHangoutMae>>
    <<warp Gregg RooftopHangoutGregg>>
    <<warp Angus RooftopHangoutAngus>>
    <<warp Bea RooftopHangoutBea>>
<<endif>>

<<if $act is 4 and $day is 3>>
    <<hide Bargoer2>>
<<endif>>

<<if $act is 2 and $day is 3>>
    <<show CeeJay>>
<<else>>
    <<hide CeeJay>>
<<endif>>
===
title: OutsideVideoStore.1
tags: demo2
colorID: 0
position: 1457,1058
---
Mae: I don’t think Angus is working today. #line:cb780d
Mae: Who goes to a video store anyway? #line:2c49f2
Mae: I wonder if it’s just weird creepos going to the adult vhs section. #line:83989d
Mae: Creepo: one who demands their porn be delivered via obsolete machines. #line:c71aac
===
title: OutsideVideoStore.2
tags: demo2
colorID: 0
position: 1664,1060
---
Mae: I mean, the rest of us use modern machines like civilized people. #line:8c28f6
<<set $outside_video_store_done to 1>>
===
title: SnackFalconPole.1
tags: demo2
colorID: 0
position: 2001,1089
---
Mae: Granddad read me a story once #line:3f3f31
Mae: about a sailor whose ship was sinking #line:123818
Mae: and he climb up one of the ship... poles?... #line:3647ab
Mae: and the ship landed on some coral or something before it totally sank #line:83eac1
Mae: and he was stuck on the ship pole for a week #line:42d0e7
Mae: watching the sharks circle. #line:4b5649
===
title: SnackFalconPole.2
tags: demo2
colorID: 0
position: 2247,1096
---
Mae: Mast! It’s called a mast. #line:c25c05
Mae: Sounds better than ship pole. #line:a0c4ce

===
title: SnackFalconPole.3
tags: demo2
colorID: 0
position: 2466,1067
---
Mae: Dude ate sea birds and drank hurricane rain. #line:c10dba
Mae: Saw sea-ghosts. #line:018ff4
Mae: Dude was rad. #line:07cf7c
<<set $snack_falcon_pole_done to 1>>
===
title: PreInitLevel_A1D1
tags: 
colorID: 0
position: -232,251
---
<<if $diner is 2>>
    <<destroy _TowneMusic>>
<<endif>>
===
title: LeftSideBlocker_A2D1
tags: demo2
colorID: 0
position: 611,668
---
Mae: Could hike out to the abandoned Food Donkey. #line:c5d910
Mae: ... #line:ec8ff7
Mae: I'm not going to hike out to the abandoned Food Donkey. #line:057d12
===
title: Pastabilities_Act2
tags: 
colorID: 3
position: 1853,1395
---
Mae: what the...
Mae: what...
Mae: NO!
Mae: no no no no no no no no noooooooooo!
Mae: Pastabilities is gone!!!!
Mae: no more pizza delivery!
Mae: no more Big Sal!
Mae: no more special birthdays.
Mae: this is the worst thing that’s happened to this town
Mae: since the flood that took out
->the petting zoo
    Mae: thank goodness all the animals swam to safety
    Mae: on the roof of the tire place.
    Mae: who knew goats were such good swimmers?
->the minigolf ice cream place.
    Mae: summer nights.
    Mae: eating ice cream.
    Mae: playing some putt-putt.
    Mae: all washed away.
Mae: i am so freaking DEPRESSED now.
<<set $knows_pastabilities to 1>>
<<set $pastabilities_act2_done to 1>>
===
title: Pastabilities_Act3
tags: 
colorID: 4
position: 2103,1397
---
Mae: whoa.
Mae: is someone moving in?
Mae: probably something dumb.
Mae: i miss you, pizza place.
<<set $pastabilities_act3_done to 1>>
===
title: Pastabilities_Act4
tags: 
colorID: 5
position: 2361,1400
---
Mae: HOLY CRAP!!!!!
Mae: A...
Mae: AAAAAAAH
Mae: A TACO PLACE!!!!
Mae: this is the best thing that’s happened to this town since
->they got rid of all those snakes in the diner!
    Mae: the owners were religious
    Mae: and they called in a preacher
    Mae: from one of the holiness camps up in the hills.
    Mae: and he got in there and yelled at the snakes to leave.
    Mae: the next day after he died they just set traps.
    Mae: nobody knows where they came from
    Mae: or why they moved in.
    Mae: but they’re long gone now.
->we got internet!
    Mae: we were still using CyberFish until like 2 years ago.
    Mae: because it’s eternally 2 decades behind here.
    Mae: takes you 3 days to torrent a movie.
    Mae: takes you 1 day to load a gif.
    Mae: barbaric times.
Mae: god.
Mae: life moves on, huh.
<<wait 2>>
Mae: oh my god! They deliver!!!
Mae: I’m gonna eat the shit out of some tacos.
<<set $pastabilities_act4_done to 1>>
===
title: ArmSpot.1
tags: 
colorID: 0
position: 2941,748
---
Mae: so how did that arm get here…
Mae: someone just running down the street
Mae: “whoops my arm fell off!”
Mae: “i’ll just come back for that later!”
===
title: ArmSpot.2
tags: 
colorID: 0
position: 3195,737
---
Mae: no one could survive their arm just falling off
Mae: or being chopped off
Mae: like, not by a doctor.
Mae: is it still called assault if you chop an arm off?
Mae: seems like it should be a different word.
Mae: “armchoppy”
Mae: you are under arrest for armchoppy!
===
title: ArmSpot.3
tags: 
colorID: 0
position: 3429,733
---
Mae: whoever’s arm that was is probably dead.
Mae: nah. Definitely.
Mae: unless I see someone walking around
Mae: who until recently had an arm.
<<end>>
===
title: StatueLedge.1
tags: 
colorID: 0
position: 2944,1005
---
Mae: now here’s a man who loved statues.
Mae: don’t get a lot of people getting statues made of themselves today.
Mae: we’re secure in ourselves, ok.
Mae: we don’t need statues to make us feel better.
===
title: StatueLedge.2
tags: 
colorID: 0
position: 3192,1008
---
Mae: if i got a statue i’d have them make me real tall
<<end>>
===
title: HellMalt.1
tags: 
colorID: 0
position: 2935,1236
---
Mae: what the hell is a malt?
<<end>>
===
title: FrontOfVideoStore.1
tags: 
colorID: 0
position: 2933,1483
---
Mae: “too”?
Mae: is that like “two”
Mae: or like “also”
Mae: this is an error so bad even i wouldn’t make it.
===
title: FrontOfVideoStore.2
tags: 
colorID: 0
position: 3179,1485
---
Mae: also where was the other video outpost?
<<end>>
===
title: Bargoers_A2D2
tags: 
colorID: 3
position: -852,2045
---
Bargoer2: It's wet and I'm cold.
Bargoer1: The smelters wouldn't say that.
Bargoer2: Hell no - Smelters play in any weather!
Bargoer1: Come rain or come shine, it's always Smelter Weather.
<<set $bargoers_done to 1>>
===
title: Bargoers_A2D3
tags: 
colorID: 3
position: -625,2039
---
Bargoer2: Sunny again. Thank goodness.
Bargoer1: ...
Bargoer2: What's got you down?
Bargoer1: Ah it's nothing.
Bargoer2: Hey uh...
Bargoer2: Go Smelters, right?
Bargoer1: Yeah, go Smelters!
<<set $bargoers_done to 1>>
===
title: Bargoers_A2D4
tags: 
colorID: 3
position: -389,2030
---
Bargoer2: You going to harfest?
Bargoer1: Naw, gonna watch the game.
Bargoer2: Is there a game tonight?
Bargoer1: Well no, but I was going to watch my fav highlights.
Bargoer2: Why don't you come on out, might be good.
Bargoer2: Stretch your legs. See the parade.
Bargoer1: ...
Bargoer2: Ever seen the game live?
Bargoer1: ...
Bargoer1: Naw, never.
Bargoer2: Come on out to Harfest. You never know.
Bargoer1: ...
Bargoer1: Yeah, alright!
Bargoer2: Yeah! Go Smelters!
Bargoer1: Gooooo Smelters!
<<set $bargoers_done to 1>>
===
title: Bargoers_A3D1
tags: 
colorID: 4
position: -1099,2281
---
Bargoer2: It's wet again.
Bargoer2: and I'm cold again.
Bargoer1: It's just rain.
Bargoer2: Hey uh...
Bargoer2: Why are we still standing out here?
Bargoer1: Someone has to.
<<set $bargoers_done to 1>>
===
title: Bargoers_A3D2
tags: 
colorID: 4
position: -861,2288
---
Bargoer2: Hey - you never told me why you were feeling down the other day...
Bargoer1: Do I gotta tell you everything?
Bargoer2: Naw, naw. Just asking is all.
Bargoer1: Well, ask someone else.
Bargoer2: ...
Bargoer1: ...
<<set $bargoers_done to 1>>
===
title: Bargoers_A3D3
tags: 
colorID: 4
position: -627,2277
---
Bargoer2: So I says to her - "Mabel, get off the dang table!"
Bargoer1: Hahaha
Bargoer2: And she goes "Why don't you just climb up here with me."
Bargoer1: No way.
Bargoer2: It's true. We both danced on that table. Nearly broke the damn thing.
Bargoer1: ...
Bargoer1: Somehow I can't see it.
Bargoer2: Well. You had to be there.
Bargoer1: I really didn't.
<<set $bargoers_done to 1>>
===
title: Bargoers_A3D4
tags: 
colorID: 4
position: -394,2264
---
Bargoer2: Well that's it, shipping out tomorrow.
Bargoer1: Can't believe you actually landed the new job.
Bargoer2: Yep. Moving onto better things.
Bargoer1: ...
Bargoer1: The kids excited?
Bargoer2: Oh yeah. They'll love it. Might take a while.
Bargoer1: Heh.
Bargoer2: Well...
Bargoer1: Yep.
Bargoer2: Yep.
Bargoer1: ...
Bargoer2: ...
Bargoer2: Maybe just one more, huh.
Bargoer1: Yeah. One more.
<<wait 1>>
<<runNextLinesTogether 2>>
Bargoer1: [shake=.05]Goooo Smelters![/shake]
Bargoer2: [shake=.05]Goooo Smelters![/shake]
<<wait .5>>
Bargoer2: Yep.
<<set $bargoers_done to 1>>
===
title: Bargoers_A4D3
tags: 
colorID: 6
position: -1098,2542
---
<<flip Bargoer1 -1>>
Bargoer1: ...
Bargoer1: Who are you?
Mae: Haven't you seen me around?
Bargoer1: Naw.
Mae: Well I've noticed you.
Bargoer1: That's not weird or anything.
Mae: Bit cold for standing outside isn't it?
Bargoer1: Someone's gotta stand here.
Bargoer1: ...
Bargoer1: Hey, you Smelters fan?
-> Uh... yeah
    Bargoer1: Oh really?
    Mae: Haha. Y-yeah!
    Bargoer1: Go Smelters?
    Mae: Go Smelters. :)
    Bargoer1: Go Smelters.
    Mae: :|
-> Umm... naw
    Bargoer1: Ah. Well.
    Bargoer1: ...
<<set $bargoers_done to 1>>
===
title: CeeJay
tags: 
colorID: 0
position: 1789,1861
---
Mae: hey... CJ?
CeeJay: yeah?
Mae: you were like... in high school when I was in middle school.
CeeJay: oh. Huh.
CeeJay: did i know you?
Mae: you used to play nerd games with Tom Bramwell
Mae: across the street from my house.
CeeJay: ha ha. Nerd games.
CeeJay: that’s quite a memory you got there
Mae: do you live here anymore?
CeeJay: nah, just back visiting.
Mae: for nerd games?
CeeJay: they were like pen and paper role playing stuff
Mae: cool.
CeeJay: but no, i’m just visiting family.
CeeJay: husband’s driving down tomorrow.
Mae: what do you do now?
CeeJay: lawyer
Mae: oh wow.
Mae: can i ask you some law stuff?
CeeJay: well I’m not acting in an professional capacity
CeeJay: but yeah sure.
	->what’s the best way to rob a bank?
		CeeJay: that’s not really legal advice
		Mae: legal is like laws, right?
		CeeJay: uh
        Mae: and bank robbing is a crime, right?
		Mae: and crimes are against the law?
		Mae: it’s all related.
		CeeJay: i guess just... Ya know... bandit mask and a gun?
		Mae: eh. It’s been done.
	->what’s the best way to get out of jail?
		CeeJay: well, it depends on the situation, you see-
		Mae: like would you dig through the wall or like
		Mae: attack a guard and take their pants and identity
		Mae: or like i dunno... just cause a riot or something?
		CeeJay: huh.
		Mae: what’s the *best* prison gang?
		Mae: like if you *HAD* to pick?
CeeJay: you know, we don’t really learn this kinda stuff in law school.
Mae: what is the point of like going to college if you don’t learn useful shit?
CeeJay: laws are pretty useful.
Mae: useful my ass, CJ!
<<wait 2>>
Mae: sorry Welcome back Have a nice visit bye
CeeJay: uh. seeya.
<<set $cee_jay_done to 1>>
===
