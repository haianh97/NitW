title: IntroPastor
tags: 
colorID: 0
position: -1326,62
---
<<if $demo2 and $act is 2 and $day is 1>>
    [[PastorDemo2]]
<<elseif $act is 4 and $day is 3>>
    Mae: Hey!
    Pastor: hey! you're candy borowski's daughter, right?
    Mae: sure. who are you?
    Pastor: Pastor Kate.
    Mae: oh right. my mom's boss.
    Pastor: ha ha. the church board is your mom's boss.
    Pastor: mine too.
    Mae: i didn't know that was a thing.
    Pastor: it's a thing.
    Mae: you're new here, right?
    Pastor: 16 months? something like that.
    Mae: oh. new to me at least.
    Pastor: you should stop by the church sometime?
    Mae: my mom would be happy if i did.
    Pastor: I know. 
    Pastor: I'm just adding extra guilt.
    Mae: ha ha ha thanks
    Pastor: yep yep.
    Pastor: we do our best
    Pastor: with what we have
    Pastor: when we have it.
	<<set $met_pastor to 1>>
    <<set $pastor_done to 1>>
<<elseif $day is 1>>
	// then bruce + pastor are together
	Mae: hey!
    Pastor: hey!
    Pastor: you're candy borowski's daughter, right?
    Mae: yep. who are you?
    Pastor: I'm Kate, but you can call me Pastor K.
    Mae: that's like... way more syllables.
    Mae: you're new here, right?
    Pastor: 16 months? something like that.
    Mae: oh. new to me at least.
    Pastor: this over here is Bruce.
    Mae: hey
    Bruce: hey
    Pastor: Bruce is new to our community!
    Mae: Do you live in the woods?
    Bruce: ...
    Bruce: yup
    Pastor: But not for long! We’re working on fixing that before winter gets here.
    Pastor: gonna take some doing, but God’s a do-er
    Pastor: and they're bigger than city council.
    Bruce: ... still thinks it’s a lot of trouble to go to...
    Pastor: Listen, nobody said loving your neighbor would be easy.
    Pastor: and you are our actual neighbor.
    Bruce: I appreciate it.
    Pastor: We all need a little help sometimes, Bruce.
    Pastor: No shame in hard times.
    Mae: Well, it was nice meeting you both!
    Pastor: Come up and see me at the church sometime!
    Mae: I will! I’ll be up to visit my mom anyway.
    Mae: See you later Bruce!
    Bruce: ok. Nice to meet you Mary
    Mae: Mae. Mae Borowski.
    Bruce: Bruce. Just Bruce.
    Pastor: Pastor. Pastor Karen.
    <<unlockDoodle BruceSketch>>
    <<set $knows_bruce_situation to 1>>
	<<set $met_pastor to 1>>
	<<set $met_bruce to 1>>
    <<set $pastor_done to 1>>
    <<set $bruce_done to 1>>
<<elseif $act is 2>>
	Mae: Hello.
    Pastor: Hello.
    Pastor: You're Candy's daughter, right?
    Mae: Yep. Who are you?
    Pastor: Kate. But most people call me Pastor K.
    Mae: you're new here, right?
    Pastor: 16 months? something like that.
    Mae: oh. new to me at least.
    Mae: Hello.
    Pastor: Hello.
    Mae: Hello.
    Pastor: do me a favor.
    Pastor: take that last hello
    Pastor: and give it to Bruce.
	<<if $met_bruce>>
		Mae: Oh yeah! I met that guy.
        Pastor: oh good!
        Pastor: Bruce is new to our community.
        Mae: looks like he's been in the woods a while
        Pastor: only a few weeks
        Pastor: but being in the woods isn't really the same
        Pastor: as being in the community.
        Mae: true.
        Mae: is he cold at night?
        Pastor: ha ha. working on it.
	<<else>>
		Mae: Bruce?
        Pastor: Bruce lives across the street.
        Mae: In the woods?
        Pastor: for now, at least.
        Mae: You want me to go talk to a strange man in the woods?
        Pastor: Bruce isn't strange. He's my friend.
        Pastor: and he's a part of this community.
        Mae: okey doke.
        Pastor: thank you.
        Pastor: it was nice to meet you!
        Mae: you too!
	<<endif>>
	<<set $met_pastor to 1>>
<<elseif $act is 3>>
	<<if $day is 3>>
		Pastor: Hello Mae.
        Mae: whoa. uh.
        Pastor: ha ha.
        Pastor: I've seen your picture on your mom's desk.
        Pastor: I'm kate. Pastor K works too.
        Mae: you're new here, right?
        Pastor: 16 months? something like that.
        Mae: oh. new to me at least.
        Pastor: she'll be happy you came by.
        Mae: oh geez don't tell her
        Mae: it'll make, like, expectations.
        Pastor: well we can't have that.
        Mae: ...
        Mae: you seem sad.
        Pastor: oh do i?
		Mae: yeah. what are you sad about?
		Pastor: Oh nothing.
        Mae: unconvincing.
        Pastor: i was never a good liar.
        Pastor: but i'll survive.
        Pastor: hey, have you met our neighbor Bruce?
		<<if $met_bruce>>
			Mae: yeah!
            Mae: guy across the street?
            Pastor: that's him.
            Mae: he's cool.
            Mae: is he cold at night?
            Pastor: i expect he is.
            Pastor: but we're doing what we can.
            Mae: well, see ya!
            Pastor: see you, Mae.
		<<else>>
			Mae: I don't think so.
			Pastor: you should say hi.
            Pastor: he lives across the street.
            Mae: in the woods?
            Pastor: yeah. he lives... in the woods.
            Mae: there's that sad again.
            Pastor: anyway, go say hi.
            Pastor: he's a guy who needs more people saying hi to him.
            Mae: ok.
            Pastor: thanks, mae.
		<<endif>>
		<<set $met_pastor to 1>>
	<<elseif $day is 4>>
		Pastor: Oh hey, how's it going?
		Pastor: I'm Pastor Kate.
        Mae: you're new here, right?
        Pastor: 16 months? something like that.
        Mae: oh. new to me at least.
        Pastor: you're candy's borowski's daughter, right?
        Mae: yeah.
		Mae: you alright?
		Pastor: I'm fine.
        Mae: you don't look fine.
        Pastor: looking for a friend
        Pastor: who seems to have left.
        Mae: oh. i'm sorry.
        Pastor: it's ok.
        Pastor: i'll wait a bit longer.
        Pastor: waiting on someone to show up
        Pastor: is a big part of my job.
        Pastor: see you, Mae.
        Mae: seeya
		<<set $met_pastor to 1>>
	<<endif>>
<<endif>>
<<unlockDoodle PastorK>>
===
title: IntroBruce
tags: 
colorID: 0
position: 768,254
---
Mae: Hello. I'm Mae Borowski!
<<if $met_pastor>>
	Mae: uh... friend of Pastor Kate?
<<endif>>
Bruce: Hey. I'm Bruce.
Bruce: ...
Bruce: I don't say much.
Mae: that's fine.
Mae: i talk a lot.
Bruce: ...
Mae: but I can like... not.
<<if $act is 2 and $day is 3>>
    Bruce: Seen a fire through the trees last night.
    Bruce: past these woods here.
    Bruce: Weird noises.
    Bruce: I just kept to myself.
    Bruce: and they went away.
        ->That sounds spooky.
            Bruce: Been around a lotta places.
            Bruce: Ain't spooked by much.
            Bruce: But out past that water tower...
        ->Where?
            Bruce: right over there
            Bruce: past the water tower.
            Mae: oh ok.
    Mae: yeah that's a place where teens hang out.
    Bruce: let's hope that's all it was.
    Bruce: in my experience
    Bruce: strange fire and strange voices
    Bruce: ain't never been good.
    Mae: I wonder if they'll be back...?
    Bruce: Hope not.
<<endif>>
<<unlockDoodle BruceSketch>>
<<set $met_bruce to 1>>
<<set $bruce_done to 1>>
===
title: Pastor
tags: 
colorID: 0
position: -369,285
---
<<if $met_pastor is 0>>
    [[IntroPastor]]
<<elseif $act is 2 and $day is 2>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA2D2]]
    <<endif>>
<<elseif $act is 2 and $day is 3>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA2D3]]
    <<endif>>
<<elseif $act is 2 and $day is 4>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA2D4]]
    <<endif>>
<<elseif $act is 3 and $day is 1>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA3D1]]
    <<endif>>
<<elseif $act is 3 and $day is 2>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA3D2]]
    <<endif>>
<<elseif $act is 3 and $day is 3>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA3D3]]
    <<endif>>
<<elseif $act is 3 and $day is 4>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA3D4]]
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    <<if $pastor is 0>>
        <<set $pastor += 1>>
        <<set $pastor_done to 1>>
        [[PastorA4D3]]
    <<endif>>
<<endif>>
===
title: Bruce
tags: 
colorID: 0
position: 1201,158
---
<<if $met_bruce is 0>>
    [[IntroBruce]]
<<elseif $act is 2 and $day is 2>>
    <<set $bruce_done to 1>>
    [[BruceA2D2]]
<<elseif $act is 2 and $day is 3>>
    <<set $bruce_done to 1>>
    [[BruceA2D3]]
<<elseif $act is 2 and $day is 4>>
    <<set $bruce_done to 1>>
    [[BruceA2D4]]
<<elseif $act is 3 and $day is 2>>
    <<set $bruce_done to 1>>
    [[BruceA3D2]]
<<elseif $act is 3 and $day is 3>>
    <<set $bruce_done to 1>>
    <<if $knows_bruce_situation>>
        [[BruceA3D3.Leaving]]
    <<else>>
        [[BruceA3D3.NotTalkingMuch]]
    <<endif>>
<<endif>>
===
title: BruceA3D3.Leaving
tags: 
colorID: 4
position: 1711,513
---
Bruce: Hey. Can you do me a favor?
Mae: sure.
Bruce: Can you tell Pastor Kate something for me?
Mae: sure.
Bruce: but don't tell her this til tomorrow.
Bruce: can you do that?
Mae: sure.
Bruce: Tell her I said thank you.
Bruce: and I'm sorry I caused her trouble.
Bruce: but I'm not worth throwing away her job.
Bruce: she's the pastor after all.
Bruce: folks need 'er.
    ->why am i telling her this?
        Bruce: cuz I won't be there to do it.
    ->are you... leaving?
        Bruce: yep.
Bruce: gonna hop a train tonight
Bruce: headed east.
Mae: where will you go?
Bruce: I'm going home.
Mae: wow, really?
Bruce: never gonna touch a bottle again.
Bruce: never gonna lift a hand to no man again.
Bruce: sit on my daughter's front porch
Bruce: watch my grandbabies play in the yard
Bruce: they'll all be there
Bruce: gonna get off that train
Bruce: my kids are gonna be there
Bruce: they'll have a sign they made
Bruce: nothing fancy, poster board and markers
Bruce: "welcome home dad".
Bruce: and i'll be home.
Mae: that sounds great, Bruce!
Bruce: yep yep.
<<if $bruce_familiarity >= 2>>
    Bruce: Thanks for talking to me. Was nice to get to know you.
    Mae: You too, Bruce.
    Mae: gonna miss you.
    Bruce: we'll all meet again.
    Mae: i hope so.
    Bruce: hope's important.
    Bruce: it's the last thing you got.
<<endif>>
Mae: you take care of yourself.
Bruce: you too, Mae.
Bruce: i'll be fine, you know.
Bruce: i'm an old hand at this.
Bruce: maybe older than i shoulda ever got to
Bruce: but i'll be fine.
Mae: see you around, Bruce.
Bruce: goodbye, Mae.
<<set $knows_bruce_leaving to 1>>
===
title: BruceA3D3.NotTalkingMuch
tags: 
colorID: 4
position: 1830,243
---
Bruce: ...
Mae: what's up?
Bruce: nothin'
Mae: cool.
Bruce: yep.
===
title: PastorA3D4
tags: question!
colorID: 4
position: 97,552
---
//question: need a variable that lets mae apologize if you did the godcatconvo. here and a4d3. not sure how to set that up//

Pastor: Wonder where Bruce has got to?
<<if $knows_bruce_leaving>>
    Mae: Actually, I know.
    Pastor: really?
    Mae: yeah. he told me not to tell you yesterday...
    Pastor: tell me what, mae?
    Mae: Bruce is going home!
    Pastor: he... what?
    Mae: yeah. he's going home to his kids
    Mae: i guess they're all waiting for him
    Pastor: oh.
    Mae: he seemed pretty happy about it.
    Pastor: ok.
    Pastor: thanks, mae.
    <<wait 3>>
    Mae: are you not happy about this?
    Pastor: ...
    Pastor: thanks for letting me know.
    <<set $told_pastor_bruce_leaving to 1>>
<<else>>
    <<if $met_bruce is 0>>
        Mae: Bruce?
        Pastor: He's a dear man.
        Pastor: Looks like you might have missed him.
        Pastor: ...
    <<else>>
        Mae: Beats me.
        Mae: He's kinda transient though, right?
        Mae: I mean he wasn't like renting the woods.
        Pastor: no... no he wasn't.
        <<wait 2>>
        Pastor: god damn this world.
    <<endif>>
<<endif>>
<<set $pastor_done to 1>>
===
title: PastorA4D3
tags: question!
colorID: 6
position: 337,388
---
Mae: Hey.
Pastor: Well hey there, Mae.
Mae: what're you doing?
Pastor: oh, just cleanin' up.
<<if $told_pastor_bruce_leaving>>
    Mae: how did the council vote go?
    Pastor: we lost.
    Pastor: i lost.
    Pastor: it was just me pushing for it, really.
    Pastor: we've got all this
    Pastor: empty space
    Pastor: could use it for so many things
    Pastor: but
    Pastor: ...
    Mae: you tried.
    Pastor: all we can do.
    Pastor: and god will show up.
    Pastor: when the time is right.
    <<if $bruce_familiarity >= 2>>
        Mae: Bruce was my friend.
        Pastor: Mine too. Bruce was my friend too.
    <<endif>>
<<elseif $knows_bruce_leaving>>
    Pastor: wonder where bruce got to?
    Mae: Actually, I know.
    Pastor: really?
    Mae: yeah. he told me not to tell you yesterday...
    Pastor: tell me what, mae?
    Mae: Bruce is going home!
    Pastor: he... what?
    Mae: yeah. he's going home to his kids
    Mae: i guess they're all waiting for him
    Pastor: oh.
    Mae: he seemed pretty happy about it.
    Pastor: ok.
    Pastor: thanks, mae.
    <<wait 3>>
    Mae: are you not happy about this?
    Pastor: ...
    Pastor: thanks for letting me know.
    <<if $bruce_familiarity >= 2>>
        Mae: Bruce was my friend.
        Pastor: Mine too. Bruce was my friend too.
    <<endif>>
<<elseif $knows_bruce_situation>>
    Mae: Do you know where Bruce went?
    Pastor: I don't know, Mae.
    Pastor: wish i did.
    <<if $bruce_familiarity >= 2>>
        Mae: I hope he's gonna be okay, he was my friend.
        Pastor: He's mine too.
    <<endif>>
<<elseif $met_bruce is 0>>
    Mae: what was out here?
    Pastor: A man named Bruce was living here.
    Mae: Bruce?
    Pastor: He's a dear man.
    Pastor: Looks like you might have missed him.
    Pastor: ... 
<<else>>
    Mae: So... Bruce is gone?
    Pastor: Yeah.
    Pastor: Sure Wish I knew where he got to.
    Mae: does he have a home somewhere?
    Pastor: No. I don't know. Not really.
    Mae: oh.
    Pastor: ...
<<endif>>
Mae: you need some help cleaning this place up?
Pastor: that'd be nice, Mae.
Pastor: there's a rake back in a shed behind the church
Mae: oh, i can't do it *now*, but
Pastor: ha ha ha.
Pastor: run along, mae.
<<set $pastor_done to 1>>
===
title: PastorA2D2
tags: 
colorID: 3
position: -1309,394
---
Mae: Hey again.
Pastor: Hey.
Pastor: Your mom will be pretty happy you're here.
Mae: oh don't I know it.
Pastor: you not a churchgoer?
    ->no
        Pastor: big fan of sleeping in?
        Mae: i mean aren't you?
        Pastor: i can't remember the last time i slept in.
        Pastor: might have been last saturday.
        Mae: that's not long ago.
        Pastor: it's been a very long week.
        Mae: same, honestly.
    ->i was when i was a kid.
        Pastor: well, you *had* to go then.
        Mae: yeah.
        Mae: i liked sunday school.
        Mae: they'd give you cookies and orange drink.
        Pastor: is "orange drink" really the only name for it?
        Mae: huh. I don't know.
        Pastor: I'm realizing I've also been calling it that my entire life.
        Pastor: "orange drink"
        Mae: the drink that is orange.
Pastor: when did you stop coming?
Mae: when my mom stopped making me.
Mae: when i was like 15 or so.
Pastor: why?
Mae: i dunno.
Mae: all the sitting and standing
Mae: sing this now, feel this thing now,
Pastor: well the feelings are more suggestions...
Mae: doesn't feel like it.
Pastor: well, I hope you'll maybe come some sunday?
Mae: yeah, I guess I haven't seen you preach.
Pastor: oh wow. "preach"
Pastor: kind of has a bad ring to it, huh?
Mae: "talk?"
Pastor: that works.
<<if $knows_bruce_situation>>
    Mae: Hey, how are things with the whole Bruce situation?
    Pastor: weeeeelll...
    Pastor: we're praying. and gears are turning.
    Pastor: but they turn slow.
    Pastor: but god can turn any gear they want
    Pastor: and i take comfort in that.
    Mae: i hope it turns out ok!
    Pastor: thank you, Mae.
<<endif>>

<<if $pastor_god_talk is 0>>
    [[PastorGodTalk]]
<<endif>>
===
title: PastorA2D3
tags: 
colorID: 3
position: -1091,577
---
Pastor: How's things?
Mae: ok. weird dreams.
Pastor: want to talk about it?
Mae: not really.
Pastor: i dreamed i was trapped in a wall
Mae: geez
Pastor: no, it was fine.
Pastor: i knew the rescue crew was coming in a few hours
Pastor: and it was cozy
Pastor: so i could take a nap, guilt-free.
Mae: you feel guilty about naps?
Pastor: wish I didn't.
<<if $knows_bruce_situation>>
    Mae: How's that whole thing with Bruce going?
    Pastor: I'm hopeful and positive
    Mae: that sounds like what someone says
    Mae: when they're not.
    Pastor: ha ha
    Pastor: it's fine, mae. it's going fine.
<<endif>>

<<if $pastor_god_talk is 0>>
    [[PastorGodTalk]]
<<endif>>
===
title: PastorA2D4
tags: 
colorID: 3
position: -873,655
---
Pastor: Howdy.
Mae: you waiting for someone?
Pastor: well, i think i'm being stood up.
Mae: a... date?
Pastor: not the kind you mean.
Pastor: honestly a big part of my job
Pastor: is someone calling
Pastor: me heading out
Pastor: and them not showing up.
Mae: you were meeting them at
Mae: the statue of... saint...
Pastor: Rubello.
Mae: yeah. the firebreathing guy
Mae: who like, ate people.
Pastor: that's a bit of an oversimplification
Mae: ...
Pastor: but yeah. basically.
Mae: cool guy to have a statue of
Mae: outside the church I mean.
Pastor: well, i didn't put it here
Pastor: but can't we learn something from Saint Rubello?
    ->breathe fire
    ->eat people
Pastor: sure.
Pastor: this statue is part of our "encourage criminal acts" outreach
Mae: it's working!
<<if $knows_bruce_situation>>
    Mae: How's the situation?
    Pastor: the situation?
    Mae: with Bruce and stuff.
    Pastor: eh.
    Pastor: we wait and see.
    Pastor: and pray.
<<endif>>

<<if $pastor_god_talk is 0>>
    [[PastorGodTalk]]
<<endif>>
===
title: PastorA3D2
tags: 
colorID: 4
position: -428,728
---
//pastor is gone this day//

Pastor: I'm gone today! This is an error!

<<if $knows_bruce_situation>>
    Mae: Bruce situation A3D2?
    Pastor: Eh.
<<endif>>

<<if $pastor_god_talk is 0>>
    [[PastorGodTalk]]
<<endif>>
===
title: PastorA3D3
tags: 
colorID: 4
position: -179,659
---
Pastor: Ugh. Stressful day.
Pastor: some days you need grace more than others.
<<if $knows_bruce_situation>>
    Mae: Was this all about Bruce?
    Pastor: It was.
    Pastor: is.
    Pastor: whatever. It's out of our hands now.
    Pastor: but there are better hands than mine.
    Mae: hm.
<<endif>>
Pastor: Anyway, how are you Mae?
Mae: Eh.
Mae: Eeeeeeeeeeeeeeeeh.
Pastor: sounds serious.
Mae: I guess.

<<if $pastor_god_talk and $pastor_cat_god_talk is 0>>
    [[PastorCatGodTalk]]
<<endif>>
===
title: PastorGodTalk
tags: temp
colorID: 0
position: -1274,997
---
Pastor: Hey listen
Pastor: You know
Pastor: If you ever want to talk or have any questions
Mae: about, like, god?
Pastor: sure.
Pastor: you can always talk to me.
Pastor: it's kind of in my job description.
Mae: ok.
<<set $pastor_god_talk to 1>>
===
title: PastorCatGodTalk
tags: 
colorID: 4
position: -99,965
---
Mae: so like
Mae: things have been rough lately
Pastor: i can see that.
Pastor: what seems to be the trouble?
Mae: i think i met god
Pastor: what?
    ->it was a dream
        Mae: but god talks to us that way, right?
        Pastor: sure.
    ->i don't think it was a dream
        Pastor: oh wow.
        Mae: that happens, right?
        Mae: or it like, happened.
        Pastor: sure.
Pastor: can you tell me about it?
    ->i was in this desert
        Mae: as far as i could see
        Mae: where like
        Mae: all that was left of the world
        Mae: was bits of wood
        Mae: and sand
    ->it was like the shore
        Mae: of this giant black ocean
        Mae: i could see like
        Mae: forever into the sky
        Mae: and below it was just
        Mae: sand
        Mae: as far as i could see
Mae: i was wandering through it but
Mae: i was like, drawn to something
Mae: and i came up over this...
Mae: what do you call a hill made of sand?
Pastor: A dune.
Mae: yeah. A Dune.
Mae: and there was god.
Mae: just sitting there. up on another hill.
Mae: or dune. whatever.
Pastor: just hangin' out?
Mae: yeah.
Mae: seemed like they were *really* into just hanging out.
Pastor: did they say anything
Mae: yeah
    ->they like, didn't care
        Pastor: they didn't care if you were there?
        Mae: no, i mean, they didn't care.
        Mae: in general.
    ->they were kind of a... jerk
        Mae: i mean, more of a straight up asshole.
        Mae: sorry. can i say that in a church?
        Pastor: ha ha.
        Pastor: i think the structure can handle the shock.
Mae: they went on and on about how annoyed they were
Mae: that people kept coming to them with questions
Mae: and annoyed that anyone thought they were god to begin with
Pastor: well, i don't think that was god then
Mae: why?
Pastor: if something tells you it isn't god
Pastor: you should probably believe it.
Mae: no, what i mean is that
Mae: the thing we call god
Mae: isn't god
Mae: it's just a thing
Mae: and it doesn't care
Pastor: hm.
Mae: they kept talking about
    ->how nothing matters
        Mae: like the universe is ending
        Mae: and everything is being forgotten.
    ->this giant hole
        Mae: in the center of everything.
        Mae: growing.
Pastor: that sounds very lonely.
Pastor: have you been feeling lonely lately?
    ->yes.
        Mae: i had this problem when i was in high school
        Mae: and i did something really bad.
        Mae: you probably heard about it.
        Pastor: your mom has mentioned it.
        Mae: yeah i figured.
        Mae: well, since then
    ->no.
        Mae: no, i mean i've got friends
        Mae: i've been with them every day since i got home.
        Mae: but... hm...
Mae: there's this deep down loneliness
Mae: been there for years
Mae: since... well, just been a long time.
Pastor: i've been there.
Pastor: i mean i'm there all the time
Pastor: i think that's where
Pastor: if we open ourselves up
Pastor: god can enter our lives most effectively
    ->i guess
        Pastor: sometimes that's the best you've got.
        Mae: yeah.
        Mae: ...
        Pastor: i feel like that's maybe not a good enough answer for you
        Mae: is that a good enough answer for anyone?
    ->i don't think i believe in god anymore
        Pastor: if you want the truth
        Pastor: i think everyone doesn't believe in god
        Pastor: for at least a few minutes a day
        Mae: hm.
Pastor: faith is a process
Pastor: you have to keep getting up and choosing to go on
Mae: do you really think there's a god?
Mae: like, literally someone up there listening?
Mae: is that something you completely believe?
Pastor: hm.
Pastor: i don't know.
Mae: oh.
Pastor: on my best days I think I do.
Pastor: but there are times where I don't.
Mae: but... but you stand up there every sunday
Mae: and tell people you do!
Mae: and even *YOU'RE* not sure?
Mae: It's your *JOB* to believe.
Pastor: well, I think it's more my job to serve others...
Mae: but you're lying to people!
Pastor: how?
Mae: you stand up there and tell people to believe
Mae: in something you don't 100% believe in yourself.
Pastor: maybe you're right Mae
Pastor: and maybe I should get up there every week
Pastor: and give some sort of tally of how much I believed that week
Pastor: but who would that help?
Mae: but you're a pastor
Mae: and you can't tell me whether or not god is up there.
Mae: whether anyone is watching
Mae: anyone who gives a shit.
Pastor: no, but-
Mae: THEN WHAT IS THE POINT OF YOU?
Pastor: ...
Mae: ...
Mae: I have to go.
Pastor: ok.
<<set $pastor_cat_god_talk to 1>>
<<set $pastor_done to 1>>
===
title: BruceA2D2
tags: 
colorID: 3
position: 769,473
---
Mae: so what's your story?
Bruce: uh. got no story.
Mae: like, where are you from?
Bruce: north of here. northeast.
Bruce: ...
Bruce: worked on the railroads
Bruce: worked some mines
Bruce: worked some construction.
Mae: how'd you end up here?
Bruce: string 'a bad luck
Bruce: ...
Mae: don't have to talk about it.
Bruce: i never turn down that offer.
<<set $bruce_familiarity += 1>>
===
title: BruceA2D3
tags: 
colorID: 3
position: 989,528
---
Bruce: Seen a fire through the trees last night.
Bruce: past these woods here.
Bruce: Weird noises.
Bruce: I just kept to myself.
Bruce: and they went away.
    ->That sounds spooky.
        Bruce: Been around a lotta places.
        Bruce: Ain't spooked by much.
        Bruce: But out past that water tower...
    ->Where?
        Bruce: right over there
        Bruce: past the water tower.
        Mae: oh ok.
Mae: yeah that's a place where teens hang out.
Bruce: let's hope that's all it was.
Bruce: in my experience
Bruce: strange fire and strange voices
Bruce: ain't never been good.
Mae: I wonder if they'll be back...?
Bruce: Hope not.
<<set $bruce_familiarity += 1>>
===
title: BruceA3D2
tags: 
colorID: 4
position: 1471,535
---
Mae: getting kinda cold, huh?
Bruce: yeah, nip in the air at night.
Mae: i like fall when it gets cold.
Bruce: me too. real pretty out a'swell.
Mae: i used to cry at fall trees when i was a kid.
Bruce: why?
Mae: because they were so pretty.
Bruce: hm. i did that when my daughter was born.
Mae: you have a daughter?
Bruce: yep.
Mae: where is she?
Bruce: ...
Mae: sorry.
Bruce: s'fine.
<<set $bruce_familiarity += 1>>
===
title: PastorInitLevel
tags: 
colorID: 0
position: -389,-165
---
<<if $demo2 and $act is 2 and $day is 1 and $TowneCentreWest>>
    <<show Pastor>>
<<elseif $act is 2 and $day is 1 and $ChurchHill>>
    <<show Pastor>>
<<elseif $act is 2 and $day is 2 and $ChurchSanctuary>>
    <<if $pastor_done>>
        <<hide Pastor>>
    <<else>>
        <<show Pastor>>
        <<warp Pastor PastorCouncil>>
        <<flip Pastor -1>>
    <<endif>>
<<elseif $act is 2 and $day is 3 and $ChurchSanctuary>>
    <<if $pastor_done is 1>>
        <<hide Pastor>>
    <<else>>
        <<warp Pastor PastorCouncil>>
        <<flip Pastor -1>>
        <<show Pastor>>
    <<endif>>
<<elseif $act is 2 and $day is 4 and $ChurchHill>>
    <<show Pastor>>
    <<warp Pastor PastorStatue>>
    <<sit Pastor>>
    <<flip Pastor -1>>
    <<setSpriteLayer Pastor BG1>>
<<elseif $act is 3 and $day is 1 and $ChurchHill>>
    <<show Pastor>>
<<elseif $act is 3 and $day is 3 and $ChurchSanctuary>>
    <<if $pastor_done is 1>>
        <<hide Pastor>>
    <<else>>
        <<warp Pastor PastorCouncil>>
        <<flip Pastor -1>>
        <<show Pastor>>
    <<endif>>
<<elseif $act is 3 and $day is 4 and $ChurchHill>>
    <<show Pastor>>
<<elseif $act is 4 and $day is 1 and $ChurchSanctuary>>
    <<show Pastor>>
<<elseif $act is 4 and $day is 3 and $ChurchHill>>
    <<show Pastor>>
<<else>>
    <<hide Pastor>>
<<endif>>

<<if $act is 2 and $day is 2 and $church_hill_background_a2d2_done is 0 and $ChurchHill>>
    <<show Council1>>
    <<show Council2>>
    <<show Council3>>
    <<show Council4>>
    <<flip Council1 -1>>
    <<flip Council2 -1>>
    <<flip Council3 -1>>
    <<flip Council4 -1>>
<<elseif $act is 2 and $day is 3 and $ChurchSanctuary>>
    <<if $council_a2d3_done is 0>>
        <<show Council1>>
        <<show Council2>>
        <<show Council3>>
        <<show Council4>>
    <<else>>
        <<hide Council1>>
        <<hide Council2>>
        <<hide Council3>>
        <<hide Council4>>
    <<endif>>
<<elseif $act is 3 and $day is 3 and $ChurchSanctuary>>
    <<if $council_a3d3_done is 0>>
        <<show Council1>>
        <<show Council2>>
        <<show Council3>>
        <<show Council4>>
    <<else>>
        <<hide Council1>>
        <<hide Council2>>
        <<hide Council3>>
        <<hide Council4>>
    <<endif>>
<<else>>
    <<hide Council1>>
    <<hide Council2>>
    <<hide Council3>>
    <<hide Council4>>
<<endif>>
===
title: BruceInitLevel
tags: 
colorID: 0
position: 1199,-133
---
<<if $ChurchHill>>
    <<if $act is 2>>
        <<if $day is 1>>
            <<show Bruce>>
            <<warp Bruce BruceOutsideShelter>>
            <<flip Bruce -1>>
            <<setCanInteract Bruce false>>
        <<elseif $day is 2>>
            <<show Bruce>>
            <<if $church_hill_background_a2d2_done is 1>>
                <<warp Bruce BruceCamp>>
                <<flip Bruce 1>>
            <<else>>
                <<warp Bruce BruceCamp>>
                <<flip Bruce 1>>
                <<setCanInteract Bruce false>>
            <<endif>>
        <<elseif $day is 3>>
            <<show Bruce>>
            <<warp Bruce BruceCliff>>
        <<elseif $day is 4>>
            <<show Bruce>>
            <<warp Bruce BruceOutsideShelter>>
            <<flip Bruce -1>>
        <<else>>
            <<hide Bruce>>
        <<endif>>
    <<elseif $act is 3>>
        <<if $day is 1>>
            <<show Bruce>>
            <<warp Bruce BruceOutsideShelter>>
            <<setCanInteract Bruce false>>
        <<elseif $day is 3>>
            <<show Bruce>>
            <<warp Bruce BruceShelter>>
        <<elseif $day is 4>>
            <<hide Bruce>>
        <<else>>
            <<show Bruce>>
        <<endif>>
    <<else>>
        <<hide Bruce>>
    <<endif>>
<<elseif $TowneCentreWest>>
    <<if $demo2>>
        <<show Bruce>>
    <<else>>
        <<hide Bruce>>
    <<endif>>
<<else>>
    <<hide Bruce>>
<<endif>>
===
title: BackgroundA2D1
tags: 
colorID: 3
position: 2348,-322
---
<<if $pastor_background_a2d1 is 0>>
    <<set $pastor_background_a2d1 to 1>>
    <<if $demo2>>
        [[BackgroundA2D1Demo2]]
    <<else>>
        Pastor: You warm enough at night?
        Bruce: Yeah, you know I get by.
        Pastor: Well, I'm working on something to help you out.
        Bruce: Aw, don't go to any trouble. I'm alright.
        Pastor: That's good.
        Bruce: Gonna get cold soon.
        Pastor: I can get you some blankets for now.
        Pastor: For some reason we have a *LOT* of blankets.
        Bruce: That'd sure be a blessing!
        Pastor: Well, we bless best with what we already have too much of anyway.
        Bruce: Heh heh.
        Pastor: And we got blankets!
        Bruce: Ha ha ha
        <<close>>
        <<wait 1>>
    <<endif>>
<<endif>>
<<if $demo2 is 0>>
    [[EmoticonLoopA2D1]]
<<endif>>
===
title: EmoticonLoopA2D1
tags: temp
colorID: 3
position: 2646,-322
---
Pastor: {emote=stars}
Bruce: {emote=money}
[[EmoticonLoopA2D1]]
===
title: CouncilA2D3
tags: 
colorID: 3
position: 2344,101
---
<<cameraLookAt PastorCouncilCamera 3>>
Council1: he's very nice.
Pastor: yeah, and he can be like, the first!
Pastor: a trial run.
Council2: where would you put him?
Pastor: oh in the basement,
Pastor: or in the old library?
Council3: with all the books?
Pastor: he's not going to eat them.
Council4: interesting proposal, Karen.
Council1: yes, thank you for bringing this to us.
Pastor: i look forward to us moving forward together on this.
Council2: it will be brought up at the town council meeting.
Pastor: oh!! that's wonderful!!
Council1: alright, troops! move out!
<<close>>
<<walk Council1 CouncilOffscreenLeft>>
<<walk Council2 CouncilOffscreenLeft>>
<<walk Council3 CouncilOffscreenLeft>>
<<walk Council4 CouncilOffscreenLeft wait>>
<<waitForMove Council3>>
<<waitForMove Council2>>
<<waitForMove Council1>>
<<hide Council1>>
<<hide Council2>>
<<hide Council3>>
<<hide Council4>>
<<cameraFollowPlayer 3>>
===
title: BackgroundA3D1
tags: 
colorID: 4
position: 2339,331
---
Bruce: I don't feel right about it.
Pastor: I know, but sometimes the right thing doesn't feel right.
Bruce: i just think... it's just wrong to take something
Bruce: when folks don't want you to take it.
Pastor: well...
Pastor: i feel like god would see this differently.
Pastor: i do at least.
Bruce: hmmm
Bruce: that's because you're a good person.
<<close>>
<<wait 1>>
[[EmoticonLoopA3D1]]
===
title: EmoticonLoopA3D1
tags: temp
colorID: 4
position: 2628,334
---
Pastor: {emote=spiral}
Bruce: {emote=hotdog}
[[EmoticonLoopA3D1]]
===
title: CouncilA3D3
tags: 
colorID: 4
position: 2345,562
---
<<cameraLookAt PastorCouncilCamera 3>>
Council2: ...because there will be children here!
Council3: and this is a historic church
Pastor: that's half empty!
Council3: that's your job to fix, not ours!
Pastor: the population drain in this region
Pastor: you have to know this place'll never be full again.
Pastor: we have the space, and we have people who need space.
Pastor: we have resources, and we have poverty!
Pastor: and not in some far off place or some big city!
Pastor: right here! right across the street!
Council1: Karen, it's our number one job right now
Council1: to bring businesses into town.
Council1: businesses mean jobs, and jobs means people.
Council1: we have to do what we can
Council1: to get the attention of the market.
Council1: and having a half-dead church stacked full of bums-
<<wait 3>>
Pastor: thanks for your time.
Council1: We'll see you for the vote tomorrow night.
Council2: thank you, karen.
Council4: ...
Council4: you have a good heart, karen.
Pastor: thank you all for coming.
<<close>>
<<walk Council1 OffscreenLeft>>
<<walk Council2 OffscreenLeft>>
<<walk Council3 OffscreenLeft>>
<<walk Council4 OffscreenLeft wait>>
<<hide Council1>>
<<hide Council2>>
<<hide Council3>>
<<hide Council4>>
<<cameraFollowPlayer 3>>
===
title: BruceA2D4
tags: 
colorID: 3
position: 1235,560
---
Mae: you gonna go down to Harfest tonight?
Bruce: dunno. maybe.
Bruce: keep to myself
Mae: you should say hi if you're there!
Mae: i'll be there.
Bruce: ok.
Bruce: we'll see i s'pose.
<<set $bruce_familiarity += 1>>
===
title: PastorA3D1
tags: 
colorID: 4
position: -645,674
---
Mae: Hey hey.
Pastor: hey again.
Bruce: hey.
Mae: so i had this really bad dream last night...
Pastor: me too.
Pastor: i dreamed i was a pizza
Pastor: and i was afraid someone would slice me
Mae: geez.
Pastor: yep
Mae: this just makes me sad we don't have a pizza place anymore
Pastor: gotta order it from out by the highway now.
Pastor: tragic.
Bruce: i had a dream i was a goose flying south
Bruce: and i had to keep up with the other gooses
Bruce: or i'd get lost.
Mae: that's sad.
Bruce: t'wernt a good dream.
Mae: what are y'all talking about?
Pastor: big meeting coming up
Pastor: city council
Bruce: yep.
Mae: about...
Pastor: about bruce here!
Bruce: ...
Mae: wow. that's intense.
Pastor: we'd appreciate your prayers
Pastor: this is precisely the kind of time
Pastor: where god shows up.
Bruce: hope so.
Mae: me too.
<<set $knows_bruce_situation to 1>>
===
title: BackgroundA2D1Demo2
tags: demo2
colorID: 3
position: 2528,-609
---
Bruce: Can we just stand out here for a while? #line:8543e6
Pastor: It’s just the Click Clack! #line:72c0e5
Pastor: It ain’t some fancy place. #line:042092
Pastor: You’ll be more than welcome. #line:051cad
Bruce: I know... I just... #line:e0c16f
Pastor: Fine. We can stand here as long as you like. #line:677065
Pastor: Or until 7:30. I have a prayer group to run. #line:bd65a4
===
title: PastorDemo2
tags: 
colorID: 3
position: -953,-50
---
<<if $pastor is 0>>
    Pastor: Hey, aren’t you Mae? #line:fdee90
    Mae: Yeah? #line:b314ed
    Pastor: You’re Candy’s daughter! #line:b8baec
    Mae: Yeah. #line:63f9a1
    Pastor: I’m Pastor Karen. Your mom works at our church. #line:e439f8
    Mae: Yep. She’s a professional reception-er. #line:d4d8c4
    Pastor: Have you met Bruce? #line:55de0e
    Mae: Hey Bruce. #line:7e043d
    Bruce: Hello. #line:90784a
    Pastor: Bruce has never been to the Click Clack! #line:c72083
    -> How is that possible? #line:c75aa6
    -> You are in for a treat. #line:f97210
        Pastor: See Bruce? #line:40bd42
        Mae: A greasy, greasy treat. #line:d66572
        Mae: and an angry waitress. #line:b8e79e
        Bruce: Sounds like my kinda place. #line:ae5fcf
    Pastor: Bruce is new in town so he’s never had the pleasure #line:7e41c7
    Mae: You live around here? #line:9ef439
    Bruce: um... #line:e3bf10
    Pastor: Bruce is our neighbor up at the church. #line:b0b9ed
    Bruce: ...I’m camped out across the street.... #line:7546e0
    Mae: oh. #line:b4e95e
    Bruce: in the woods. #line:546f7e
    Pastor: But not for long! We’re working on fixing that before winter gets here. #line:d4e95a
    Pastor: gonna take some doing, but God’s a do-er. #line:59209c
    Bruce: ... still thinks it’s a lot of trouble to go to... #line:2b4f8f
    Pastor: Listen, nobody said loving your neighbor would be easy. #line:91bc98
    Pastor: and you are our actual neighbor. #line:6248f3
    Bruce: I appreciate it. #line:d2a077
    Pastor: We all need a little help sometimes, Bruce. #line:4dc41e
    Pastor: No shame in hard times. #line:3a1ac8
    Mae: Well, it was nice meeting you both! #line:e40af4
    Pastor: Come up and see me at the church sometime! #line:bdf30a
    Mae: I will! I’ll be up to visit my mom anyway. #line:0abf8d
    Mae: See you later Bruce! #line:9c52a8
    Bruce: ok. Nice to meet you Mary #line:ce519f
    Mae: Mae. Mae Borowski. #line:f2cece
    Bruce: Bruce. Just Bruce. #line:3093c9
    Pastor: Pastor. Pastor Karen. #line:4983fe
    <<unlockDoodle BruceSketch>>
    <<set $pastor to 1>>
<<elseif $pastor is 1>>
    Mae: are you guys waiting for someone? #line:4b43e2
    Bruce: no #line:eb5ed0
    Pastor: Bruce is a little gunshy #line:3f8673
    Bruce: ain’t been in a restaurant in a while #line:26d878
    Pastor: Click Clack’s as good as any place to start! #line:f0aefd
    Bruce: ...gotta get back on the horse sometime… #line:0c07a8
    Mae: You might actually order horse in there without knowing it. #line:243cf6
    Pastor: Mae... #line:dbd361
    Mae: It’s true! #line:2584ac
    Bruce: heh heh. #line:cc5c47
    <<set $pastor_done to 1>>
<<endif>>
===
title: BackgroundA2D2
tags: 
colorID: 3
position: 2341,-110
---
Council3: oh ok.
<<set $church_hill_background_a2d2_done to 1>>
Council2: so you're more of a... drifter?
Bruce: Yeah, but it's temporary...
Council1: Of course! Well, we just wanted to see if you're ok up here.
Bruce: Yeah, Pastor Kate has been very kind.
Council4: Mind if we see your setup here?
Council3: What do you think, Molly?
Molly: Uh
Bruce: Uh
Molly: It's... fine?
<<close>>
<<playOneShot event:/ui/snap_animation Council1>>
<<flip Council1 1>>
<<wait .5>>
<<playOneShot event:/ui/snap_animation Council2>>
<<flip Council3 1>>
<<wait .5>>
<<playOneShot event:/ui/snap_animation Council3>>
<<flip Council4 1>>
<<wait .5>>
<<playOneShot event:/ui/snap_animation Council4>>
<<flip Council2 1>>
<<wait .5>>
//<<walk Council2 Council2Camp>>
//<<walk Council3 Council3Camp>>
//<<walk Council4 Council4Camp>>
//<<walk Molly MollyCamp>>
//<<walk Bruce BruceCamp>>
//<<walk Council1 Council1Camp wait>>
Council1: Ha ha! Alright... Bruce?
Bruce: Yeah.
Council3: How are you finding Possum Springs?
Bruce: nice place. nice people.
Council4: how long you been in town, bruce?
Bruce: I been here for a while now...
Bruce: and I was here before back in the 70s.
Bruce: They were looking for miners, at least for a while.
Council4: Got a whole thing going here, Bruce!
Bruce: yup.
Council1: pretty impressive!
Bruce: uh
Bruce: thanks.
<<wait 1>>
Council4: Well, we sure appreciate you talking to us, Bruce.
Council2: It was really great to meet you!
Bruce: Yup. Likewise.
Council1: Alright, troops! Let's move out!
<<close>>
<<playOneShot event:/ui/snap_animation Council1>>
<<flip Council1 -1>>
<<wait .5>>
<<playOneShot event:/ui/snap_animation Council2>>
<<flip Council3 -1>>
<<wait .5>>
<<playOneShot event:/ui/snap_animation Council3>>
<<flip Council4 -1>>
<<wait .5>>
<<playOneShot event:/ui/snap_animation Council4>>
<<flip Council2 -1>>
<<wait .5>>
<<wait .5>>
<<walk Council1 Council1Church>>
<<walk Council2 Council2Church>>
<<walk Council3 Council3Church>>
<<walk Council4 Council4Church>>
<<wait 1>>
Council3: Thanks again Bruce!
<<close>>
<<wait 1>>
Molly: Thanks, Bruce.
Bruce: no trouble.
<<setCanInteract Bruce 1>>
<<walk Molly MollyChurch>>
Council3: That's quite a setup he has there.
Council2: Sure is.
Council4: When I used to go camp with my dad it wasn't nearly that nice.
Council1: Oh really? We belonged to a hunter's camp north of Possum Jump.
Council4: My father's idea of camping was a bit more rustic.
Council3: Quite a setup, he's got.
Council2: He hasn't harmed anyone.
Council1: *sigh*
Council1: it's a toughie.
Molly: I have things to do.
<<close>>
<<waitForMove Molly>>
<<walk Molly OffscreenLeft>>
<<wait 1>>
Council3: I don't know that it serves the interests of the community.
Council1: Either way he shouldn't be sleeping outside.
Council4: Can we discuss this indoors somewhere?
Council1: TO THE CLICK CLACK!
<<close>>
<<walk Council1 OffscreenLeft>>
<<walk Council2 OffscreenLeft>>
<<walk Council3 OffscreenLeft>>
<<walk Council4 OffscreenLeft>>
===
