title: Bed
tags: demo2
colorID: 0
position: 3493,1361
---
[[DoSleep]]
===
title: Internal
tags: demo
colorID: 0
position: 1835,1792
---
<<if $mae_room_internal is 0>>
	Mae: {align=top,furthestLocator}If I don't get out of this room I am probably going [speed=.7][color=999]to burn it down with me in it.[/all] 
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 1>>
	Mae: {align=top,furthestLocator}I bet someone has done that. For like [color=999]political reasons.[/all]-_-
    Mae: o_o
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 2>>
	Mae: {align=top,furthestLocator}I wonder what would be left if I did [speed=.7][color=999]burn the room down with me in it?[/all]o_o
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 3>>
	Mae: {align=top,furthestLocator,width=2}Bones probably. Copper pipes. [speed=.7][color=999]A shrieking phantom.[/all]
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 4>>
	Mae: {align=top,furthestLocator,width=2}"Yeah, this is a good location"
	Mae: {align=top,furthestLocator,width=3}"but I hear there was once a house here and a girl burned it down."
	Mae: "[speed=.5][color=999][shake=.03]With her inside it.[/all]":( \o/
    Mae: :| _o_
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 5>>
	Mae: {align=top,furthestLocator,width=2}"We like to mention that to potential buyers."[/all]
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 6>>
	Mae: {align=top,furthestLocator,width=2}"There is a lot of screaming."
	Mae: "[speed=.7][color=999]I mean was."[/all]
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 7>>
	Mae: {align=top,furthestLocator,width=2}In fact, she did it on a night [speed=.7][color=999][shake=.04]JUST LIKE TONIGHT...[/all]-_-
    Mae: o_o
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 8>>
	Mae: {align=top,furthestLocator,width=2}And that's why I should [wave]go outside.[/all]
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 9>>
	Mae: {align=top,furthestLocator,emote=maeburn,emoteAlign=middle}
<<endif>>
===
title: Mirror_A1D3
tags: 
colorID: 2
position: 2088,1793
---
<<fadeOut 1>>
<<wait 1>>
<<scene Mirror>>
===
title: BedChoice
tags: 
colorID: 0
position: 3789,1092
---
<<if $act is 2 and $day is 4 and $night is 1>>
    Mae: No sleep! Harfest!!!
<<elseif $act is 1 and $day is 3 and $party_prep>>
    Mae: No sleep! Party Time! Gotta get ready!
<<elseif $night is 0>>
    [[Bed]]
<<elseif $act is 1 and $day is 0>>
    [[Bed]]
<<else>>
    Mae: Bedtime?
    [[No|NoBed]]
    [[Yes|Bed]]
<<endif>>
===
title: NoBed
tags: 
colorID: 0
position: 3805,1659
---
Mae: You can't live in bed, Mae.
===
title: EnterLevel
tags: 
colorID: 0
position: 2830,1303
---
<<if $night is 0>>
    <<if $mae_room_morning>>
        <<tell _AmbientSFXMorning Play>>
    <<else>>
        <<tell _AmbientSFXDay Play>>
    <<endif>>
<<else>>
    <<tell _AmbientSFXNight Play>>
<<endif>>
<<if $day > 0 and $night is 0 and $party_prep is 0 and $woke_up is 0>>
    <<fadeIn 3>>
    <<if $act is 4 and $day is 2>>
        // let the hospital ambience keep playing
    <<else>>
        <<if $act is 3>>
            <<playMusic waking_up_act3>>
        <<else>>
            <<playMusic waking_up>>
        <<endif>>
    <<endif>>   
    <<wait 3>>
    <<waitForInteractOrJump>>
    <<fadeOutMusic .5>>
    <<if $act is 4 and $day is 2>>
        <<fadeOut .5 wait>>
        <<animationTrigger MaeBed Empty>>
    <<else>>
        <<animationTrigger MaeBed WakeUp>>
        <<wait 3.5>>
        <<fadeOut .5 wait>>
    <<endif>>
    <<show Laptop>>
    <<animationTrigger MaeBed Idle>>
    <<warp Mae PlayerStart>>
    <<snapToFloor>>
    <<setCameraMode FollowPlayer>>
    <<if $act is 4 and $day is 1>>
        <<hide Laptop>>
    <<elseif $act is 4 and $day is 2>>
        <<hide Laptop>>
    <<elseif $act > 1 or $day >= 2>>
        <<show Laptop>>
    <<else>>
        <<hide Laptop>>
    <<endif>>
    <<fadeIn .5>>
    <<set $woke_up to 1>>
<<else>>
    <<if $act > 1 or $day >= 2>>
        <<show Laptop>>
    <<else>>
        <<hide Laptop>>
    <<endif>>
<<endif>>

<<if $act is 1 and $day is 2>>
    <<if $enter_mae_room < 010200 and $night is 0>>
        [[A1D2Morning]]
    <<endif>>
<<elseif $act is 1 and $day is 3>>
    <<if $after_party>>
        <<animationTrigger MaeBed Sleeping>>
        <<hide Laptop>>
        <<hide Player>>
        [[A1D3AfterParty]]
    <<elseif $party_prep>>
        <<set $mirror_done to 0>>
        <<if $mae_room_party_prep is 0>>
            [[A1D3PartyPrep]]
        <<endif>>
    <<endif>>
<<elseif $act is 2 and $day is 1>>
    <<if $night is 0>>
        <<set $mirror_done to 0>>
    <<endif>>
    <<if $enter_mae_room < 020100 and $night is 0>>
        [[A2D1Morning]]
    <<endif>>
<<elseif $act is 2 and $day is 4>>
    <<if $harfest_prep is 1 and $harfest_costume is 0>>
        <<set $mirror_done to 0>>
    <<endif>>
<<endif>>

<<if $act is 4 and $day is 2>>
    <<if $night is 0>>
        <<set $death_walk to 1>>
        <<playIdleAnimationPlayer>>
        //<<set $death_prairie_effects to 1>>
        <<if $enter_mae_room < 040200>>
            [[A4D2Morning]]
        <<endif>>
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    <<if $found_pentagrams >= 3>>
        <<unlockAchievement Thryy_Wyrd_Tyyns>>
    <<endif>>
<<endif>>

<<if $act is 3 and $day is 3>>
    <<unlockAchievement Astral_Secrets>>
<<endif>>

<<if $act is 2 and $day is 1>>
    <<unlockDoodle EverythingFeelsBad>>
    <<unlockDoodle NeverDrink>>
<<endif>>

<<if $act is 3 and $day is 1>>
    <<unlockDoodle Ibon1>>
    <<unlockDoodle Eide1>>
<<endif>>

<<if $act is 4 and $day is 3>>
    <<unlockDoodle Ibon3>>
    <<unlockDoodle Shapes>>
    <<unlockPentagramDoodle>>
<<endif>>
===
title: DoSleep
tags: 
colorID: 0
position: 3478,1701
---
<<close>>
<<fadeOut .5 wait>>
<<setCameraMode Stationary>>
<<camera position 0 0>>
<<hide Laptop>>
<<hidePlayer>>
<<fadeIn .5>>
<<animationTrigger MaeBed Sleep>>
<<wait 7>>

[[EndSleep]]
===
title: A1D2Morning
tags: 
colorID: 2
position: 2531,1376
---
Mae: Wow. It’s been literally 36 hours since I’ve been online.
Mae: This shall not stand.
<<set $enter_mae_room to 010200>>
<<fadeOut 1 wait>>
<<scene Laptop>>
===
title: A1D3PartyPrep
tags: 
colorID: 2
position: 2829,1645
---
<<set $mae_room_party_prep to 1>>
Mae: OK ok ok.
Mae: It’s go time.
Mae: Ugh I should probably make sure I don’t look like a cave beast.
===
title: A1D3AfterParty
tags: 
colorID: 2
position: 3197,1348
---
<<set $mirror_done to 1>>
<<show Bea>>
Bea: there you go.
Mae: I'm sorry, Bea. I’m a mess.
Bea: You know? Whatever.
Bea: It’s not your fault.
Bea: You're just a kid.
Mae: I'm older than you! Two months!
Bea: Yeah well,
Bea: I stayed here and got older
Bea: while you went off and stayed the same.
Mae: zzz
Bea: *sigh*
Bea: G’night Mayday.
<<close>>
[[EndSleep]]
===
title: Laptop
tags: 
colorID: 0
position: 2076,1507
---
<<fadeOut .5 wait>>
<<scene Laptop>>
===
title: TriggerLaptop
tags: 
colorID: 0
position: 1836,1512
---
<<set $trigger_laptop to 0>>

Mae: Let's see if this Angus fix works...
Mae: can't get any worse at least.
Mae: i guess it could be on fire?
Mae: anyway.
<<irisOut 1 wait>>
<<scene Laptop>>
===
title: EndSleep
tags: 
colorID: 0
position: 3161,1654
---
<<fadeOut 3 wait>>

<<if $act is 1 and $day is 3>>
    <<scene AstralAct1Day3>>
<<elseif $act is 2 and $day is 1>>
    <<scene AstralAct2Day1>>
<<elseif $act is 2 and $day is 2>>
    <<scene AstralAct2Day2>>
<<elseif $act is 2 and $day is 3>>
    <<scene AstralAct2Day3>>
<<elseif $act is 2 and $day is 4>>
    <<scene BlackoutWoods>>
<<elseif $act is 3 and $day is 1>>
    <<scene AstralAct3Day1>>
<<elseif $act is 3 and $day is 2>>
    <<scene AstralAct3Day2>>
<<else>>
    <<endDay>>
<<endif>>
===
title: Mirror_A2D4
tags: 
colorID: 3
position: 2079,2258
---
<<fadeOut 1>>
<<wait 1>>
<<scene Mirror>>
===
title: Mirror_A2D1
tags: demo2
colorID: 3
position: 2082,2027
---
Mae: Oh good. #line:0ca0d4
Mae: I was right. #line:cd1c65
Mae: I look like utter hell. #line:d8f5db
Mae: BURN IT! #line:cfe544
<<set $mirror_done to 1>>
===
title: A2D1Morning
tags: demo2
colorID: 3
position: 2538,1642
---
<<set $enter_mae_room to 020100>>
Mae: =_= #line:297513
Mae: Well #line:71032c
Mae: that was a weird one. #line:31fabd
Mae: I feel awful. #line:f6439d
Mae: :\ #line:df2910
Mae: I probably look awful. #line:c09536
Mae: Ugh. #line:044650
Mae: I'm gonna burn this room down. #line:cb2c74
Mae: o_o #line:80183e
Mae: With me in it. #line:0f0a0d
Mae: Live out my remaining days as a shrieking ghost. #line:18afa3
Mae: Yup. #line:12291f
Mae: :| #line:d0dffe
<<unlockDoodle FireGhost>>
===
title: InitLevel
tags: 
colorID: 0
position: 2746,1036
---
<<set $mirror_done to 1>>

<<if $day > 0 and $night is 0 and $party_prep is 0 and $woke_up is 0>>
    <<hidePlayer>>
    <<setCameraMode Stationary>>
    <<camera position 0 0>>
    <<hide Laptop>>
    <<animationTrigger MaeBed WakeUpIdle>>
<<endif>>
===
title: A4D2Morning
tags: 
colorID: 0
position: 2603,1911
---
<<set $enter_mae_room to 040200>>
Mae: Urg.
<<close>>
<<fadeOut 1 wait>>
<<overrideLoadingScreenType Blank>>
<<scene MaeStreet>>
===
