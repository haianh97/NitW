title: EnterLevel_A1D0
tags: 
colorID: 1
position: -2503,485
---
<<if $dad_done is 0>>
    <<fadeIn 3>>
    <<playMusic come_home>>
    <<destroy Collision:DoorAtNight>>
    <<wait 6>>
    <<tell _AmbientSFX:Door Play>>
    <<wait 3>>
    <<walk Mae EnterMaePositionForDadCouchTalk wait>>
    <<set $door_blocker to 1>>
    Mae: HEY! REMEMBER ME?
    Dad: AAAAAAAAAAAAGH!
    Dad: Mae! You gave me a heart attack!
    Mae: GOOD! /o\
    Dad: What are you doing-
    <<wait 2>>
    Dad: Ooooooh.
    Mae: _o_
    Dad: Mae honey we thought you were coming tomorrow night!
    Mae: YOU THOUGHT WRONG! \o/
    Mae: _o_
    Dad: How did you get here?
    Mae: I WALKED UNTIL I GOT ARRESTED
    Mae: BY AUNT MALL COP. :\
    Dad: Oh did you say hi to Molly for me?
    Mae: NO! :(
    Mae: I SAY HI FOR NO ONE!
    Mae: EFF THE COPS!
    Mae: I'M GOING TO BED!
    Mae: IS MY BED STILL HERE? :o
    Mae: OR ARE WE WAITING FOR TOMORROW NIGHT FOR THAT TOO?
    Dad: Ah Mae. So good to hear that voice again.
    Mae: GOODNIGHT! :| \o/
    Mae: _o_
    <<set $dad_done to 1>>
    <<unlockDoodle DadOlder>>
<<endif>>
===
title: EnterLevel_A1D1
tags: 
colorID: 2
position: -2155,503
---

===
title: EnterLevel_A1D3
tags: 
colorID: 2
position: -1426,551
---
<<if $party_prep is 1>>
    <<walk Mae KitchenTalk wait>>
    [[Dad_A1D3]]
<<endif>>
===
title: EnterLevel_A2D1
tags: 
colorID: 3
position: -870,533
---


===
title: Dad_A2D1_0
tags: 
colorID: 3
position: -1043,1136
---
Mae: 'Sup dad.
Dad: Heard you had a wild time last night!
Mae: I didn't drink!
Dad: It works better if you let me accuse you first.
Mae: Right.
Dad: Here's my hangover cure:
Dad: not drinking the night before.
Mae: wow. hold on, let me write that down.
Dad: it's complicated, i know.
<<unlockDoodle DadStillFunny>>
===
title: Dad_A2D1_1
tags: 
colorID: 3
position: -914,1466
---
Dad: What did you do tonight?
<<if $did_gregg_friendship_quest_1 is 1>>
    Mae: Um.
    Mae: Helped Gregg move some stuff.
    Dad: Look at what a good friend you are.
    Dad: Helping someone move is quite a gesture.
    Mae: Well, we've been best friends since like
    Mae: forever.
    Mae: So I feel like we're well past that point.
    Mae: I am really, really sore though.
    Dad: Sometimes you gotta get sore for friends.
<<elseif $did_bea_friendship_quest_1 is 1>>
	Mae: Went out with Bea.
	Dad: You have a good time?
	Mae: Yeah. She's kinda... I dunno.
	Dad: ?
	Mae: Like, she's older. Like, much older.
	Mae: And kinda... sad?
	Dad: Well, she's been through a lot.
	Mae: Yeah.
<<else>>
    Mae: I didn't register any friendship quests.
    Dad: Huh, okay. Someone's gone wrong here.
<<endif>>

===
title: EnterLevel_A2D2
tags: 
colorID: 3
position: -148,609
---


===
title: InitLevel_A1D0
tags: 
colorID: 1
position: -2515,252
---
<<hide Bea>>
<<show Dad>>
<<hide Mom>>
<<sit Dad>>

<<if $dad_done is 0>>
    <<warp Mae MaeOutside>>
<<endif>>
===
title: InitLevel_A1D1
tags: 
colorID: 2
position: -2156,239
---
<<hide Bea>>
<<if $night is 0>>
	<<hide Dad>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
<<elseif $night is 1>>
	<<show Dad>>
    <<sit Dad>>
	<<hide Mom>>
<<endif>>

<<if $night is 0>>
    <<set $mom_stairs to 1>>

    <<if $mom is 0>>
        <<set $door_blocker to 1>>
    <<else>>
        <<set $door_blocker to 0>>
    <<endif>>
<<endif>>
===
title: InitLevel_A1D3
tags: 
colorID: 2
position: -1402,264
---
<<if $party_prep is 1>>
	<<show Mom>>
    <<sit Mom>>
	<<show Dad>>
	<<hide Bea>>
	<<warp Dad DadKitchen>>
    <<setRigOffsetZ Dad 0>>
    <<setSpriteLayer Dad DadFG0>>
    <<setPlayerInteractLocator Dad KitchenTalk>>
    <<setCanInteract Mom 0>>
<<elseif $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<elseif $night is 1>>
	<<hide Mom>>
	<<hide Dad>>
	<<hide Bea>>
<<endif>>

===
title: InitLevel_A2D1
tags: 
colorID: 3
position: -852,305
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<show Dad>>
    <<sit Dad>>
	<<hide Bea>>
<<endif>>

<<if $night is 0>>
    <<set $mom_stairs to 1>>

    <<if $mom is 0>>
        <<set $door_blocker to 1>>
    <<else>>
        <<set $door_blocker to 0>>
    <<endif>>
<<endif>>
===
title: InitLevel_A2D2
tags: 
colorID: 3
position: -156,310
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<show Dad>>
    <<sit Dad>>
	<<hide Bea>>
<<endif>>
===
title: InitLevel_A1D2
tags: 
colorID: 2
position: -1796,268
---
<<hide Bea>>
<<if $night is 0>>
    <<hide Dad>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
<<else>>
    <<show Dad>>
    <<sit Dad>>
    <<hide Mom>>
<<endif>>

<<if $night is 0>>
    <<set $mom_stairs to 1>>
<<endif>>
===
title: Dad_A1D1_0
tags: 
colorID: 2
position: -2383,1085
---
Dad: Hey Kitten.
Mae: *ROWR*
Dad: Listen Mae, I'm sorry about last night.
Dad: I really thought you were coming home tonight.
[[It's ok|Dad_A1D1_ItsOk]]
[[It's ok I guess...|Dad_A1D1_ItsOkIGuess]]
===
title: Dad_A1D1_ItsOk
tags: 
colorID: 2
position: -2387,1327
---
Mae: Went through the ravine by the old mill.
Mae: Almost died.
Dad: What?
Mae: Yeah logs almost killed me.
Mae: Normal stuff.
Dad: Well, I promise you I'll never leave you stranded again.
[[Dad_A1D1_Continue]]
===
title: Dad_A1D1_Continue
tags: 
colorID: 2
position: -2378,1552
---
Mae: Tell you what.
Mae: How about we pretend I did come home tonight.
Mae: And we just got back from the bus station.
Dad: Start over?
Mae: Yeah.
Dad: Ok.
Mae: Hey Dad.
Dad: Hey Mae.
Mae: Thanks for picking me up.
Dad: Welcome home, Mae.
Mae: I'm going to go to sleep I think.
Dad: Well you've had a big day.
Dad: Goodnight, kitten.
<<unlockDoodle DadStillFunny>>
===
title: Dad_A1D1_ItsOkIGuess
tags: 
colorID: 2
position: -2102,1317
---
Mae: Its ok I guess...
Dad: You know me. With the bad memory.
Mae: Yeah I remember.
Mae: I mean you do pretty well.
Dad: Yeah I remember to eat and breathe.
Mae: You're really good at, like, remembering how to walk.
Dad: I'm a champ.
[[Dad_A1D1_Continue]]
===
title: Dad_A1D1_1
tags: 
colorID: 2
position: -2110,1087
---
Mae: OH WAIT! I FORGOT TO TELL YOU!
Dad: What?
Mae: WE FOUND AN ARM ON THE GROUND TODAY!
Dad: Are we still pretending it's yesterday?
Mae: No! We came out of the Clik Clak and there was an arm!
Dad: Just like... an arm?
Dad: What?
Mae: Aunt Mall Cop came and everything!
Dad: Oh my god. That's really... weird.
Mae: I poked it with a stick!
Dad: Ok really Mae don't poke dead body parts with sticks.
Mae: Too late!
Dad: Wow. I'll have to email Molly.
Mae: Alright goodnight for real now!

===
title: A1D3_PartyPrep
tags: 
colorID: 2
position: -1335,1500
---
Mom: Hey sweetie!
Dad: Look who's home!
Mae: Hey, the parents.
Mom: I made tacos.
Dad: Excuse me, WE made tacos.
Mae: Tacos?
Mom: Tacos!
Dad: Do you want Tacos?
[[I want tacos. Every day. All day.|Mom_A1D3_IWantTacos]]
[[No, I'm gonna snack it up at the party later.|Mom_A1D3_NoGonnaSnackItUp]]
===
title: Mom_A1D3_IWantTacos
tags: 
colorID: 2
position: -1433,1860
---
Dad: The family that tacos together rockos together.
Mae: ...
Mom: Honey no.
Dad: What?
Mae: I am so angry right now.
Dad: You’ve all turned against me.
Mom: Mae honey do you want sour cream on your taco?
Mae: I.
Mae: Want.
Mae: Everything.
<<set $ate_tacos to 1>>
<<close>>
<<set $night to 1>>
<<fadeOut 1 wait>>
<<scene MaeRoom>>
===
title: Mom_A1D3_NoGonnaSnackItUp
tags: 
colorID: 2
position: -1107,1851
---
Dad: Did you mean snake it up?
Mom: Watch out for snakes, honey.
Mae: Guys there's gonna be a fire and people.
Mae: Snakes will be running in terror.
Dad: Snakes don't run, kitten.
Mom: Can you imagine snakes with legs?
Mom: *shiver*
Mae: Those are lizards, mom.
Mom: Watch out for lizards then.
Dad: They'll getcha.
<<close>>
<<set $night to 1>>
<<fadeOut 1 wait>>
<<scene MaeRoom>>
===
title: InitLevel_A0D0
tags: demo
colorID: 2
position: -2815,248
---
<<hide Bea>>
<<hide Dad>>
<<show Mom>>
<<sit Mom>>
===
title: Clock
tags: demo
colorID: 0
position: -848,-522
---
<<if $mae_house_clock is 0>>
	[[ClockClock]]
<<elseif $mae_house_clock is 1>>
	[[ClockDuck]]
<<elseif $mae_house_clock is 2>>
	[[ClockOldCountry]]
<<endif>>
===
title: ClockClock
tags: demo
colorID: 0
position: -968,-89
---
Mae: {align=middle,locator=clock} Great-grandad's clock.
Mae: {align=middle,locator=clock} It's from the old country!
Mae: {align=middle,locator=clock} It's never really worked.
<<set $mae_house_clock to 1>>

===
title: ClockOldCountry
tags: demo
colorID: 0
position: -432,-462
---
Mae: {align=middle,locator=clock} There are a few things hanging around in here from the Old Country.
Mae: {align=middle,locator=clock} This clock, the trunk in the storage room
Mae: {align=middle,locator=clock} and whatever is buried under the house after the cement incident.
===
title: ClockDuck
tags: demo
colorID: 0
position: -634,-173
---
Mae: {align=middle,locator=clock} This thing goes off randomly every month or so.
Mae: {align=middle,locator=clock} Little wooden duck pops out. Makes the weirdest sound.
Mae: {align=middle,locator=clock} Scares dad half to death every time.
Mae: {align=middle,locator=clock} Used to joke it was Grandad getting even with him from beyond the grave.
<<set $mae_house_clock to 2>>
===
title: Door1
tags: demo
colorID: 0
position: -74,-676
---
<<if $mae_house_door_1 is 0>>
	Mae: Don't need the bathroom at the moment.
	Mae: Tooth-brushing is for kissers.
	<<set $mae_house_door_1 to 1>>
<<elseif $mae_house_door_1 is 1>>
	Mae: I hate walking downstairs and finding out someone is in there.
	Mae: I feel like I've stomped all over their right to privacy.
	<<set $mae_house_door_1 to 2>>
<<elseif $mae_house_door_1 is 2>>
	Mae: I will use the bathroom when I feel like it.
	Mae: Which is not now.
<<endif>>
===
title: Door2
tags: demo
colorID: 0
position: 186,-625
---
<<if $mae_house_door_2 is 0>>
	Mae: I think I've only been in mom and dad's room like once.
	Mae: Wait. That can't be right.
	Mae: What do I even remember about it?
	<<set $mae_house_door_2 += 1>>
<<elseif $mae_house_door_2 is 1>>
	Mae: They have that creepy statue of naked people kissing
	Mae: on the table at the foot of the bed.
	Mae: NOPE
	<<set $mae_house_door_2 += 1>>
<<elseif $mae_house_door_2 is 2>>
	Mae: My mom got a cedar chest from her mom.
	Mae: Some kind of heirloom.
	Mae: I think back in the old days you were supposed to fill it with a dowry?
	Mae: What is a dowry?
	<<set $mae_house_door_2 += 1>>
<<elseif $mae_house_door_2 is 3>>
	Mae: I'll just leave mom and dad's room to the imagination a bit longer.
<<endif>>

===
title: MomStairs_A0D0
tags: demo
colorID: 0
position: -1630,-508
---
Mom: {align=middle,locator=momstairs} ...Mae?
Mae: Yeah?
Mom: {align=middle,locator=momstairs} Are you going to go out?
Mae: Yeah!
Mom: {align=middle,locator=momstairs} [wave]Come back to the kitchen for a second![/wave]
===
title: Dad_A1D1
tags: 
colorID: 2
position: -2191,838
---
<<if $dad is 0>>
	<<set $dad += 1>>
	[[Dad_A1D1_0]]
<<elseif $dad is 1>>
	<<set $dad += 1>>
	<<set $dad_done to 1>>
	[[Dad_A1D1_1]]
<<endif>>

===
title: Dad_A2D1
tags: 
colorID: 3
position: -859,876
---
<<if $dad is 0>>
	<<set $dad += 1>>
	[[Dad_A2D1_0]]
<<elseif $dad is 1>>
	<<set $dad += 1>>
	[[Dad_A2D1_1]]
<<elseif $dad is 2>>
    [[GarboCheck]]
<<endif>>
===
title: Dad_A1D2
tags: 
colorID: 2
position: -1771,812
---
<<if $dad is 0>>
	<<set $dad += 1>>
	[[Dad_A1D2_0]]
<<elseif $dad is 1>>
	<<set $dad += 1>>
	<<set $dad_done to 1>>
	[[Dad_A1D2_1]]
<<endif>>
===
title: Dad_A1D2_0
tags: 
colorID: 2
position: -1854,1103
---
Mae: Hey dadders.
Dad: Why if it isn’t the Mayor of Possum Springs herself.
Dad: What did you get up to today?
Mae: Hung out with Angus a bit. You remember him?
Dad: Oh yeah! I like Angus. He’s smart.
Dad: In the old days I used to work with his dad.
Dad: I know they fell on some hard times after we all got laid off.
Dad: Used to see him at the bar…
Mae: Yeah.
Dad: After I stopped drinking I didn’t really see him as much.
Dad: I wonder how he is.
<<set $dad_knows_angus to 1>>

===
title: Dad_A1D2_1
tags: 
colorID: 2
position: -1609,1114
---
Dad: Did they find out anything about that arm?
Mae: Not that I heard.
Dad: huh. Well, be careful out there.
Mae: Maybe I should carry mace or brass knuckles or something.
Dad: Well it’d be a shame if you got into trouble
Dad: and were unarmed.
Mae: Naw, I think I’ll be ok.
Dad: ...
Mae: What?
Dad: ...
Mae: HA HA HA HA HA ok I get it.
Dad: UN-ARMED??? EH??
Mae: I’m going to bed now.
Dad: Goodnight Mayor Kitten.
Mae: Goodnight!
<<unlockDoodle DadStillFunny>>
===
title: Internal
tags: demo
colorID: 0
position: -555,-1026
---
<<if $mae_room_internal is 0>>
    Mae: {width=5}If I don't get out of this room I am probably going [speed=.7][color=999]to burn it down with me in it.[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 1>>
    Mae: {width=3}I bet someone has done that. For like [color=999]political reasons.[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 2>>
    Mae: {width=3}I wonder what would be left if I did [speed=.7][color=999]burn the room down with me in it?[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 3>>
    Mae: {width=3}Bones probably. Copper pipes. [speed=.7][color=999]A shrieking phantom.[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 4>>
    Mae: {width=3}"Yeah, this is a good location"
    Mae: "but I hear there was once a house here and a girl burned it down."
    Mae: "[speed=.5][color=999]With her inside it.[/all]"
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 5>>
    Mae: {width=3}"We like to mention that to potential buyers."[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 6>>
    Mae: {width=3}"There is a lot of screaming."
    Mae: "[speed=.7][color=999]I mean was."[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 7>>
    Mae: {width=3}In fact, she did it on a night [speed=.7][color=999]JUST LIKE TONIGHT...[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 8>>
    Mae: {width=3}And that's why I should [wave]go outside.[/all]
    <<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 9>>
    Mae: {align=top,furthestLocator,emote=maeburn,emoteAlign=middle}
<<endif>>
===
title: Couch
tags: 
colorID: 0
position: -3202,242
---
<<sit>>
<<if $act > 0>>
    <<if $night is 1>>
        Dad: You're on the couch!
    <<endif>>
<<endif>>
<<waitForInteractOrJump>>
===
title: EnterLevel_A1D2
tags: 
colorID: 2
position: -1748,545
---

===
title: Dad_A1D3
tags: 
colorID: 2
position: -1315,1134
---
[[A1D3_PartyPrep]]
===
title: InitLevel_A2D3
tags: 
colorID: 3
position: 222,326
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<setAnimInt Mom gloomy 1>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
    <<show Dad>>
    <<sit Dad>>
	<<hide Bea>>
<<endif>>
===
title: InitLevel_A2D4
tags: 
colorID: 3
position: 541,340
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<hide Dad>>
	<<hide Bea>>
<<endif>>
===
title: InitLevel_A3D1
tags: 
colorID: 4
position: 816,347
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<show Dad>>
    <<sit Dad>>
	<<hide Bea>>
<<endif>>

<<if $night is 0>>
    <<set $mom_stairs to 1>>

    <<if $mom is 0>>
        <<set $door_blocker to 1>>
    <<else>>
        <<set $door_blocker to 0>>
    <<endif>>
<<endif>>
===
title: InitLevel_A3D2
tags: 
colorID: 4
position: 1314,344
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<show Dad>>
    <<sit Dad>>
	<<hide Bea>>
<<endif>>
===
title: InitLevel_A3D3
tags: 
colorID: 4
position: 1922,344
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<show Dad>>
    <<sit Dad>>
	<<hide Bea>>
<<endif>>
===
title: InitLevel_A3D4
tags: 
colorID: 4
position: 2336,346
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<hide Dad>>
	<<hide Bea>>
<<endif>>
===
title: GarboCheck
tags: 
colorID: 3
position: -545,1699
---
<<if $knows_crawlspace_boxes and $mae_house_boxes_moved is 0>>
    <<if $watched_tv_with_dad is 0>>
        Mae: Hey Dad, would you mind moving the boxes in the crawlspace?
        Dad: Well what have you done for me lately?
        -> Been an excellent daughter.
        -> Been a credit to this family.
        Dad: That doesn't count.
        Dad: Well would you watch some TV with me?
    <<elseif $watched_tv_with_dad is 1>>
        Mae: Um... what about those boxes I asked about? In the crawlspace?
        Dad: Hmm. I'm thinking about it.
        Dad: Watch some TV with me?
    <<else>>
        <<set $dad_will_move_boxes to 1>>
        Mae: So how about those crawlspace boxes.
        Dad: Yeah, okay. I'll move 'em.
        Dad: TV time?
    <<endif>>
<<else>>
    Dad: You staying up?
<<endif>>
[[Yeah for a little bit.|YeahForALittleBit]]
[[Nah, I'm heading to bed.|NawImHeadingToBed]]
===
title: YeahForALittleBit
tags: 
colorID: 3
position: -396,2059
---
<<set $dad_done to 1>>
<<if $watched_tv_with_dad is 0>>
    Dad: Hop up. I'm watching Garbo and Malloy.
<<elseif $watched_tv_with_dad is 1>>
    Dad: Perfect timing, Garbo and Malloy is starting.
<<endif>>

<<if $player_y * 10 > -14>>
    <<playerFallThrough wait>>
<<endif>>
<<walk Mae MaeCouch>>
<<if $watched_tv_with_dad is 0>>
    Mae: Niiiiiiice.
<<elseif $watched_tv_with_dad is 1>>
    Mae: Aw yes.
<<endif>>
<<close>>
<<set $watched_tv_with_dad += 1>>
<<waitForMove Mae>>
<<cameraLookAt CameraTV 1>>
<<sit>>
<<tell TVOverlay Show>>
<<wait 2>>
<<if $act is 2 and $day is 1>>
    [[GarboAndMalloy_A2D1]]
<<elseif $act is 2 and $day is 2>>
    [[GarboAndMalloy_A2D2]]
<<elseif $act is 2 and $day is 3>>
    [[GarboAndMalloy_A2D3]]
<<else>>
    [[EndTV]]
<<endif>>
===
title: NawImHeadingToBed
tags: 
colorID: 3
position: -635,2083
---
Dad: Sounds like a plan.
===
title: Dad_A2D2_0
tags: 
colorID: 3
position: -95,1330
---
Mae: How's work?
Dad: Ya know. Better than the glass factory!
Mae: Is it?
Dad: I've mined, I've ran machines,
Dad: but now I get to slice steaks
Dad: and hock salamis.
Dad: I mean, it's easier on the back that's for sure.
Mae: But like, do you like it?
Dad: I like havin' a paycheck!
Mae: Fair.
Dad: Paychecks: the sweetest meats of all.
<<unlockDoodle DadStillFunny>>
===
title: Dad_A2D2
tags: 
colorID: 3
position: -350,920
---
<<if $dad is 0>>
	<<set $dad += 1>>
	[[Dad_A2D2_0]]
<<elseif $dad is 1>>
	<<set $dad += 1>>
	[[Dad_A2D2_1]]
<<elseif $dad is 2>>
    [[GarboCheck]]
<<endif>>
===
title: Dad_A2D2_1
tags: 
colorID: 3
position: 144,1255
---
Dad: What'd you do tonight?
Mae: Oh ya know. Stuff.
Mae: tired.
Mae: Feel like I just did a day's work.
Dad: You know, there are places you can get paid for work these days.
Mae: Oh really?
Dad: Yeah, they call it working.
Mae: Huh. You kids with your slang.
Dad: Slang you could pick up at work.
Mae: ok yes point made
===
title: Dad_A2D3_0
tags: 
colorID: 3
position: 332,956
---
Dad: Oof.
Mae: What's up?
Dad: Long day.
Mae: Yeah, same here.
Dad: How's socializing?
Mae: Surprisingly tiring sometimes.
Dad: Yeah. That's why I don't do much of it!
<<if $did_bea_friendship_quest_3 is 1>>
	Dad: Pleasant surprise seeing you at work tonight
	Mae: Yeah! I can't believe I hadn't visited yet.
	Dad: Well you haven't been back long.
	Dad: And you're not usually lurking at the Ham Panther.
	Mae: Who's to say I'm not?
	Dad: Well it was nice seeing you anyway.
	Mae: Lurk lurk.
<<endif>>
<<unlockDoodle DadStillFunny>>
===
title: Dad_A2D3_1
tags: 
colorID: 3
position: 553,954
---
<<if $mom > 0 or $mom_church > 0>>
	Mae: Any idea what was up with mom today?
	Dad: Oh, she's just stressed.
<<else>>
	Mae: You seem a little stressed tonight.
	Dad: Yeeeep. A bit stressed.
	Mae: Stressed about...
	Dad: Oh, just stuff.
<<endif>>
Mae: That's vague.
Dad: Ha ha, well.
Dad: I hate to be the one to break it to you
Dad: but you spend a lot of adulthood being vaguely stressed.
Mae: Aw dang.
Mae: I thought the stress would at least get more specific.
Dad: Oh no you'll have that too.
Mae: hrm.
Dad: Cheer up, kiddo. It's only life.
Mae: Gee, thanks pop.

===
title: Dad_A2D3
tags: 
colorID: 3
position: 265,713
---
<<if $dad is 0>>
	<<set $dad += 1>>
	[[Dad_A2D3_0]]
<<elseif $dad is 1>>
	<<set $dad += 1>>
	[[Dad_A2D3_1]]
<<elseif $dad is 2>>
    [[GarboCheck]]
<<endif>>
===
title: Dad_A3D1_0
tags: 
colorID: 4
position: 781,873
---
<<if $big_fight is 0>>
	Mae: Urgh.
	Dad: Urgh?
	Mae: Feeling kinda beat tonight.
	Dad: Aw poor Mae. Anything I can do?
	Mae: I...
	Mae: no.
	Mae: I don't know.
	Dad: Well if you figure it out, let me know.
	Mae: Thanks, dad.
<<else>>
    Mae: Urgh
	Dad: Yeah, seems like that's going around.
	Dad: The Urgh.
	Mae: Yeppers.
	Dad: So... uh... you and mom had a bit of a fight yesterday, huh?
	Mae: yeah.
	Dad: I thought you two were getting along?
        ->we were
            Mae: but we talked about it.
        ->we are
            Mae: just a bump
            Dad: You two. Always too alike.
            Mae: I guess.
            Mae: I got your chill and her smartass...ness.
    Dad: I'd just hate to see you two on the outs again.
    Mae: It'll be fine. We're cool.
    <<if $money_trouble is 1>>
		Mae: So we're having money problems?
        Dad: What? No. We're fine.
        Mae: Dad.
        Dad: Mae. It's fine.
		Mae: It's ok. I'm 20. I can handle it.
        Dad: Nothing to handle.
        Mae: ok.
    <<endif>>
<<endif>>
===
title: Dad_A3D1_1
tags: 
colorID: 4
position: 1022,853
---
Mae: Might just head up to bed tonight.
Dad: Sounds good, kid. Been a long couple days.
===
title: Dad_A3D1
tags: 
colorID: 4
position: 843,617
---
<<if $dad is 0>>
	<<set $dad += 1>>
	[[Dad_A3D1_0]]
<<elseif $dad is 1>>
	<<set $dad += 1>>
    <<set $dad_done to 1>>
	[[Dad_A3D1_1]]
<<endif>>
===
title: Dad_A3D2_0
tags: 
colorID: 4
position: 1312,917
---
Mae: Hey dad.
Dad: Hey you.
Dad: You ok?
Mae: Yeah. Weird night.
Dad: You look really worn out.
Mae: Yeah...
===
title: Dad_A3D2_1
tags: 
colorID: 4
position: 1553,913
---
Dad: Geez. I am sore tonight.
Dad: Need a backbrace or something.
Dad: How do you do it?
Mae: Do what?
Dad: Always running and jumping around all crazy
Mae: I dunno. I mean I'm really out of shape.
Mae: I don't think I've ever been *in* shape.
Dad: I used to be, but that was because of my job.
Dad: New job don't require much like that.
Mae: yeah.
<<wait 2>>
<<if $did_mom_friendship_quest is 1>>
    Dad: Heard you and your mom went an adventure today?
    Mae: Ha ha yeah.
    Dad: She talked about it all through dinner.
    Dad: You should, you know, be home for dinner sometime.
    Mae: Yeah.
    Dad: Yeah?
    Mae: Yeah.
<<endif>>
Dad: Ok, go to bed, you.
Mae: Will do.
===
title: Dad_A3D2
tags: 
colorID: 4
position: 1389,628
---
<<if $dad is 0>>
	<<set $dad += 1>>
	[[Dad_A3D2_0]]
<<elseif $dad is 1>>
	<<set $dad += 1>>
    <<set $dad_done to 1>>
	[[Dad_A3D2_1]]
<<endif>>
===
title: Dad_A3D3_0
tags: 
colorID: 4
position: 1983,845
---
Mae: Hi.
Dad: Hello.
Dad: Oh wow. Are you ok?
Mae: No.
<<close>>
<<if $player_y * 10 > -14>>
    <<playerFallThrough wait>>
<<endif>>
<<walk Mae MaeCouch>>
<<waitForMove Mae>>
<<cameraLookAt CameraTV 1>>
<<sit>>
Dad: Ok
Dad: so what's going on?
Mae: I don't think I can talk about it.
Dad: You're gonna have to start talking sometime, kitten.
Mae: I wish I could.
Dad: Why can't you?
    ->I don't even know.
        Dad: that's how it is sometimes
        Dad: but it's not always a great reason.
        Mae: i know.
    ->where to even start?
Mae: it's just
Mae: i've been having these dreams
Mae: and i keep thinking i see a ghost
Dad: a ghost?
Mae: and i've had these horrible nightmares
Mae: that feel really real
Mae: and i think something really bad is going to happen
Mae: and i've been running around doing all this stupid stuff
Mae: and where's it all going?
Mae: what even is my life right now?
Mae: i think i patched up some stuff tonight
Mae: but like
Mae: everyone is on their way somewhere else
<<if $big_fight>>
	Mae: and I had that big fight with mom.
    Dad: Kitten, it's ok.
<<endif>>
<<if $money_trouble>>
	Mae: and that's not even counting the money problems.
	Dad: Yeah.
    Dad: Money problems make everything worse. I know it.
    Mae: and i can't do anything about it
    Mae: i don't even know if i could hold down a job
    Mae: and how am i ever going to survive
<<endif>>
Mae: and- and- and
Dad: shhhhh, kitten
Dad: take a breath.
Mae: ok. ok. ok.
<<wait 4>>
Dad: Why don't you get some sleep?
Mae: ...
Mae: Can I just stay down here for a bit?
<<wait 3>>
Dad: As long as you like.
<<close>>
<<wait 2>>
[[GarboAndMalloy_A3D3]]
===
title: PreInitLevel
tags: 
colorID: 0
position: -2996,0
---
<<if $night is 0>>
    <<play MaeHouseMusic>>
<<else>>
    <<musicFadeOut 4>>
<<endif>>
===
title: InitMaeHouseFromGreggAngusApartment
tags: 
colorID: 0
position: -2528,16
---
<<playerFace -1>>
===
title: MomStairs
tags: 
colorID: 0
position: -3049,563
---
<<set $mom_stairs to 0>>

<<if $act is 1 and $day is 1>>
    Mom: Honey?
    Mae: Yeah?
    Mom: Come see me in the kitchen before you leave!
    Mae: Okay!
<<elseif $act is 1 and $day is 2>>
    Mom: Hey Hon.
    Mae: Yeah?
    Mom: Come say Hi.
<<elseif $act is 2 and $day is 1>>
    [[MomStairsA2D1]]
<<elseif $act is 3 and $day is 1>>
    Mom: Hey Hon.
    Mae: Yeah?
    Mom: Come say hello whydoncha?
<<elseif $act is 4 and $day is 2>>
    Mom: You were in a coma! Come talk to me.
<<elseif $act is 4 and $day is 3>>
    Mom: Mae honey? Is that you?
    Mae: yeah!
    Mom: I'm back in the kitchen
    Mom: if you want to talk...
<<endif>>
===
title: EndTV
tags: 
colorID: 3
position: -444,2458
---
<<close>>
<<tell TVOverlay to Hide>>
<<wait 2>>
<<cameraFollowPlayer 1>>
===
title: GarboAndMalloy_A2D2
tags: 
colorID: 3
position: -23,2079
---
<<animate TV Logo>>
<<tell _AmbientSFX:GarboAndMalloy Play>>
<<wait 5.5>>
//======================================
<<animate TV MalloyTalk>>
TV: aaaah, yes.
<<animate TV GarboTalk>>
TV: you know we like to get political here
<<animate TV MalloyTalk>>
TV: gotta do it! Gotta!
<<animate TV GarboTalk>>
TV: you hear about this new law on the books
<<animate TV GarboTalk>>
TV: pirating a movie online, you know about that?
<<animate TV MalloyTalk>>
TV: i do.
<<animate TV GarboTalk>>
TV: punishable by up to 10 years in prison!
<<animate TV MalloyTalk>>
TV: wow! And  here i’ve been trying to get in the hard way!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV GarboStare>>
<<wait 2>>
<<animate TV GarboTalk>>
TV: he has, folks!
<<animate TV MalloyTalk>>
TV: Wonder if pirating a movie about first degree murder
<<animate TV MalloyTalk>>
TV: earns you more prison respect than one about grand larceny?
<<animate TV GarboTalk>>
TV: hey Malloy, you know what i think about that law?
<<animate TV MalloyTalk>>
TV: what Garbo?
<<animate TV GarboTalk>>
TV: . . . “that’s a whoppah!”
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV MalloyLaugh>>
<<wait 2>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV MalloyStare>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: Coming up next: Stupid Sandwiches!
//======================================
<<animate TV Static>>
<<wait 3>>
[[EndTV]]
===
title: GarboAndMalloy_A2D1
tags: 
colorID: 3
position: -43,1803
---
<<animate TV Logo>>
<<tell _AmbientSFX:GarboAndMalloy Play>>
<<wait 5.5>>
//======================================
<<animate TV GarboTalk>>
TV: Welcome back folks!
<<animate TV MalloyTalk>>
TV: Hell of a show tonight.
<<animate TV GarboTalk>>
TV: Say, Malloy
<<animate TV MalloyTalk>>
TV: Yes, Garbo
<<animate TV GarboTalk>>
TV: Ever get the feeling that disaster’s right around the corner?
<<animate TV MalloyTalk>>
TV: Sure do, right before we start the show every night!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV GarboStare>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: You superstitious, Garbo?
<<animate TV GarboTalk>>
TV: Not anymore!
<<animate TV MalloyTalk>>
TV: What happened!
<<animate TV GarboTalk>>
TV: I’ve broken every mirror I’ve ever owned
<<animate TV GarboTalk>>
TV: and now I’m lucky enough to never have to look at this mug!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV GarboLaugh>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: yeah but whattabout the rest of us?
<<animate TV GarboTalk>>
TV: Wha-? “that’s a whoppah!”
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV MalloyLaugh>>
<<wait 2>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV GarboLaugh>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: Coming up next: Garbo and I join a cult!
//======================================
<<animate TV Static>>
<<wait 3>>
[[EndTV]]
===
title: GarboAndMalloy_A2D3
tags: 
colorID: 3
position: -16,2317
---
<<animate TV Logo>>
<<tell _AmbientSFX:GarboAndMalloy Play>>
<<wait 5.5>>
//======================================
<<animate TV GarboTalk>>
TV: And we’re back!
<<animate TV GarboTalk>>
TV: Great crowd in here tonight!
<<animate TV MalloyTalk>>
TV: we’d never leave for too long, folks!
<<animate TV MalloyTalk>>
TV: say Garbo!
<<animate TV GarboTalk>>
TV: yeah malloy?
<<animate TV MalloyTalk>>
TV: crowds like this remind me of our early days
<<animate TV GarboTalk>>
TV: i disagree. None of them are angry and drunk!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV GarboStare>>
<<wait 2>>
<<animate TV GarboTalk>>
TV: most of ‘em anyway.
<<animate TV MalloyTalk>>
TV: no, i mean those old nights, those good nights
<<animate TV MalloyTalk>>
TV: just the heat of the stagelights and the thrill of winging it!
<<animate TV GarboTalk>>
TV: and the gentle thump of an empty beer bottle thrown at your head!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV GarboLaugh>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: These days things are pretty cushy!
<<animate TV GarboTalk>>
TV: You think we’re getting soft?
<<animate TV MalloyTalk>>
TV: You sure are… AROUND THE MIDSECTION
<<animate TV GarboTalk>>
TV: “that’s a whoppah!”
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV GarboStare>>
<<wait 2>>
<<animate TV GarboTalk>>
TV: Malloy, it’s company policy to never body-shame!
<<animate TV MalloyTalk>>
TV: oh, i never worry about that, Garbo.
<<animate TV GarboTalk>>
TV: why is that, Malloy?
<<animate TV MalloyTalk>>
TV: I’m what you call bodyshameless!
<<playOneShot event:/bus_station/tv_applause TV>>
<<animate TV MalloyStare>>
<<wait 2>>
<<playOneShot event:/bus_station/tv_applause TV>>
<<animate TV GarboLaugh>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: Coming up next: Garbo and I go to counseling!
//======================================
<<animate TV Static>>
<<wait 3>>
[[EndTV]]
===
title: Picture_A1D1
tags: 
colorID: 2
position: -3823,-1153
---
Mae: {align=middle,locator=picture,width=2} I was so cute then. We were all so cute then.:)
Mae: {align=middle,locator=picture} Cuties, the lot of us.
Mae: {align=middle,locator=picture} We could have a traveling show. The Cute Borowskis.:|
<<set $mae_house_picture_done to 1>>
===
title: Picture_A1D3
tags: 
colorID: 2
position: -3824,-913
---
Mae: Home again.
Mae: Where the people know my name
Mae: and my mom's name
Mae: and my dad's name
Mae: and about the time I put that kid in the hospital.
<<set $mae_house_picture_done to 1>>
===
title: Picture_A3D1
tags: 
colorID: 4
position: -3334,-1154
---
Mae: Hm.
Mae: We're all looking a bit older I guess.
Mae: weird to take like one moment
Mae: and make it like
Mae: the official version of us.
<<set $mae_house_picture_done to 1>>
===
title: Picture_A2D4
tags: 
colorID: 3
position: -3584,-920
---
Mae: I've definitely grown quite a bit since then.
Mae: A little bit taller
Mae: a regular bit wider.
Mae: Granddad said I was "sturdy".
<<wait 2>>
Mae: screw it. I *AM* sturdy.
<<set $mae_house_picture_done to 1>>
===
title: Picture_A2D1
tags: demo2 
colorID: 3
position: -3587,-1149
---
Mae: Afternoon, us. #line:f41b8b
<<set $mae_house_picture_done to 1>>
===
title: HarfestExit
tags: 
colorID: 3
position: 532,610
---
<<playerFace -1>>
Mae: Hello?
Mae: Hm. They must already be out.
<<close>>
<<unlockDoodle Halloween>>
<<playerFace 1>>
<<walk Mae DoorExit>>
<<irisOut 1 wait>>
<<sectionTitle Harfest TowneCentreEast_Harfest left>>
===
title: GarboAndMalloy_A3D3
tags: 
colorID: 4
position: 1965,1122
---
<<close>>
<<tell TVOverlay Show>>
<<wait 2>>
<<animate TV Logo>>
<<wait 5.5>>
//========================
<<animate TV GarboTalk>>
TV: Malloy.
<<animate TV MalloyTalk>>
TV: Garbo.
<<animate TV GarboTalk>>
TV: Ever think about, ya know, the afterlife?
<<animate TV MalloyTalk>>
TV: I don’t have a life as it is!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV MalloyLaugh>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: why, garbo? Do you think there’s someone up there watching?
<<animate TV GarboTalk>>
TV: hey, i just hope anyone’s watching at this time slot!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV MalloyLaugh>>
<<wait 2>>
<<playOneShot event:/bus_station/tv_applause TV>>
<<animate TV GarboStare>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: these are the big questions, Garbo
<<animate TV GarboTalk>>
TV: don’t get any bigger, Malloy!
<<animate TV MalloyTalk>>
TV: are you a churchgoer, Garbo?
<<animate TV GarboTalk>>
TV: hey, the last time i was in church- they hadn’t invented religion yet!
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<animate TV MalloyLaugh>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: you were making it up as we went along!
<<animate TV GarboTalk>>
TV: “ok so we all think there’s a god, right?”
<<animate TV MalloyTalk>>
TV: “we should have potlucks!”
<<animate TV MalloyTalk>>
TV: can’t have a church without potlucks!
<<animate TV GarboTalk>>
TV: ya could, Malloy
<<animate TV GarboTalk>>
TV: but it
<<animate TV GarboTalk>>
TV: would be
//Malloy stares for 2 seconds
<<animate TV MalloyStare>>
<<wait 2>>
<<animate TV GarboTalk>>
TV: “A WHOPPAH!”
<<playOneShot event:/bus_station/tv_cheering TV>>
<<animate TV MalloyLaugh>>
<<wait 2>>
<<animate TV GarboLaugh>>
<<wait 2>>
<<animate TV MalloyLaugh>>
<<wait 2>>
<<animate TV GarboStare>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: coming up next: Garbo and I contact the dead!
//========================
<<animate TV Static>>
<<wait 3>>
[[EndTV]]
===
title: Dad_A3D3
tags: 
colorID: 4
position: 1983,594
---
<<if $dad is 0>>
	<<set $dad += 1>>
    <<set $dad_done to 1>>
	[[Dad_A3D3_0]]
<<endif>>
===
title: InitLevel_A4D2
tags: 
colorID: 5
position: 2986,357
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<hide Bea>>
<<endif>>

<<if $night is 0>>
    <<set $mom_stairs to 1>>

    <<if $mom is 0>>
        <<set $door_blocker to 1>>
    <<else>>
        <<set $door_blocker to 0>>
    <<endif>>
<<endif>>
===
title: InitLevel_A4D3
tags: 
colorID: 5
position: 3258,368
---
<<if $night is 0>>
    <<if $mom_home > 0>>
        <<sit Mom>>
    <<else>>
        <<hide Mom>>
    <<endif>>
	<<hide Dad>>
	<<hide Bea>>
<<else>>
	<<hide Mom>>
	<<hide Bea>>
<<endif>>

<<if $night is 0>>
    <<set $mom_stairs to 1>>

    <<if $mom is 0>>
        <<set $door_blocker to 1>>
    <<else>>
        <<set $door_blocker to 0>>
    <<endif>>
<<endif>>
===
title: MomStairsA2D1
tags: demo2
colorID: 0
position: -2790,568
---
Mom: Mae? #line:7b3670
Mae: Yeah? #line:190a5a
Mom: Come say Hi! #line:e0b858
Mae: Okay! #line:17e307
===
title: BirdCage_A1D2
tags: 
colorID: 2
position: -3471,-2424
---
Mae: Ugh. I have never liked this bird.
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A1D3
tags: 
colorID: 2
position: -3485,-2200
---
Mae: Hey bird! I'm home!!!!
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A1D1
tags: 
colorID: 2
position: -3479,-2659
---
Mae: How is this bird still alive? :\
Mae: o_o :|
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A2D1
tags: 
colorID: 3
position: -3240,-2652
---
Mae: 'sup bird.
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A2D2
tags: 
colorID: 3
position: -3255,-2417
---
Mae: what are you even thinking about in there?
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A2D3
tags: 
colorID: 3
position: -3251,-2201
---
Mae: i wonder if you can learn to talk to birds.
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A2D4
tags: 
colorID: 3
position: -3255,-1960
---
Mae: hey trashbird.
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A3D1
tags: 
colorID: 4
position: -3010,-2640
---
Mae: you're just like
Mae: the same every day?
Mae: what is your bird existence like?
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A3D2
tags: 
colorID: 4
position: -3004,-2417
---
Mae: hey bird.
Mae: sorry i'm always rude to you.
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A3D3
tags: 
colorID: 4
position: -3007,-2197
---
Mae: Bird.
Mae: i kinda get you now.
Mae: all like trapped and shit.
Mae: i should let you out.
<<wait 2>>
Mae: nah.
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A3D4
tags: 
colorID: 4
position: -3002,-1963
---
Mae: hey bird.
Mae: things are like
Mae: really intense right now.
Mae: pray to your bird gods for my soul, bird.
<<set $mae_house_bird_cage_done to 1>>
===
title: BirdCage_A4D3
tags: 
colorID: 6
position: -2736,-2612
---
Mae: We're gonna eat you for thanksgiving, bird.
<<set $mae_house_bird_cage_done to 1>>
===
title: Picture_A3D4
tags: 
colorID: 4
position: -3343,-915
---
Mae: ...
Mae: ...
Mae: geez
Mae: feels like it's been years since i got back.
Mae: and saw this again.
<<set $mae_house_picture_done to 1>>
===
title: Picture_A4D3
tags: 
colorID: 6
position: -3092,-1146
---
Mae: god.
Mae: it's us.
Mae: it's just... us.
Mae: home and us
<<wait 2>>
Mae: i don't know what this feeling is.
Mae: but i'm not gonna cry today.
<<set $mae_house_picture_done to 1>>
===
title: CheckForSafeCombination
tags: 
colorID: 0
position: -1418,-2813
---
<<if $safe_combination is 0>>
    <<set $safe_combination to 1>>
    Mae: oh hm
    Mae: there's like...
    Mae: something written inside this book cover...
    Mae: left 45, Right 100, left 22, left 90, right 15.
    Mae: is this like...
    Mae: what is this?
<<endif>>
===
title: BookshelfAct1.1
tags: 
colorID: 2
position: -1942,-2432
---
Mae: It was pretty great when Grandad gave me that apple crate of old scary stories.
Mae: Not sure how they ended up out here.
Mae: Death Comes To Wigglesbury.
Mae: The Fancy Corpse.
Mae: Hellmole Stevenson.
Mae: Necropuss, book the first.
Mae: Ladyshark.
Mae: The Stoat On Gallows Hill.
===
title: BookshelfAct1.2
tags: 
colorID: 2
position: -1943,-2205
---
Mae: Mom's has all of her grisly books
Mae: about creepy crimes and sad children
Mae: on a creepy sad shelf in their room.
Mae: Oh looks like one of mom's books made it out here.
Mae: ...
Mae: "And We Even Ate The Horses"
Mae: "One family's horrfying farm life in the 1880s"
Mae: Geez, mom.
===
title: BookshelfAct1.3
tags: 
colorID: 2
position: -1941,-1981
---
Mae: Hey look, some of Dad's books got in here.
Mae: Finches Of North America
Mae: World Of Titmice
Mae: The Complete Grackle: A Three Volume Set
Mae: Owls Are Owlsome!
Mae: Treacherous Cranes.
Mae: ...
Mae: ... the hell is a grackle?
<<end>>
===
title: BookshelfAct2.1
tags: 
colorID: 3
position: -1679,-2421
---
Mae: This bookshelf is like the ghost of Granddad #line:281559
Mae: hanging out in the corner. #line:6edba0
Mae: Maybe not his ghost. #line:67b54d
Mae: Like, his skeleton. #line:3c467a
Mae: =_= :( #line:2f6dab
Mae: ugh ok I just made myself sad #line:cd16ed
Mae: Why did I come over here? #line:280674
Mae: :| o_o #line:b8dd2f
<<unlockDoodle GrandadComeBackToLife>>
[[CheckForSafeCombination]]
===
title: BookshelfAct2.2
tags: 
colorID: 3
position: -1674,-2196
---
Mae: Oh yeah! #line:399912
Mae: Let's see... -_- #line:571c32
Mae: Where is it... #line:98a457
Mae: ... #line:fa8847
Mae: ... #line:7dae04
Mae: o_o #line:fd4542
Mae: Oh yeah. Here it is. #line:7b26b9
Mae: "The Floating Island And Other Stories". #line:1cc1cc
Mae: Granddad didn't read out of this. #line:962fdf
Mae: He, like, recited it. From memory. #line:7c1801
Mae: And made stuff up on top of that. #line:4e0e09
Mae: To get me to sleep when I was a kid. #line:584b94
Mae: ... #line:a70559
Mae: -_- :) #line:61d1e2
Mae: Miss you, dude. #line:543a23
Mae: :| o_o #line:1a99c0
===
title: BookshelfAct2.3
tags: 
colorID: 3
position: -1671,-1971
---
Mae: The Floating Island.
Mae: geez.
Mae: i'm old.
<<end>>
===
title: BookshelfAct3.1
tags: 
colorID: 4
position: -1444,-2425
---
Mae: Do do do we have any detective books out here?
Mae: oh of course. of course we do.
[[CheckForSafeCombination]]
===
title: BookshelfAct3.2
tags: 
colorID: 4
position: -1446,-2199
---
Mae: Doooo we have any books about ghosts?
Mae: like real life ghosts?
Mae: dooooo we have any books for nutjobs I guess?
===
title: BookshelfAct3.3
tags: 
colorID: 4
position: -1446,-1981
---
Mae: No, this is good.
Mae: research is good.
Mae: confront the problem head on.
Mae: you're not losing it, dude.
Mae: you are in fact keeping it
Mae: very close.
<<end>>
===
title: BookshelfAct4.1
tags: 
colorID: 6
position: -1172,-2401
---
Mae: I should like
Mae: re-read some of these.
Mae: i mean i got time.
Mae: and the cozy months are coming.
===
title: BookshelfAct4.2
tags: 
colorID: 6
position: -1168,-2180
---
Mae: "Shapes From Beyond The Veil Of Space"
Mae: ok yeah. let's go with that one.
===
title: BookshelfAct4.3
tags: 
colorID: 6
position: -1176,-1952
---
Mae: let's see what the back of the book says.
Mae: Released in 1937,
Mae: "Shapes From Beyond The Veil Of Space" is a towering achievement
Mae: and marks the start of the weird gothic genre.
Mae: an influence on much of 20th and 21st century horror,
Mae: science fiction,
Mae: and pulp literature,
Mae: "Shapes" is sure to make you question
Mae: just how far away the night sky is
Mae: from you
Mae: at this moment."
Mae: Cool.
<<end>>
===
title: GrandfatherClockAct1.1
tags: 
colorID: 2
position: -1935,-1565
---
Mae: {align=middle,locator=clock} There are a few things hanging around in here from the Old Country.
Mae: {align=middle,locator=clock} This clock, the trunk in the storage room
Mae: {align=middle,locator=clock} and whatever is buried under the house after the cement incident.
===
title: GrandfatherClockAct1.2
tags: 
colorID: 2
position: -1945,-1344
---
Mae: {align=middle,locator=clock} This thing goes off randomly every month or so.
Mae: {align=middle,locator=clock} Little wooden duck pops out. Makes the weirdest sound.
Mae: {align=middle,locator=clock} Scares dad half to death every time.
Mae: {align=middle,locator=clock} Used to joke it was Granddad getting even with him from beyond the grave.
<<end>>
===
title: GrandfatherClockAct2.1
tags: 
colorID: 3
position: -1678,-1561
---
Mae: {align=middle,locator=clock} god. this thing is intense.
Mae: {align=middle,locator=clock} weird how big clocks are like
Mae: {align=middle,locator=clock} luxury furniture now
Mae: {align=middle,locator=clock} since everything has a clock and internet on it.
Mae: {align=middle,locator=clock} like having something big that only does one thing
Mae: {align=middle,locator=clock} is like
Mae: {align=middle,locator=clock} a statement or something.
===
title: GrandfatherClockAct2.2
tags: 
colorID: 3
position: -1671,-1340
---
Mae: {align=middle,locator=clock} Wonder how old this thing is?
<<end>>
===
title: GrandfatherClockAct3.1
tags: 
colorID: 4
position: -1430,-1558
---
Mae: {align=middle,locator=clock} I wonder if the new working clock downstairs
Mae: {align=middle,locator=clock} thinks this clock is scary.
Mae: {align=middle,locator=clock} Big old thing upstairs
Mae: {align=middle,locator=clock} Like you, but different, huge, and spooky
Mae: {align=middle,locator=clock} making weird noises at night.
===
title: GrandfatherClockAct3.2
tags: 
colorID: 4
position: -1424,-1336
---
Mae: {align=middle,locator=clock}Aw, I can't find this thing spooky.
Mae: {align=middle,locator=clock}It's a piece of granddad.
Mae: {align=middle,locator=clock}we need more of those.
<<end>>
===
title: GrandfatherClockAct4.1
tags: 
colorID: 6
position: -1182,-1565
---
Mae: {align=middle,locator=clock} Hey Granddad's clock.
===
title: GrandfatherClockAct4.2
tags: 
colorID: 6
position: -1178,-1353
---
Mae: {align=middle,locator=clock}Hey Granddad.
<<end>>
===
