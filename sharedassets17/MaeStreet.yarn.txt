title: Pole1_A0D0
tags: demo
colorID: 0
position: -8847,-1879
---
Mae: {width=2,size=.8,align=middle,locator=Pole1} I remember the time in 9th grade
Mae: {width=2,size=.8,align=middle,locator=Pole1}when I snuck out and could see into Tom Bramwell's bedroom.
<<wait .25>>
Mae: {width=2,size=.8,align=middle,locator=Pole1,emote=butt,emoteAlign=middle}:\
<<wait .25>>
Mae: {width=2,size=.8,align=middle,locator=Pole1} Yep that sure did happen. :|
<<set $pole1_done to 1>>
===
title: Pole2_A0D0
tags: demo
colorID: 0
position: -8185,-1888
---
Mae: {align=middle,nearestLocator}I could jump down on Mr. Twigmeyer from here.
Mae: {align=middle,nearestLocator}It would easily kill him.
<<wait .25>>
Mae: {align=middle,nearestLocator}I hold your life in my hands, Twigmeyer. 
<<end>>
===
title: OnTree_A0D0
tags: demo
colorID: 0
position: -7090,-631
---
Penderson: {locator=Penderson2,align=middle}[size=.8]Wait, what are you doing...[/size]

===
title: OnYell_A0D0
tags: demo
colorID: 0
position: -4558,-507
---
<<if $penderson is 0>>
    <<set $penderson to 1>>
    <<camera lookOffset .8 .25>>
    Penderson: {size=1.4,locator=Penderson} [shake=.04]Hey! You [color=aaaaaa]kid![/color][/shake]
    Mae: Yes, Mr Penderson?
    Penderson: {size=1.2,locator=Penderson}[shake=.04]You stay off my roof![/shake]
    Mae: I have no idea what you mean.
    Penderson: {locator=Penderson}I seen you. [stomp]Stomping[/stomp] all over my roof. [hop]Jumping[/hop] all over my chimney.
    [[Ok, well I have to go now...|OnYellEnd]]
    [[What's your problem, Penderson?|OnYellClimbers]]
<<endif>>


===
title: Penderson_A0D0
tags: demo
colorID: 0
position: -7082,-926
---
Penderson: [size=.6]...freeloadin' trash mammals...[/size]

===
title: OnYellClimbers
tags: demo
colorID: 0
position: -4330,-145
---
Penderson: Should be a [speed=.5][color=aaaaaa]tax[/all] on you cats, squirrels, mice...
Penderson: {locator=Penderson,animation=Talk2}the whole lot of you [speed=.4][color=aaaaaa]climber types.[/all]
Mae: =_= o>
Mae: [speed=.4][size=.65]Lovely.[/all] 
Penderson: {width=3.5,locator=Penderson}Running up on the [speed=.75][color=aaaaaa]power lines[/all], ruining the [speed=.75][color=aaaaaa]infrastructure[/all], paying the [speed=.75][color=aaaaaa]same taxes[/all] as everyone else. [speed=.4][wave]Ain't Right![/wave]
Mae: o_o _o_
Mae: OK listen, I have to go live my young life.
<<addToJournal PendersonIsAWalkingReminder>>
<<addToJournal TodoAvoidTorturePit>>
[[OnYellEnd]]



===
title: OnWire_A0D0
tags: demo
colorID: 0
position: -7076,-370
---
Penderson: {width=1,size=1,locator=PendersonWire} [shake=.04]Hey![/shake] What did I [color=aaaaaa]just say!![/all]

===
title: OnYellEnd
tags: demo
colorID: 0
position: -4726,-149
---
Penderson: {locator=Penderson}AND YOU TELL YOUR FOX FRIEND I KNOW WHAT HE DID.
Mae: You mean Gregg?
Mae: What's your problem with Gregg?
Penderson: {locator=Penderson}[shake=.04]DIRTY FOX STOLE MY GARDEN GNOME!!![/shake]
Penderson: {locator=Penderson}I saw his fancy little leather jacket and his dainty little boots disappear through the hedge.
Penderson: {locator=Penderson}Everybody knows about him.
Mae: [speed=.5]Everyone loves Gregg. Gregg's the bomb.[/speed]=_= o>
Penderson: {locator=Penderson}You tell him I'm calling the cops.
Penderson: {locator=Penderson}And I'll do the same for you if I see you up on the powerlines again!
Penderson: {locator=Penderson}You kids...
Mae: Adults. We're adults. We run the world now. o_o _o_
Penderson: {locator=Penderson,size=1.2,animation=Pah}[color=aaaaaa][shake=.04]Pah![/all]
Mae: {size=1.2}[color=aaaaaa][shake=.04]Pah![/all]:( \o/
Mae: :| _o_
<<addToJournal TodoFindGregg>>
<<addToJournal PendersonCops>>
<<unlockDoodle FindGregg>>
<<unlockDoodle Gnome>>
<<unlockDoodle Penderson>>
<<camera lookOffset 0 0>>
===
title: InitLevel
tags: 
colorID: 0
position: -6865,-1486
---
<<set $taxi_stop to 0>>
===
title: Twigmeyer_A1D1
tags: 
colorID: 2
position: -3275,-386
---
Mae: Hey Mr Twigmeyer!
Twigmeyer: Mae? Zat you?
[[Sure is!|Twigmeyer_00_SureIs]]
[[Nope!|Twigmeyer_00_Nope]]
===
title: Twigmeyer_00_SureIs
tags: 
colorID: 2
position: -3604,-101
---
Twigmeyer: You home for a visit?
Mae: No, I'm home for good!
Twigmeyer: What, they not have college anymore?
[[Nope. It burned down|Twigmeyer_00_ItBurnedDown]]
[[Not for me at least.|Twigmeyer_00_NotForMe]]
===
title: Twigmeyer_00_NotForMe
tags: 
colorID: 2
position: -3389,210
---
Twigmeyer: I always thought you'd either go on to do great things
Twigmeyer: or sit on your ass here until it put down roots.
Mae: Ass roots?
Twigmeyer: Guess I can settle that one in my mind.
Mae: [speed=.25]...[/all]
Mae: [speed=.25]...see you around...[/all]
<<set $twigmeyer_done to 1>>
===
title: Twigmeyer_00_ItBurnedDown
tags: 
colorID: 2
position: -3781,220
---
Twigmeyer: Well I'm sure you burned it down.
Mae: These allegations, Mr Twigmeyer.
Twigmeyer: I ain't allegatin'
Twigmeyer: Just try not to burn my lawn down, you firebug.
Mae: No promises.
<<set $twigmeyer_done to 1>>
===
title: Twigmeyer_00_Nope
tags: 
colorID: 2
position: -2936,-82
---
Twigmeyer: Well it looks like Mae.
Twigmeyer: Bet it smells like Mae.
Mae: I have a smell?
Twigmeyer: When you were a kid,
Twigmeyer: Susan used to call you a little dirtball.
[[Well I was a dirtball.|Twigmeyer_00_IWasADirtball]]
[[I was never a dirtball.|Twigmeyer_00_IWasNever]]
===
title: Twigmeyer_00_IWasADirtball
tags: 
colorID: 2
position: -3071,194
---
Twigmeyer: You still are a dirtball I bet.
Mae: Cradle to grave. 100 percent dirtball.
Twigmeyer: That's the spirit!
Mae: See you around, Mr Twigmeyer!
<<set $twigmeyer_done to 1>>
===
title: Twigmeyer_00_IWasNever
tags: 
colorID: 2
position: -2691,181
---
Twigmeyer: Aw Mae, you were our dirtball.
Mae: Every child's dream.
Twigmeyer: See? Count your every blessing.
Mae: Well, you can *COUNT* on seeing me around tomorrow.
Twigmeyer: Feel free to rake my lawn.
Mae: You paying?
Twigmeyer: Get out of here, dirtball.
<<set $twigmeyer_done to 1>>
===
title: InitLevel_A1D1
tags: 
colorID: 2
position: -6571,-1559
---
<<show Twigmeyer>>
<<setAnimInt Twigmeyer raking 1>>
===
title: Construction
tags: demo2
colorID: 0
position: -6598,-1226
---
Mae: Well it sure is fall. #line:9dd5f6
Mae: All the roadwork crammed in before winter. #line:ae1c9f
Mae: Makes like half the town inaccessible. #line:5ad7a0
Mae: Unless you wanna walk the long way around #line:29a6a9
Mae: for like 20 minutes. #line:05ca88
Mae: Which, I mean, no. #line:1b963f
<<end>>
===
title: TwigmeyerDemo
tags: demo
colorID: 0
position: -5782,-631
---
<<if $twigmeyer is 0>>
    Mae: Hey Mr Twigmeyer!
    Twigmeyer: {align=middle,emote=rake,emoteAlign=middle}
    Mae: Alright then!
    <<set $twigmeyer to 1>>
<<else>>
    Mae: Rake 'em good, Mr. Twigmeyer!
<<endif>>

===
title: InitLevel_A0D0
tags: 
colorID: 0
position: -7074,-1569
---
<<show Penderson>>
<<show Twigmeyer>>
<<setAnimInt Twigmeyer raking 1>>
===
title: Penderson
tags: 
colorID: 0
position: -6738,-501
---
<<if $act is 1 and $day is 0>>
    [[PendersonDemo]]
<<endif>>
===
title: Penderson_00_00
tags: 
colorID: 2
position: -5007,879
---
Penderson: HEY! YOU KID!
Mae: Adult.
Penderson: So you’re back, eh?
Mae: Yes, Mr. Penderson.
Penderson: Didn’t last long, eh?
Mae: No, Mr. Penderson
Penderson: You get a job yet?
[[I’ve only been back for like 24 hours.|BackFor24Hours]]
[[Yes. I’ve been elected mayor.|ElectedMayor]]
===
title: BackFor24Hours
tags: 
colorID: 2
position: -5263,1169
---
Penderson: Soon it'll be "only a week"
Penderson: then a month
Penderson: then your whole life.
Mae: You're right. I have so much time left. =_= :)
<<wait 1>>
Mae: o_o :|
[[Penderson_00_00_End]]
===
title: ElectedMayor
tags: 
colorID: 2
position: -4754,1164
---
Penderson: Hhmph. Couldn't do worse than the current one.
Mae: And I've ordered your house to be bulldozed.
Penderson: What, finishing the job you started?
Mae: I hope you enjoy living in the woods!
Penderson: Those eggs took the paint off the vinyl siding.
Mae: Scavenge food! Commune with the beasts, Mr Penderson!
Penderson: Watch yourself, lil miss.
Mae: "Lil. Effing. Miss?"
[[Penderson_00_00_End]]
===
title: Penderson_00_00_End
tags: 
colorID: 2
position: -5020,1455
---
Penderson: No one's forgotten who you are and what you did, you know.
Penderson: They may act like they have.
Penderson: But that's just small town polite right there.
Mae: Is that what this is?
Penderson: Small town polite's all you got kid.
Penderson: So watch it.
Mae: Well that was really interesting
Mae: but I need to go listen to my cells die.
Penderson: PAH!
Mae: PAH!
===
title: PendersonDemo
tags: demo
colorID: 0
position: -6748,-232
---
Penderson: [size=.6]...freeloadin' trash mammals...[/size]
===
title: TriggerMolly_A1D3
tags: 
colorID: 2
position: -6038,-1267
---
<<set $trigger_molly to 0>>
Molly: Hey Mae.
Mae: What?
Molly: I was just stopping by to see how you were doing.
Mae: Why?
Molly: After the other night I mean.
Mae: Why wouldn’t I be fine?
Molly: Mae, a lot of things that happen around here,
Molly: sometimes in town, sometimes out by the tracks,
Molly: sometimes up in the hills...
Molly: ...
Mae: What?
Molly: It's my job to make sure folks like you
Molly: never see something like what you saw the other night.
[[You’re not doing a very good job of it.|NotDoingAVeryGoodJobOfIt]]
[["Folks like you"?|MollyA1D3_01]]
===
title: NotDoingAVeryGoodJobOfIt
tags: 
colorID: 2
position: -6198,-967
---
Molly: Better than you might think.
Mae: I’ve been back less than 3 days and I already saw dead body parts.
Mae: Which wasn't a big deal, but
Molly: You didn't see Tom Jennings shoot his brother in the stomach
Molly: last night out on Doty Line Road. Out past Arner.
Molly: You didn't have to walk out into that cornfield.
Mae: Geez.
[[MollyA1D3_01]]
===
title: MollyA1D3_01
tags: 
colorID: 2
position: -5954,-945
---
Molly: You're a kid, Mae.
Molly: And you're naive.
Molly: And in its own stupid way, that's beautiful.
Molly: But the world isn't.
Molly: And the world isn’t out there somewhere.
Molly: It's here.
Mae: Alright, are you gonna arrest me or something?
Mae: Because I have a party to go to.
Molly: Watch yourself, Mae.
Molly: I'd really hate to see something happen to you.
Molly: And being in the wrong place at the wrong time is all it takes.
Mae: ...
Molly: That's all I'm saying.
Mae: That's ominous.
Molly: Good night, Mae.
<<close>>
<<walk Molly MollyOffscreenLeft>>
<<wait 4>>
<<hide Molly>>
<<unlockDoodle MollyA1D3>>
===
title: InitLevel_A1D2
tags: 
colorID: 2
position: -6308,-1566
---
<<show Penderson>>
===
title: InitLevel_A1D3
tags: 
colorID: 2
position: -6009,-1575
---
<<if $trigger_molly is 1>>
    <<show Molly>>
    <<setCanInteract Molly 0>>
<<endif>>
===
title: InitLevel_A2D1
tags: 
colorID: 3
position: -5699,-1585
---
<<if $demo2>>
    <<show Twigmeyer>>
    <<setAnimInt Twigmeyer raking 1>>
    <<hide Penderson>>
<<endif>>
===
title: InitLevel_A2D2
tags: 
colorID: 3
position: -5417,-1589
---

===
title: InitLevel_A2D3
tags: 
colorID: 3
position: -5172,-1594
---

===
title: InitLevel_A2D4
tags: 
colorID: 3
position: -4905,-1599
---

===
title: OnMoveForHarleys
tags: 
colorID: 0
position: -6486,-966
---
<<if $harleys_ran_by is 0>>
    <<set $harleys_ran_by to 1>>
    <<show Harley1>>
    <<show Harley2>>
    <<show Harley3>>
    <<warp Harley1 Harley1Start>>
    <<warp Harley2 Harley2Start>>
    <<warp Harley3 Harley3Start>>
    <<flip Harley1 -1>>
    <<flip Harley2 -1>>
    <<flip Harley3 -1>>
    <<setInteract Harley1 0>>
    <<setInteract Harley2 0>>
    <<setInteract Harley3 0>>
    <<run Harley1 Harley1End>>
    <<run Harley2 Harley2End>>
    <<run Harley3 Harley3End>>
    <<trackAllSpeechBubblesToCharacters 1>>
    <<setSpriteLayer Harley1 CharacterFG>>
    <<setSpriteLayer Harley2 CharacterFG>>
    <<setSpriteLayer Harley3 CharacterFG>>
    Harley1: Kill Kill Kill!
    Harley3: FBI freeze!
    <<trackAllSpeechBubblesToCharacters 0>>
<<endif>>
===
title: OnMoveForPenderson
tags: 
colorID: 0
position: -5004,589
---
<<set $penderson += 1>>
<<set $penderson_done to 1>>
[[Penderson_00_00]]
===
title: RightSide
tags: demo
colorID: 0
position: -7106,-90
---
Mae: Nah, not feelin' the graveyard today.
Mae: I feel like you might need Bea for the full graveyard experience.
Mae: That was not a joke about her mother.
===
title: ExitLevel
tags: 
colorID: 0
position: -5906,-1927
---
<<if $scene_link_id != 99>>
    <<set $mom_home to 0>>
<<endif>>
===
title: PreInitLevel
tags: 
colorID: 0
position: -6200,-2161
---
<<hide Molly>>
<<hide Penderson>>
<<hide Twigmeyer>>
<<hide CopCar>>
<<hide Dad>>


===
title: InitLevel_A3D4
tags: 
colorID: 4
position: -4116,-1593
---
<<if $molly_done is 0>>
    <<show CopCar>>
    <<show Molly>>
    <<setCanInteract Molly 0>>
    <<warp Molly MollyLeft>>
    <<flip Molly 1>>
<<endif>>
===
title: TriggerMolly_A3D4
tags: 
colorID: 4
position: -4116,-1302
---
<<playerFace -1>>
Mae: oh god.
Mae: what do *YOU* want?
<<wait 3>>
Molly: hello to you too, mae.
Mae: well?
Molly: I wanted to let you know.
Molly: we ran a test on that arm you guys found a few days back.
Mae: oh really???
Molly: yeah. thought you'd be interested is all.
Mae: definitely!
Molly: arm belonged to a middle aged man
Molly: and medium build.
Molly: and he was dead when it was removed.
Mae: whoa.
Mae: that means...
Molly: it doesn't mean anything yet.
Molly: i just thought you'd like to know.
Molly: you always liked detective stories when you were a kid.
Mae: ok. yeah. thanks.
Mae: this ghost is seriously messed up.
Molly: ghost?
Molly: wasn't no ghost involved in this, whatever it was.
Mae: yeah yeah. nobody believes me. i get it.
Molly: be careful, mae.
Molly: strange stuff going on.
Mae: yeah you're telling me.
Molly: have a nice evening, mae.
<<set $molly_done to 1>>
===
title: Pole1Act1.1
tags: 
colorID: 2
position: -8994,-1077
---
Mae: I remember the time in 9th grade
Mae: when I snuck out and could see into Tom Bramwell's bedroom.
<<wait .25>>
Mae:{emote=butt} :\
<<wait .25>>
Mae: Yep that sure did happen. :|
<<end>>
===
title: Pole1Act2.1
tags: demo2
colorID: 3
position: -8999,-834
---
Mae: When the flood happened that one year this pole fell over #line:ed72bd
Mae: and I was scared it’d somehow electrify the water all over the neighborhood #line:388fe4
Mae: and it’d stay that way #line:c64377
Mae: and I was kinda afraid of taking a shower #line:1aa17c
Mae: or turning on the faucet for weeks. #line:4d1b8d
===
title: InitLevel_A4D3
tags: 
colorID: 5
position: -3401,-1574
---
<<show Dad>>
===
title: Dad_A4D3_00
tags: 
colorID: 5
position: -3574,-957
---
Mae: hey dad.
Mae: no work today?
Dad: i told bob to shove it.
Mae: really?
Dad: no.
Dad: …
Mae: you ok?
Dad: yeah, i’ll be fine.
Dad: not gonna sugarcoat it. I hate my job.
Mae: aw dad. Why?
Dad: i’ve mined, i’ve made glass, i’ve done a heap other stuff.
Dad: but this place… they just don’t respect you for workin’.
Mae: they don’t?
Dad: Job’s s’posed to pay you enough to live
Dad: Job’s s’posed to let you provide for your family
Dad: Job’s s’posed to have regular hours
Dad: Boss’ s’posed to respect you.
Dad: Workers s’posed to be able to talk
Dad: Workers s’posed to be able to have a life
Dad: Workers s’posed to be able to live.
Dad: Your mom always had to work but
Dad: I wanted to give you and her your time
Dad: I wanted her to have hobbies
Dad: and enough time and money to do them
Dad: that’s what made me happy
Dad: now i just work so i can keep our health insurance
Dad: and hold onto the house. Try to at least.
<<if $did_germ_friendship_quest_3>>
	Mae: my friend Germ
	Mae: his dad works for department of transportation
	Mae: he talked about you getting a union going at Ham Panther
	Dad: they fire you just for talking about it
	Mae: is that legal?
	Dad: legal don’t always matter.
<<endif>>
Mae: is there anything you can do?
Dad: i mean, we *do* need a union
Dad: it’s just easier to say it than get it done
Dad: unions were always there in the mines and the factory
Dad: least for a while
Dad: but here…
<<if $has_tooth and $knows_tooth>>
	//if have tooth and know what tooth is <-variable set at microfiche if you see item//
	Mae: i got something for ya
	Mae: got it out of the basement
	Dad: how’d you get in the basement?
	Mae: door in the crawlspace
	Dad: that’s spooky.
	Mae: so what i got is…
	Mae: a tooth.
	Dad: a what?
	Mae: do you know what this tooth is?
	Dad: no… really? 
	Dad: you sure?
	Mae: yeah. Granddad musta had it from the old days.
	Dad: Mae… thank you.
	Dad: wow. Looks bright and new
	Dad: like they just pulled it out of his skull yesterday.
	Mae: you show that to your boss and tell him to eat shit.
	Dad: MAE!
	Mae: sorry.
	Dad: I’ll just keep it with me.
	Mae: sure thing.
	Dad: Mae.
	Mae: yeah?
	Dad: you come down and picket with us if we walk out?
	Mae: i will breathe fire, dad.
	Dad: love you, kitten.
	Mae: be back later, dad.
    <<unlockAchievement Clock_Quest>>
    <<set $did_clock_quest to 1>>
<<else>>
	//if don’t have tooth or know what it is//
	Mae: listen, i’m gonna be around tonight for dinner
	Dad: wow! What’s the occasion?
	Mae: well mom said we could make breakfast
	Dad: oooh! Sounds good!
	Mae: i was gonna talk about some… stuff.
	Dad: ok
	Mae: maybe we can talk about your stuff too?
	Mae: just one big… uh… stuff session?
	Dad: that sounds great kitten.
	Mae: love you dad
	Dad: love you too
	Mae: be back later!
<<endif>>
===
title: TwigmeyerDemo2
tags: demo2
colorID: 0
position: -4630,-935
---
<<if $twigmeyer is 0>>
    Mae: Hey Mr. Twigmeyer. #line:20bdd4
    Twigmeyer: Hello. #line:4a0f21
    Mae: Raking the... uh... #line:e6708c
    -> Grass? #line:1d9459
    -> Nothing? #line:e98b12
    Twigmeyer: Lawns are like people. #line:457d87
    Mae: and trees are like friends. Or something. #line:dbe5c2
    Twigmeyer: Ok. #line:7f99fc
    <<set $twigmeyer to 1>>
<<elseif $twigmeyer is 1>>
    Mae: Wait. How are lawns like people? #line:19cfee
    Twigmeyer: Gettin’ late in the year #line:65cafd
    Twigmeyer: and we ain’t gonna see each other much #line:d3d02f
    Twigmeyer: so I’m out here putting in that good time. #line:0a238c
    Mae: Huh. #line:eac70f
    Mae: You have a pretty strong bond with your grass. #line:9fec42
    Twigmeyer: Heh heh well I grab hold of just about everything I can. #line:9ceef1
    <<set $twigmeyer_done to 1>>
<<endif>>

===
title: Pole1Act2.2
tags: demo2
colorID: 3
position: -8743,-842
---
Mae: After the flood it laid in our yard for 2 weeks #line:8d78d2
Mae: like a big dead... thing. #line:540544
Mae: But then they put it back up #line:dbd8e5
Mae: and I could see it from my window at night #line:e1b9a3
Mae: and it felt spooky for some reason. #line:453016
===
title: Pole1Act2.3
tags: demo2
colorID: 3
position: -8498,-853
---
Mae: Spooky pole. #line:01ea1c
Mae: Undead friend. #line:095f86
Mae: Watching me from the curb. #line:147e8b
<<end>>
===
title: Penderson_A2D1
tags: demo2
colorID: 0
position: -5656,-1266
---
Penderson: Bah! #line:48d069
Mae: Bah! \o/ #line:5c557b
Mae: _o_ #line:747a7c
<<set $penderson_done to 1>>
===
title: InitLevel_A4D2
tags: 
colorID: 5
position: -3685,-1571
---
<<hide Twigmeyer>>
===
title: Dad_A4D3
tags: 
colorID: 5
position: -3328,-1299
---
<<if $dad is 0>>
    <<set $dad += 1>>
    [[Dad_A4D3_00]]
<<elseif $dad is 1>>
    <<set $dad += 1>>
    [[Dad_A4D3_01]]
<<else>>
    [[Dad_A4D3_Loop]]
<<endif>>
===
title: Dad_A4D3_Loop
tags: 
colorID: 5
position: -2922,-985
---
Dad: dum de dum dum
===
title: Dad_A4D3_01
tags: 
colorID: 5
position: -3295,-958
---
Mae: what are you doing out here, by the way?
Dad: getting an early start on our Longest Night lights plan
Dad: gotta dig them out of the closet
Dad: see if they even work still
Mae: can i help put them up?????
Dad: we’ll see. You’re a small fry.
Mae: DAD.
===
title: Pole1Act3.1
tags: 
colorID: 4
position: -8991,-593
---
Mae: Can see Penderson's house from here.
Mae: What a ghoul that guy is.
Mae: he has hated me forever
Mae: just because i put an m80 down his roof gutter.
Mae: i mean it's just petty, this grudge.
Mae: that was like forever ago.
<<end>>
===
title: Pole1Act4.1
tags: 
colorID: 6
position: -8995,-347
---
Mae: oh wow the Kareems got a new dog!
Mae: hey puppers!
Mae: aw see saw me!
Mae: i can see you from here!
Mae: i will be watching!
Mae: so [shake=.05]beware[shake].
Mae: and be a good pup.
<<end>>
===
title: Twigmeyer_A2D1
tags: 
colorID: 2
position: -2577,-396
---
Twigmeyer: rake rake rake
Mae: rake rake rake?
Twigmeyer: that's how you rake!
<<set $twigmeyer_done to 1>>
===
title: Twigmeyer_A2D3
tags: 
colorID: 3
position: -2272,-389
---
Twigmeyer: goddamn leaves!
Mae: they should leave your yard alone.
Twigmeyer: mae borowski you leave those puns at home.
Mae: ha ha ha
<<set $twigmeyer_done to 1>>
===
title: Twigmeyer_A3D2
tags: 
colorID: 3
position: -1958,-384
---
Twigmeyer: Winds gettin' colder
Twigmeyer: more leaves, in my dang yard.
Mae: what's the big deal?
Twigmeyer: it kills the grass is what!!!!
Mae: ok ok! geez!
Twigmeyer: i'm fired up!!!
<<set $twigmeyer_done to 1>>
===
title: Twigmeyer_A3D4
tags: 
colorID: 3
position: -1686,-378
---
Twigmeyer: you alright, mae?
Mae: yeah. i'm fine.
Twigmeyer: i got a sense about people.
Twigmeyer: and you're not alright.
Mae: well i hate it break it to you
Mae: but your sense is wrong.
Twigmeyer: hm. we'll see.
<<set $twigmeyer_done to 1>>
===
title: Twigmeyer_A4D3
tags: 
colorID: 6
position: -1439,-371
---
Mae: hey mr twigmeyer!
Twigmeyer: well hey there mae!
Twigmeyer: you seem to be doing a lot better!
Mae: eh. I'm really sore.
Twigmeyer: you gotta take care!
Twigmeyer: life breaks easy.
Mae: nah. i'm forever.
Twigmeyer: heh heh. that'd be nice.
<<set $twigmeyer_done to 1>>
===
title: TriggerMollyTop_A3D4
tags: 
colorID: 4
position: -4388,-1319
---
<<set $trigger_molly to 0>>
Molly: Mae! Get down from those powerlines!
Mae: ...?
<<fadeOut 1 wait>>
<<jump>>
<<warp Mae MaeByMollyA3D4>>
<<playerFace -1>>
<<playIdleAnimationPlayer>>
<<fadeIn 1 wait>>
[[TriggerMolly_A3D4]]
===
