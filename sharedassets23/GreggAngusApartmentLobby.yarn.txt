title: EnterLevel
tags: 
colorID: 0
position: 1,-107
---
<<if $take_elevator is 1>>
    <<warp Mae MaeAtIntercom>>
    [[TakeElevator]]
<<else>>
    <<if $act is 2 and $day is 2 and $gregg_friendship_quest is 2>>
        [[GreggFQ2Intro]]
    <<endif>>
<<endif>>
===
title: TakeElevator
tags: 
colorID: 0
position: -51,246
---
<<set $take_elevator to 0>>
<<playerFace -1>>
<<wait 1>>
<<play SfxElevatorOpen>>
<<wait 1>>
<<animationTrigger Elevator Open>>
<<wait 2.5>>
<<walk Mae MaeAtElevator>>
<<wait .1>>
<<fadeOut 1>>
<<wait 1>>
<<scene GreggAngusApartment>>
===
title: InitLevel
tags: 
colorID: 0
position: -1,-368
---
<<hide Gregg>>
<<if $act is 2 and $day is 2 and $gregg_friendship_quest is 2>>
    <<show Gregg>>
    <<warp Mae MaeWithGregg>>
    <<playerFace -1>>
    <<cameraLookAt CameraCenter>>
<<endif>>
===
title: GreggFQ2Intro
tags: 
colorID: 0
position: 375,-27
---
<<set $gregg_hat to 1>>
<<playMusic gregg_manic 3>>
Gregg: the bullet went clean through!
Gregg: you can still see the bullet hole in it.
Mae: how did that not kill him?
Gregg: it musta missed all the important parts of the brain.
Mae: wow.
Gregg: Yeah Great-great granddad was pretty tough.
Mae: But isn't that the helmet the fascists were wearing?
Gregg: Ugh! No! Wrong war!
Gregg: And wrong country!
Gregg: These weren’t the bad guys!
Gregg: READ SOME HISTORY.
Mae: GEEZ! OK!
Mae: When did you learn all this stuff?
Gregg: When people kept saying it was a fascist hat!
Mae: So what are we doing?
<<if $did_gregg_friendship_quest_1>>
	Gregg: Continuing our crime spree.
	Mae: Ooooh yeah.
<<else>>
	Gregg: You up for a crime?
	Mae: Boy, am I ever.
	Mae: I mean, I kinda did some last night.
	Gregg: Oh really? Without me?
	Mae: Aw dude.
	Mae: You’re always my number one crimefriend.
	Gregg: That’s beautiful, dude. Likewise.
	Mae: So what’s the plan?
<<endif>>
Gregg: Follow me!
Mae: YES DEAR LEADER
Gregg: i am both a leader, and a dear.
<<close>>
<<walk Mae MaeRight>>
<<walk Gregg GreggRight>>
<<wait 1>>
<<irisOut 1 wait>>
<<scene GreggAngusParkingLot>>
===
