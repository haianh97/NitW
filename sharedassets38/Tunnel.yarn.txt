title: TunnelTeens
tags: 
colorID: 0
position: 7467,-9822
---
<<set $tunnel_teens_done to 1>>
<<if $met_tunnel_teens is 0>>
    [[TunnelTeens_Intro]]
<<else>>
    // A1D1, A1D3, A2D2, A2D4, A3D1, A3D3, A4D3 //
    <<if $act is 1 and $day is 1>>
        [[TunnelTeens_A1D1]]
    <<elseif $act is 1 and $day is 3>>
        [[TunnelTeens_A1D3]]
    <<elseif $act is 2 and $day is 2>>
        [[TunnelTeens_A2D2]]
    <<elseif $act is 3 and $day is 1>>
        [[TunnelTeens_A3D1]]
    <<elseif $act is 3 and $day is 3>>
        [[TunnelTeens_A3D3]]
    <<elseif $act is 4 and $day is 3>>
        [[TunnelTeens_A4D3]]
    <<endif>>
<<endif>>
===
title: Old_TunnelTeenStairs
tags: 
colorID: 0
position: 7053,-8551
---
<<if $tunnel_teens_no_talk is 1>>
    <<set $tunnel_teens_no_talk to 1>>
    Mae: I’m gonna need some kind of teen interpreter
    Mae: if I’m gonna come down here.
    Mae: My god, I’m 20 and they’re already like aliens.
    Mae: Oh my old bones.
<<endif>>
===
title: InitLevel
tags: 
colorID: 0
position: 8999,-11125
---
<<if $act is 2 and $day is 1 and $tunnel_west_city_council_a2d1_done is 0>>
    <<setCanInteract Molly 0>>
    <<setCanInteract Council1 1>>
    <<show Council1>>
    <<if $demo2>>
        <<hide Council2>>
        <<hide Council3>>
        <<hide Council4>>
    <<else>>
        <<show Council2>>
        <<show Council3>>
        <<show Council4>>
    <<endif>>
<<else>>
    <<hide Council1>>
    <<hide Council2>>
    <<hide Council3>>
    <<hide Council4>>
<<endif>>

<<if $act is 2 and $day is 1 and $tunnel_west_city_council_a2d1_done is 0>>
    <<show Molly>>
<<else>>
    <<hide Molly>>
<<endif>>

<<if $act is 3 and $day is 2>>
    <<show Germ>>
<<else>>
    <<hide Germ>>
<<endif>>

// A1D1, A1D3, A2D2, A2D4, A3D1, A3D3, A4D3 //
<<if $act is 1 and $day is 1>>
    // show TunnelTeens
<<elseif $act is 1 and $day is 3>>
    // show TunnelTeens
<<elseif $act is 2 and $day is 2>>
    // show TunnelTeens
<<elseif $act is 3 and $day is 1>>
    // show TunnelTeens
<<elseif $act is 3 and $day is 3>>
    // show TunnelTeens
<<elseif $act is 4 and $day is 3>>
    // show TunnelTeens
<<else>>
    <<hide TunnelTeen1>>
    <<hide TunnelTeen2>>
    <<hide TunnelTeen3>>
    <<hide TunnelTeen4>>
<<endif>>

<<hide MissQuelcy>>
<<hide Rosa>>
<<hide Fisherman>>

<<if $act is 0>>
    <<show MsQuelcy>>
    <<set $cook_done to 1>>
<<endif>>

<<if $act is 1 and $day is 2>>
    <<show Fisherman>>
    <<setAnimInt Fisherman sitting 1>>
    //<<show Rosa>>
<<endif>>

<<if $act is 2 and $day is 1>>
    <<show Fisherman>>
    <<setAnimInt Fisherman sitting 1>>
<<endif>>

<<if $act is 2 and $day is 3>>
    <<show Fisherman>>
    <<setAnimInt Fisherman sitting 1>>
<<endif>>

<<if $act is 3 and $day is 2>>
    <<show Fisherman>>
    <<setAnimInt Fisherman sitting 1>>
<<endif>>

<<if $act is 3 and $day is 4>>
    <<show Fisherman>>
    <<setAnimInt Fisherman sitting 1>>
<<endif>>

<<if $act is 4 and $day is 3>>
    <<show Fisherman>>
    <<setAnimInt Fisherman sitting 1>>
<<endif>>

<<if $act is 4 and $day is 3 and $did_birdland_quest>>
    <<show Saleem>>
    <<show Sadie>>
    <<setCanInteract Saleem 0>>
    <<setCanInteract Sadie 0>>
<<else>>
    <<hide Saleem>>
    <<hide Sadie>>
<<endif>>


<<if $act is 2 and $day is 4>>
    // show PumpkinHeadGuy
<<else>>
    <<hide PumpkinHeadGuy>>
<<endif>>
===
title: TunnelTeen1
tags: 
colorID: 0
position: 7248,-10182
---
[[TunnelTeens]]
===
title: TunnelTeen2
tags: 
colorID: 0
position: 7483,-10239
---
[[TunnelTeens]]
===
title: TunnelTeen3
tags: 
colorID: 0
position: 7723,-10204
---
[[TunnelTeens]]
===
title: Old_TunnelTeens_01_00
tags: 
colorID: 0
position: 6757,-8589
---
Mae: Hey kids!
TunnelTeen1: Hey adult.
Mae: Well I was your age only a few years ago.
TunnelTeen2: Uh huh.
Mae: ...
Mae: Well, see ya later!
===
title: TunnelTeen4
tags: 
colorID: 0
position: 7987,-10128
---
[[TunnelTeens]]
===
title: TriggerCityCouncil_A2D1
tags: 
colorID: 3
position: 12033,-10521
---
<<if $demo2>>
    [[CityCouncil_A2D1_Demo2]]
<<else>>
    Council1: Because this mural is of historical note, Molly!
    Council1: What do we know?
    Molly: Well, sometime overnight
    Molly: someone used bolt cutters on the gate
    Molly: and came down here.
    Council1: What’d they use, a paint roller?
    Molly: Looks to be.
    Council3: Says they intend to Nuke Poss... something?
    Molly: I think it’s likely some kid
    Molly: saw one of the old Nuke Possum Springs tags from back when
    Molly: and got bored halfway through writing it.
    Council4: We need to treat this as a terrorist action.
    Molly: I’m... sorry?
    Council1: This mural is of historical note!
    Molly: Alright well I’ll round up any… uh… terrorists I see.
    Council3: Round up any dirtbag teens or vagrants that’ve been hanging around town
    Council1: check out by the abandoned Food Donkey.
    Council4: they come off the train there
    Molly: Already on it.
    Council1: Molly.
    Molly: Yeah?
    Council1: Find whoever did this.
    Molly: Ok.
    Council1: I want to see them hang.
    Molly: I think at most they’d get a fine and some jail time
    Council3: this was of historical note.
    Council4: without this I don't know if there's a reason to keep this place open
    Council3: yeah colleen we all know what you think
    Council4: what? You think I wanted this?
    Council4: it's not like any of you had ideas for this place!
    Council1: colleen, we are not going to put a rest home down here!
    Council4: it's just wasted space!
    Council4: And they're down here anyway at Trolleyside News everyday!
    Council2: We are not putting our eldery in a hole!
    Council1: Yeah colleen!
    Molly: 'Least if we closed it I wouldn't have to keep an eye on this place.
    Council3: thank you, molly. That's helpful.
    Molly: If you're all done I got things to do.
    <<close>>
    <<walk Molly MollyOffRight>>
    <<wait 3>>
    Council1: Yeah fine.
    Council3: This was of historical note.
    Council2: this sure is a picture.
    <<close>>
    <<walk Council1 CouncilOffLeft>>
    <<walk Council2 CouncilOffLeft>>
    <<walk Council3 CouncilOffLeft>>
    <<walk Council4 CouncilOffLeft wait>>
    <<hide Council1>>
    <<hide Council2>>
    <<hide Council3>>
    <<hide Council4>>
    <<hide Molly>>
<<endif>>
===
title: Old_TunnelTeens_A2D3
tags: 
colorID: 3
position: 4526,-8688
---
<<set $talked_to_teens_a2d3 to 1>>
TunnelTeen1: Hey don't you know Lori?
Mae: Yeah.
TunnelTeen1: Oh ok.
TunnelTeen2: I thought so
TunnelTeen3: So you're like a college dropout, right?
-> Mind your damn business!
    TunnelTeen2: Fine.
-> Yeah I guess.
    TunnelTeen3: What happened?
    TunnelTeen4: You fail out?
    -> Mind your damn business!
        TunnelTeen4: Fine.
    -> It just didn't work out.
        TunnelTeen4: What does that mean?
        TunnelTeen1: Yeah what?
        Mae: I had kind of a slow nervous breakdown.
        TunnelTeen1: Cool.
        Mae: Well... ok sure.
        <<set $intro_teens to 1>>
===
title: UNUSED
tags: 
colorID: 0
position: 8791,-10187
---
Mae: Hey hey! Gimme all yer pretzels!
<<wait .9>>
Mae: [size=1.25]Hey![/all]
<<wait .9>>
Mae: [shake=.05][size=1.5]Hey![/all]
Table1: Do you mind? He's in a [speed=.5]pierogi trance[/speed]. Come back later.
Mae: I come here all the time and I've never seen a pierogi trance.
Table1: He's makin' something [speed=.5]extra special[/speed], [wave]dear.[/wave] Something that ain't never been pierogi'd before.
Mae: Ugh. [color=aaaaaa]Dear.[/all]
===
title: Cook_00_00
tags: demo2
colorID: 0
position: 9646,-10664
---
Mae: Hey, I just got back in town
Mae: and the one thing I missed most was-
Cook: HEY!
Cook: I REMEMBER YOU!
Mae: :) o/
Mae: Hello!
Cook: THIEF!
Mae: :| _o_
Mae: Oh COME ON.
Mae: That was years ago!
Cook: ONCE A THIEF, ALWAYS A THIEF.
Mae: Alright, you know what? =_=
[[I’m not a thief! You take that back!|Cook_NotAThief]]
[[This isn't even a restaurant!|Cook_IsntEvenARestaurant]]
===
title: Cook
tags: 
colorID: 0
position: 9726,-10977
---
<<if $cook_sequence is 0>>
    <<if $cook is 0>>
        <<set $cook += 1>> 
        <<set $cook_done to 1>>
        [[Cook_00_00]]
    <<endif>>
<<endif>>
===
title: Cook_NotAThief
tags: demo2
colorID: 0
position: 9411,-10408
---
Mae: I’ve stolen from better places than this! o_o
Mae: I wouldn’t lower myself to steal from here!
Cook: YEAH YEAH YEAH
Cook: GO JUMP IN THE RIVER.
===
title: Cook_IsntEvenARestaurant
tags: demo2
colorID: 0
position: 9843,-10366
---
Mae: It's a crappy foodstand in a hole!
Cook: This establishment has been in business for 50 years!
Mae: 50 CRAPPY YEARS IN A HOLE! o_o
Mae: You know what else has been in a hole for 50 years?
Mae: \o/ :(
Mae: DEAD PEOPLE FROM THE 60s!
Cook: No respect! No respect, this one!
Mae: YOU CAN KEEP YOUR HOLE PRETZELS
Mae: AND YOUR DAMP PIEROGIES
Mae: _o_ :|
Cook: YEAH YEAH YEAH
Cook: GO JUMP IN THE RIVER.
===
title: RightBench
tags: demo
colorID: 0
position: 10068,-9941
---
Mae: Usually someone fishing here. For tunnel fish. Because why the hell not.
Mae: The tunnel flooded, and we lost a bit more land to the fish.
<<set $tunnel_east_right_bench_done to 1>>
===
title: Stairs
tags: demo
colorID: 0
position: 8997,-8702
---
<<if $tunnel_east_stairs is 0>>
    <<set $tunnel_east_stairs += 1>>
    [[Stairs00]]
<<elseif $tunnel_east_stairs is 1>>
    <<set $tunnel_east_stairs += 1>>
    [[Stairs01]]
<<elseif $tunnel_east_stairs is 2>>
    <<set $tunnel_east_stairs += 1>>
    <<set $tunnel_east_stairs_done to 1>>
    [[Stairs02]]
<<endif>>
===
title: Stairs00
tags: demo
colorID: 0
position: 8866,-8420
---
Mae: I still can't believe they kept this place open after the flood took out the trolleys.
Mae: Never underestimate the power of old people to get state funding.
Mae: Which is why we have more war memorials than there were wars.
===
title: Stairs01
tags: demo
colorID: 0
position: 9143,-8342
---
Mae: We were at Aunt Janet's place up in Fort Lucenne when that flood hit.
Mae: Our house had two feet of water in the living room when we came back.
Mae: Mudslides took out a few houses in Underhill.
Mae: Casey Hartley came by in his dad's boat.
Mae: Gregg kept trying to ride his bike in the water and got stranded on top of a doghouse.
===
title: Stairs02
tags: demo
colorID: 0
position: 9440,-8551
---
Mae: We were at Aunt Janet's place up in Fort Lucenne when that flood hit.
Mae: Our house had two feet of water in the living room when we came back.
Mae: Mudslides took out a few houses in Underhill.
Mae: Casey Hartley came by in his dad's boat.
Mae: Gregg kept trying to ride his bike in the water and got stranded on top of a doghouse.
===
title: LeftSideLocked
tags: demo
colorID: 0
position: 8400,-10448
---
<<if $tunnel_east_left_side_locked is 0>>
    Mae: {align=middle,nearestLocator}Well if I can't get a pretzel this is basically a wet, underground old folks home.
    Mae: {align=middle,nearestLocator}Can't even get into rest of the tunnel right now, but it's just as well.
    Mae: {align=middle,nearestLocator}It's full of teens.
    <<set $tunnel_east_left_side_locked to 1>>
<<else>>
    Mae: {align=middle,nearestLocator}Ugh. Teens.
<<endif>>
===
title: Stand
tags: demo
colorID: 0
position: 10078,-8870
---
<<if $tunnel_east_stand is 0>>
    <<set $tunnel_east_stand += 1>>
    [[Stand00]]
<<elseif $tunnel_east_stand is 1>>
    <<set $tunnel_east_stand += 1>>
    [[Stand01]]
<<elseif $tunnel_east_stand is 2>>
    <<set $tunnel_east_stand_done to 1>>
    [[Stand02]]
<<endif>>
===
title: Stand00
tags: demo
colorID: 0
position: 9878,-8591
---
Mae: Always pretty empty in here this time of day.
Mae: Don't know why they don't just close up until dinner time.
Mae: =_=
Mae: But whatever, screw this place. They won't sell me a pretzel anyway.
Mae: o_o
===
title: Stand01
tags: demo
colorID: 0
position: 10132,-8570
---
Mae: It was years ago.
Mae: It was one pretzel.
Mae: I'm not a thief.
Mae: But I was [shake=.01][size=1.2]HUNGRY[/all]
Mae: What, did they want me to [size=1.2]STARVE[/all]?
Mae: =_=
Mae: They probably did.
Mae: Monsters.
Mae: o_o
===
title: Stand02
tags: demo
colorID: 0
position: 10398,-8622
---
Mae: I've stolen from better places than this!
===
title: Fisherman_A1D2
tags: 
colorID: 2
position: 11650,-10116
---
Fisherman: Pale white tunnel fish.
Fisherman: Easy to catch and so delish.
Mae: That’s good.
Fisherman: Oh god you were listening
<<set $fisherman_done to 1>>
===
title: Rosa_A1D2
tags: 
colorID: 2
position: 11269,-10298
---
Rosa: Oh, hello Mae!
Mae: Um.
Mae: Do I know you?
Rosa: I don't know if you remember me? Miss Rosa?
Mae: Hm...
Rosa: I knew your grandad.
Rosa: I visited you the day after you were born.
Rosa: You were so small.
Mae: ...
Rosa: I guess some things don’t change, eh?
Mae: How did you know grandad?
Rosa: I’m here most days, why don’t you stop by sometime
Rosa: and I’ll tell you all about him?
Rosa: *things your parents might not ‘a told ya*
Mae: Alright, you’ve got my interest.
Rosa: See you later, Mae!
<<set $rosa += 1>>
<<set $rosa_done to 1>>

===
title: Fisherman_A1D3
tags: 
colorID: 2
position: 11879,-10111
---
Fisherman: Wife long gone, kids don’t care.
Fisherman: Tunnel fish are always there.
Mae: Ha ha yeah man.
Fisherman: Oh god please don’t listen.
<<set $fisherman_done to 1>>
===
title: MissQuelcy
tags: 
colorID: 0
position: 8162,-9206
---
<<if $act is 0 and $day is 0>>
    [[MissQuelcy_Demo]]
<<endif>>
===
title: MissQuelcy_Demo
tags: demo
colorID: 0
position: 8138,-8801
---
<<if $miss_quelcy is 0>>
    <<alwaysLookAt Mae MissQuelcy>>
    <<wait .25>>
    Mae: o/
    Mae:{locator=MaeQuelcy}Hey Ms. Quelcy! Are you on... a phone?
    Mae: _o_
    MissQuelcy:{locator=MaeQuelcy}Mae, I think I found signal.
    Mae:{locator=MaeQuelcy}Oh my god.
    MissQuelcy:{locator=Quelcy}I know, Mae. I know.
    [[{locator=MaeQuelcy}So how's art class this year?|MissQuelcy_Demo_ArtClass]]
    [[{locator=MaeQuelcy}I'm surprised you even carry a phone around town. No one else does.|MissQuelcy_Demo_Phone]]
<<elseif $miss_quelcy is 1>>
    MissQuelcy: Mae, you're scaring the signal away.
<<endif>>
===
title: MissQuelcy_Demo_Phone
tags: demo
colorID: 0
position: 8413,-8493
---
MissQuelcy: I have a comprehensive internal database of unsecured wifi hotspots.
Mae:{locator=MaeQuelcy}That's scandalous.
MissQuelcy: Me stealing wifi is about the 10th most scandalous thing involving a phone I've encountered this week.
Mae:{locator=MaeQuelcy}Oh?
MissQuelcy:{locator=Quelcy}I caught several students earlier this week, back of the art room, looking at ...visual reference.
Mae:{locator=MaeQuelcy}Oh... :\
MissQuelcy:{locator=Quelcy}I swear they should stop calling these smartphones and start calling them tupperware for porn.
Mae:{locator=MaeQuelcy}Yeah. They should call them that. :|
MissQuelcy: It's not like we get any signal in Possum Springs. 
MissQuelcy: If I see a kid with a phone I just assume they're carrying a smut bomb waiting to go off.
[[MissQuelcy_Demo_End]]
===
title: MissQuelcy_Demo_ArtClass
tags: demo
colorID: 0
position: 7913,-8526
---
MissQuelcy: Well, we're studying anatomy for figure drawing
MissQuelcy: and I'm trying to somehow work it into the sex ed class this district won't allow us to have.
Mae:{locator=MaeQuelcy}I won't tell. :)
MissQuelcy: I guess if all else fails we could make onesies.
Mae: :|
[[MissQuelcy_Demo_End]]
===
title: MissQuelcy_Demo_End
tags: demo
colorID: 0
position: 8144,-8231
---
Mae:{locator=MaeQuelcy}Well, best of luck with the signal!
MissQuelcy: Hey, Mae? I know you just recently got back, but I have some free advice for you.
Mae:{locator=MaeQuelcy}What's that?
MissQuelcy: Get out now while you still can.
Mae:{locator=MaeQuelcy}Um. Thanks. :\
Mae:{locator=MaeQuelcy}Why are you still here if you hate it so much?
MissQuelcy: Child, I am paid to be here.
Mae:{locator=MaeQuelcy}Ok. Thanks?
MissQuelcy: Run. Run away. Get a job in far away lands. Join the circus. Be not here. 
Mae:{locator=MaeQuelcy}... :|
MissQuelcy: You're welcome.
<<set $ms_quelcy to 1>>
===
title: Old_TunnelTeens_A2D4
tags: 
colorID: 3
position: 5739,-8851
---
//THIS IS THE PUMPKIN HEAD GUY!!!!//
<<if $tunnel_teens is 0>>
    <<set $tunnel_teens += 1>>
    <<set $tunnel_teens_done to 1>>
    Mae: Where is everybody?
    TunnelTeen3: *mmf**mmf*
    Mae: What?
    TunnelTeen3: *MMF**MMF*
    Mae: Oh my god. We used to do that. Awesome.
    TunnelTeen3: *mmf**mmf**mmf*
    Mae: Well you go easy on my street.
    TunnelTeen3: *mmf**mmfmmffmffff*
    Mae: Deal
    <<set $knows_mischief to 1>>
<<endif>>
===
title: Old_TunnelTeens_A3D1
tags: 
colorID: 3
position: 6055,-8801
---
<<if $tunnel_teens is 0>>
    <<set $tunnel_teens += 1>>
    <<set $tunnel_teens_done to 1>>
    <<if $knows_mischief is 1>>
    	Mae: Wow.
    	Mae: You guys sure did a number on my street.
    	TunnelTeen3: We did nothing.
    	TunnelTeen2: You got no proof.
    	Mae: That's my neighbor's pumpkin right there.
    	TunnelTeen2: This is a different pumpkin.
    	Mae: ...
    <<endif>>
    <<if $intro_teens is 0>>
    	TunnelTeen1: Hey don't you know Lori?
    	Mae: Yeah.
    	TunnelTeen1: Oh ok.
    	TunnelTeen2: I thought so
    	<<set $intro_teens to 1>>
    <<endif>>
    Mae: Hey... uh... kind of a weird question, but
    Mae: is anyone missing?
    TunnelTeen3: What?
    Mae: Like anyone you know who was out last night
    Mae: and didn't come back?
    TunnelTeen2: Uh.
    TunnelTeen4: I don't think so?
    TunnelTeen1: What's going on?
    Mae: Nothing. Thought I saw something.
    TunnelTeen3: Ok... uh...
    TunnelTeen1: You should tell us if this is something.
    Mae: Just be careful, ok? Keep an eye out
    Mae: and let me know if you see anything weird, ok?
    <<set $teen_sleuth to 1>>
    Mae: I mean you all hang out together constantly.
    Mae: You're safer than anyone else I know!
    TunnelTeen1: ...
<<endif>>
===
title: Old_TunnelTeens_A3D3
tags: 
colorID: 3
position: 6241,-8929
---
<<if $tunnel_teens is 0>>
    <<set $tunnel_teens += 1>>
    <<set $tunnel_teens_done to 1>>

    <<if $no_lori is 1>>
    	TunnelTeen1: Eff off.
        <<stop>>
    <<endif>>
    <<if $intro_teens is 0>>
    	TunnelTeen1: Hey don't you know Lori?
    	Mae: Yeah.
    	TunnelTeen1: Oh ok.
    	TunnelTeen2: I thought so
    	<<set $intro_teens to 1>>
    <<else>>
    	Mae: Hey teens.
    	TunnelTeen3: Hey.
    <<endif>>
    Mae: So like... are you *always* together?
    TunnelTeen1: Yeah I guess.
    Mae: That's cool. Friends are cool.
    Mae: Safer.
    TunnelTeen3: Safer?
    Mae: Yeah, like the buddy system.
    Mae: Safety in numbers!!!
    TunnelTeen4: You're a little paranoid, you know that?
    Mae: Kid, you don't even know.
    <<if $teen_sleuth is 1>>
    	Mae: Anyone hear anything about anyone around here missing?
    	TunnelTeen3: Uh, yeah actually.
    	TunnelTeen3: There's some girl I know online
    	TunnelTeen3: she hasn't been on since Harfest.
    	Mae: Ok well I think we might need a bit more than that.
    	TunnelTeen3: Ok.
    	Mae: Is she from here? I could ask my aunt...
    	TunnelTeen3: some place called Mulvay
    	Mae: !!!! (eyes dilate, big frown)
    	Mae: *Mulvay*?
    	Mae: You know someone from *Mulvay*?
    	TunnelTeen3: Yeah?
    	Mae: Do you know where Mulvay is?
    	Mae: Do you know *WHAT* Mulvay is?
    	TunnelTeen3: No...
    	TunnelTeen1: I heard about Mulvay.
    	TunnelTeen1: My dad drives a bread truck.
    	TunnelTeen1: Mulvay used to be one of his stops.
    	Mae: What happened?
    	TunnelTeen1: He don't stop there no more.
    	TunnelTeen1: They had to pay some other guy to do it.
    	Mae: Sounds about right.
    	TunnelTeen3: WHAT'S Mulvay?
    	TunnelTeen1: I'll take you out there this weekend.
    	Mae: Don't.
    	TunnelTeen1: It'll be fine. We'll just drive through.
    	Mae: ...
    	Mae: I need to go get back in the sun now.
        <<set $munroy to 1>>
    <<else>>
    	Mae: So like... and I don't want this to sound weird, but...
    	Mae: Do you know of anyone who's gone missing in the past couple days?
    	TunnelTeen1: No?
    	TunnelTeen4: Nah, not that I know of
    	Mae: Ok, well tell me if you notice anyone is.
    	TunnelTeen3: You are really spooking me.
    	Mae: You and me both, kid.
    	<<set $teen_sleuth to 1>>
    <<endif>>
<<endif>>
===
title: Old_TunnelTeens_A3D4
tags: 
colorID: 3
position: 6468,-8865
---
<<if $tunnel_teens is 0>>
    <<set $tunnel_teens += 1>>
    <<set $tunnel_teens_done to 1>>
    <<if $no_lori is 1>>
    	TunnelTeen1: Eff off.
    <<else>>
        <<if $intro_teens is 0>>
        	TunnelTeen1: Hey don't you know Lori?
        	Mae: Yeah.
        	TunnelTeen1: Oh ok.
        	TunnelTeen2: I thought so
        	<<set $intro_teens to 1>>
        <<else>>
        	Mae: Hey teens.
        	TunnelTeen3: Hey.
        <<endif>>
        Mae: Hey you guys.
        TunnelTeen2: Yeah?
        Mae: Stay safe, alright?
        TunnelTeen3: wow. Thanks mom.
        TunnelTeen4: are you our new mom?
        Mae: apparently.
        Mae: life, man. it goes.
        TunnelTeen1: slowly
        Mae: nah, dude.
        Mae: nah.
    <<endif>>
<<endif>>
===
title: PawsModeStealingPretzel
tags: 
colorID: 0
position: 10930,-11012
---
<<if $mallard_rats>>
    Mae: My precious rat babies have left the nest! no need for Momma to feed 'em anymore.
    Mae: ...they grow up so fast...
<<elseif $rats_fed_today is 1 >>
    Mae: My babies are all fed. Momma don't need to steal today.
<<elseif $paws_mode_mallard is 0>>
    Mae: Delicious pretzels...
<<elseif $paws_mode_mallard is 1 and $pretzel is 0 and $pretzel_steal_attempt is 0>>
    Mae: I bet some rat babies would love a delicious pretzel...
    <<set $pretzel_steal_attempt to 1>>
    <<irisOut 1 wait>>
    <<scene PawsModeStealingPretzel>>
<<elseif $paws_mode_mallard is 1 and $pretzel is 0 and $pretzel_steal_attempt > 0>>
    Mae: ugh probably not gonna get another chance today. dammit.
<<elseif $paws_mode_mallard is 1 and $pretzel > 0>>
    Mae: One pretzel is enough for my tiny rat darlings.
<<endif>>
===
title: MuralVandalism
tags: demo2
colorID: 3
position: 11629,-10944
---
Mae: {emote=exclamation}
Mae: HOLY SHIT.
Mae: The Dudes! Someone hecked up the dudes!
Mae: Who... did... this?
Mae: Why did they do it?
<<set $knows_mural to 1>>
<<set $mural_vandalism_done to 1>>
<<unlockDoodle NukePossumSprings>>
===
title: Fisherman_A2D1
tags: demo2
colorID: 3
position: 12133,-10105
---
Fisherman: Many miners old and young
Fisherman: With home in heart and coal in lung
Fisherman: Were taken by the trolley men
Fisherman: Up to the mine and back again
Fisherman: But mudslide, flood, and awful storm
Fisherman: Have given tunnel fishes ho...rm.
Mae: "Horm?" :)
Fisherman: Please stop listening.
Mae: :|
<<set $fisherman_done to 1>>
<<unlockDoodle HoneyImHorm>>
===
title: TrolleyWonder
tags: demo2
colorID: 3
position: 10426,-10721
---
<<lookOut Mae>>
Mae:{locator=TrolleyWonder}Amazing to think these things used to run.
Mae:{locator=TrolleyWonder}Kind of amazing they're still down here too.
Mae:{locator=TrolleyWonder}No way to get them out of here I guess.
Mae:{locator=TrolleyWonder}This place sure was something a long time ago.
<<lookNearest Mae>>
<<set $trolley_wonder_done to 1>>
===
title: AboutTunnelDemo2
tags: demo2
colorID: 3
position: 10728,-10716
---
Mae: I can't believe they've kept this open.
Mae: Historical note, I guess.
Mae: I think it's just because the old people like the pierogi stand.
Mae: Never underestimate the ability of old people to get state funding.
<<end>>
===
title: Council1
tags: demo2
colorID: 0
position: 12187,-10828
---
<<if $council1 is 0>>
    Council1: Damn shame
    <<set $council1 += 1>>
<<elseif $council1 is 1>>
    Council1: This was of historical note!
    <<set $council1 += 1>>
<<elseif $council1 is 2>>
    Council1: Without it I don’t know if we can justify keeping this place open...
    <<set $council1 += 1>>
<<else>>
    Council1: {emote=cloud_rain}
<<endif>>

===
title: CityCouncil_A2D1_Demo2
tags: demo2
colorID: 3
position: 11790,-10463
---
<<set $council1_done to 1>>
Council1: Because this mural is of historical note, Molly!
Council1: What do we know?
Molly: Well, sometime overnight
Molly: someone used bolt cutters on the gate
Molly: and came down here.
Council1: What’d they use, a paint roller?
Molly: Looks to be.
Council1: Says they intend to Nuke Poss... something?
Molly: I think it’s likely some kid
Molly: saw one of the old Nuke Possum Springs vandalisms from back when
Molly: and got bored halfway through writing it.
Council1: We need to treat this as a terrorist action.
Molly: I’m... sorry?
Council1: This mural is of historical note!
Molly: Alright well I’ll round up any… uh… terrorists I see.
Council1: Round up any dirtbag teens or vagrants that’ve been hanging around town
Council1: check out by the abandoned Food Donkey.
Council1: they come off the train there
Molly: Already on it.
Council1: Molly.
Molly: Yeah?
Council1: Find whoever did this.
Molly: Ok.
Council1: I want to see them hang.
Molly: I think at most they’d get a fine and some jail time
Molly: but alright. I’ll let you know when we’ve got something.
<<close>>
<<set $council1_done to 0>>
<<walk Molly MollyOffRight>>
<<wait 3>>
===
title: Old_TunnelTeens_A4D3
tags: 
colorID: 6
position: 6270,-8527
---
<<if $tunnel_teens is 0>>
    <<set $tunnel_teens += 1>>
    <<set $tunnel_teens_done to 1>>
    <<if $no_lori is 1>>
    	TunnelTeen1: Eff off.
        Mae: No. Today *YOU* eff off.
        TunnelTeen1: Dang, bud.
    <<else>>
        <<if $intro_teens is 0>>
        	TunnelTeen1: Hey don't you know Lori?
        	Mae: Yeah.
        	TunnelTeen1: Oh ok.
        	TunnelTeen2: I thought so
        	<<set $intro_teens to 1>>
        <<else>>
        	Mae: Hey teens.
        	TunnelTeen3: Hey.
        <<endif>>
        Mae: oof.
        TunnelTeen1: Oof?
        TunnelTeen2: you alright?
        Mae: I guess.
        TunnelTeen3: How old did you say you were?
        Mae: 20
        TunnelTeen4: Told you she couldn't get us beer.
        Mae: What do you want beer for anyway?
        Mae: Beer is gross.
        Mae: It's not delicious at all.
        TunnelTeen1: Life ain't always delicious, mom.
        TunnelTeen2: Thought you'd know that by now.
        Mae: That doesn't even have any bearing on this.
        Mae: I hear there is actual good-tasting alcohol out there.
        Mae: I dunno. Fruit punch that makes you do things you regret.
        TunnelTeen3: Right.
        Mae: Am I uncool again?
        TunnelTeen4: You never were cool, mom.
        Mae: Dang.
        TunnelTeen1: But life ain't always cool.
        TunnelTeen2: Life is garbage.
        TunnelTeen3: But like, you know. Whatever.
        TunnelTeen4: Yeah. We're all trash anyway.
        TunnelTeen1: We belong here.
        Mae: Ok we can stop with the words of teenly wisdom now.
        Mae: Catch you later.
    <<endif>>
<<endif>>
===
title: Fisherman_A2D3
tags: 
colorID: 3
position: 12399,-10113
---
Fisherman: Wife long gone, kids don’t care.
Fisherman: Tunnel fish are always there.
Mae: Ha ha yeah man.
Fisherman: Oh god please don’t listen.
<<set $fisherman_done to 1>>
===
title: Fisherman_A3D2
tags: 
colorID: 4
position: 12648,-10102
---
Fisherman: Methinks I spy a tunnel shark
Fisherman: Scary monster in the dark
Mae: Holy crap!
Fisherman: I didn't really see it.
Fisherman: please don't listen.
<<set $fisherman_done to 1>>
===
title: Fisherman_A3D4
tags: 
colorID: 4
position: 12921,-10097
---
Fisherman: Will tunnel fish be always there?
Fisherman: Will tunnel drain, canal be bare?
Fisherman: I would miss the tunnel fish.
Fisherman: To have them back would be my wish.
Mae: are they gonna drain the tunnel or something?
Fisherman: No. It's poetry, ok?
Fisherman: I'm just making stuff up!
Fisherman: Please don't listen!!!
<<set $fisherman_done to 1>>
===
title: Fisherman_A4D3
tags: 
colorID: 6
position: 13212,-10085
---
Fisherman: When winter come and water freeze
Fisherman: Here the fish can be at ease
Fisherman: the tunnel is not flooded ruin
Fisherman: it's a home, a place for doin'
Mae: Sure. That works.
Fisherman: please go away.
<<set $fisherman_done to 1>>
===
title: TunnelTeens_A1D1
tags: 
colorID: 2
position: 6937,-9585
---
Mae: Cool that people still hang around down here.
Mae: We used to do that when I was in highschool.
TunnelTeen1: yeah.
Mae: I wasn’t in high school that long ago, you know.
TunnelTeen2: How old are you?
Mae: 20!
TunnelTeen2: Oh so you can’t buy beer.
Mae: ...no.
TunnelTeen1: Do you have a car?
Mae: ...no.
TunnelTeen2: hm.
===
title: TunnelTeens_A1D3
tags: 
colorID: 2
position: 7194,-9576
---
Mae: Hey kids!
TunnelTeen1: Hey adult.
Mae: Well I was your age only a few years ago.
TunnelTeen2: Uh huh.
Mae: ...
Mae: Well, see ya later!
===
title: TunnelTeens_A2D2
tags: 
colorID: 3
position: 6936,-9343
---
Mae: Rainy day, huh.
TunnelTeen3: But not down here.
Mae: You kids have your own little secret hideout down here, huh. :)
TunnelTeen2: Pfft.
TunnelTeen3: Whatever.
Mae: :|
Mae: When I was a kid - 
TunnelTeen2: Literally nobody cares.
<<wait 1>>
Mae: Well - :(
TunnelTeen3: Whatever.
<<wait 1>>
Mae: ... :|
Mae: Okay then.
===
title: TunnelTeens_A3D1
tags: 
colorID: 4
position: 6930,-9099
---
Mae: Hey... uh... kind of a weird question, but
Mae: is anyone missing?
TunnelTeen3: What?
Mae: Like anyone you know who was out last night
Mae: and didn't come back?
TunnelTeen2: Uh.
TunnelTeen4: I don't think so?
TunnelTeen1: What's going on?
Mae: Nothing. Thought I saw something.
TunnelTeen3: Ok... uh...
TunnelTeen1: You should tell us if this is something.
Mae: Just be careful, ok? Keep an eye out
Mae: and let me know if you see anything weird, ok?
Mae: I mean you all hang out together constantly.
Mae: You're safer than anyone else I know!
TunnelTeen1: ...
===
title: TunnelTeens_A3D3
tags: 
colorID: 4
position: 7186,-9097
---
Mae: So like... are you *always* together?
TunnelTeen1: Yeah I guess.
Mae: That's cool. Friends are cool.
Mae: Safer.
TunnelTeen3: Safer?
Mae: Yeah, like the buddy system.
Mae: Safety in numbers!!!
TunnelTeen4: You're a little paranoid, you know that?
Mae: Kid, you don't even know.
Mae: Anyone hear anything about anyone around here missing?
TunnelTeen3: Uh, yeah actually.
TunnelTeen3: There's some girl I know online
TunnelTeen3: she hasn't been on since Harfest.
Mae: Ok well I think we might need a bit more than that.
TunnelTeen3: Ok.
Mae: Is she from here? I could ask my aunt...
TunnelTeen3: some place called Mulvay
Mae: !!!!
//(eyes dilate, big frown)
Mae: *Mulvay*? \o/
Mae: You know someone from *Mulvay*?
TunnelTeen3: Yeah?
Mae: Do you know where Mulvay is? _o_
Mae: Do you know *WHAT* Mulvay is?
TunnelTeen3: No...
TunnelTeen1: I heard about Mulvay.
TunnelTeen1: My dad drives a bread truck.
TunnelTeen1: Mulvay used to be one of his stops.
Mae: What happened?
TunnelTeen1: He don't stop there no more.
TunnelTeen1: They had to pay some other guy to do it.
Mae: Sounds about right.
TunnelTeen3: WHAT'S Mulvay?
TunnelTeen1: I'll take you out there this weekend.
Mae: Don't.
TunnelTeen1: It'll be fine. We'll just drive through.
Mae: ...
Mae: I need to go get back in the sun now.
===
title: TunnelTeens_A4D3
tags: 
colorID: 6
position: 7460,-9339
---
Mae: Hey you guys.
TunnelTeen2: Yeah?
Mae: Stay safe, alright?
TunnelTeen3: wow. Thanks mom.
TunnelTeen4: are you our new mom?
Mae: apparently.
Mae: life, man. it goes.
TunnelTeen1: slowly
Mae: nah, dude.
Mae: nah.
<<wait 1>>
<<if $did_birdland_quest>>
    TunnelTeen1: I wish those two playing the music would shut the eff up.
    Mae: Hey!
    TunnelTeen1: ...
    Mae: Watch it.
<<endif>>
===
title: TunnelTeens_Intro
tags: 
colorID: 0
position: 6949,-9843
---
Mae: Hey teens.
TunnelTeen1: Hey.
Mae: What are you guys doing?
TunnelTeen1: ...
Mae: ...
TunnelTeen1: nothing.
Mae: Cool!
<<wait 1>>
<<set $met_tunnel_teens to 1>>

// A1D1, A1D3, A2D2, A2D4, A3D1, A3D3, A4D3 //
<<if $act is 1 and $day is 1>>
    [[TunnelTeens_A1D1]]
<<elseif $act is 1 and $day is 3>>
    [[TunnelTeens_A1D3]]
<<elseif $act is 2 and $day is 2>>
    [[TunnelTeens_A2D2]]
<<elseif $act is 2 and $day is 4>>
    [[TunnelTeens_A2D4]]
<<elseif $act is 3 and $day is 1>>
    [[TunnelTeens_A3D1]]
<<elseif $act is 3 and $day is 3>>
    [[TunnelTeens_A3D3]]
<<elseif $act is 4 and $day is 3>>
    [[TunnelTeens_A4D3]]
<<endif>>
===
title: PumpkinHeadGuy
tags: temp
colorID: 3
position: 8359,-9802
---
Mae: Where is everybody?
PumpkinHeadGuy: *mmf**mmf*
Mae: What?
PumpkinHeadGuy: *MMF**MMF*
Mae: Oh my god. We used to do that. Awesome.
PumpkinHeadGuy: *mmf**mmf**mmf*
Mae: Well you go easy on my street.
PumpkinHeadGuy: *mmf**mmfmmffmffff*
Mae: Deal
<<set $pumpkin_head_guy_done to 1>>
===
