title: InitLevel_A1D2
tags: 
colorID: 2
position: -943,-1045
---
<<show VideostoreGirl>>
===
title: VideostoreGirl_00_01
tags: 
colorID: 2
position: -704,-708
---
Mae: Nightshift, huh? That's cool.
VideostoreGirl: I'm at home with my baby during the day
VideostoreGirl: until my mom gets back from work?
Mae: Ew, babies.
VideostoreGirl: Pretty cool job, though?
VideostoreGirl: Get to watch movies, eat candy, meet cute guys?
Mae: Ew, cute guys.
VideostoreGirl: What's wrong with guys?
Mae: I dunno. Cute guys are overrated.
<<set $not_cute_ones to 1>>
===
title: VideostoreGirl_00_02
tags: 
colorID: 2
position: -569,-466
---
VideostoreGirl:{emote=heartbreak}
<<close>>
VideostoreGirl:{emote=music}
<<close>>
===
title: InitLevel_A1D3
tags: 
colorID: 2
position: -30,-1056
---
<<show Angus>>
===
title: Angus_A1D3_00
tags: 
colorID: 2
position: -134,-14
---
Angus: Hey, Mae!
Angus: How's the laptop running?
Mae: Really good! Thanks so much!
Angus: Glad to be of service.
===
title: Angus_A1D3_01
tags: 
colorID: 2
position: 119,-12
---
Mae: So, like...
Angus: Yeah?
[[Who rents movies anymore?|WhoRentsMoviesAnymore]]
[[How is this store still open?|HowIsThisStoreStillOpen]]
===
title: WhoRentsMoviesAnymore
tags: 
colorID: 2
position: 75,255
---
Angus: More people than you'd think!
Angus: Older people who don’t know any better.
Angus: Kids who for some reason can't get something.
Mae: Do you ever want to tell them?
Angus: What, about the internet?
Angus: About rental by mail?
Mae: Yeah.
Angus: Mae, it's not my job to save them
Angus: I am paid to stand here, assist when needed,
Angus: take their money, and not openly judge them.
===
title: HowIsThisStoreStillOpen
tags: 
colorID: 2
position: 329,260
---
Angus: There's a certain romance to vhs and dvd
Angus: that simply cannot be matched.
Mae: ... really?
Angus: Nope.
Mae: Wait, you actually have vhs?
Angus: Mostly the adult section back behind the curtain.
Mae: WHOA, really?
Angus: Yeah some folks are like really into seeking them out.
Angus: It's like a whole little scene that goes back before the internet.
Angus: We had a historian in there last month looking for some obscure video.
Mae: How do you get THAT job?
Angus: I think that's the kind of job you can only have
Angus: if you don’t need to have a job.
===
title: Angus_A1D3_02
tags: 
colorID: 2
position: 404,-25
---
Mae: So this party tonight, huh?
Angus: Yeah. Should be fun?
Mae: I hadn't pegged you as a party person.
Angus: I'm not really. But parties with Gregg are different.
Mae: How so?
Angus: You know how you want to just go and stand in a corner sometimes?
Mae: Yeah.
Angus: Gregg's my corner.
Mae: Awwww.
Angus: Ha ha shut up
===
title: Angus_A1D3_03
tags: 
colorID: 2
position: 654,-16
---
Mae: See ya later!
Angus: For sure!
===
title: Angus_A1D3
tags: 
colorID: 2
position: 273,-284
---
<<if $angus is 0>>
    <<set $angus += 1>>
    [[Angus_A1D3_00]]
<<elseif $angus is 1>>
    <<set $angus += 1>>
    [[Angus_A1D3_01]]
<<elseif $angus is 2>>
    <<set $angus += 1>>
    [[Angus_A1D3_02]]
<<elseif $angus is 3>>
    <<set $angus += 1>>
    <<set $angus_done to 1>>
    [[Angus_A1D3_03]]
<<endif>>
    
===
title: VideostoreGirl
tags: 
colorID: 0
position: -1094,-157
---
<<if $act is 2 and $day is 2>>
    [[VideostoreGirl_A2D2]]
<<elseif $knows_angus_at_videostore > 0>>
    <<if $videostore_girl is 0>>
        <<set $videostore_girl += 1>>
        [[VideostoreGirl_00_00]]
    <<elseif $videostore_girl is 1>>
        <<set $videostore_girl += 1>>
        [[VideostoreGirl_00_01]]
    <<else>>
        [[VideostoreGirl_00_02]]
    <<endif>>
<<else>>
    [[VideostoreGirl_CantHelp]]
<<endif>>

===
title: VideostoreGirl_00_00
tags: 
colorID: 2
position: -1043,-734
---
VideostoreGirl: Can I help you?
Mae: Where's Angus? 
VideostoreGirl: Oh he works dayshift?
Mae: It is day?
VideostoreGirl: I got here early?
<<if $knows_apartment_location is 0>>
	Mae: Any idea where he is?
	VideostoreGirl: I assume he went home?
	VideostoreGirl: He lives next door?
    VideostoreGirl: Go inside and ring the buzzer?
    <<set $knows_apartment_location to 1>>
<<endif>>
Mae: Wait. Why does everything you say sound like a question?
VideostoreGirl: You do the same thing?
VideostoreGirl: I just didn’t comment on it?
Mae: Really?
VideostoreGirl: Yeah?
Mae: Huh?
<<set $apartment_unlocked to 1>>
===
title: VideostoreGirl_CantHelp
tags: 
colorID: 2
position: -717,76
---
VideostoreGirl: Can I help you?
Mae: I don't think so...
===
title: PreInitLevel
tags: 
colorID: 0
position: -183,-1518
---
<<hide Angus>>
<<hide VideostoreGirl>>
===
title: InitLevel_A2D1
tags: 
colorID: 3
position: 866,-1064
---
<<show Angus>>
===
title: InitLevel_A2D2
tags: 
colorID: 3
position: 1124,-1059
---
<<show VideostoreGirl>>
===
title: VideostoreGirl_A2D2
tags: 
colorID: 3
position: 1119,-748
---
VideostoreGirl:{emote=heartbreak}
<<close>>
VideostoreGirl:{emote=music}
<<close>>
===
title: InitLevel_A2D3
tags: 
colorID: 3
position: 1387,-1054
---
<<show Angus>>
===
title: InitLevel_A2D4
tags: 
colorID: 3
position: 1643,-1056
---
<<show Angus>>
===
title: Angus_A2D1
tags: 
colorID: 3
position: 812,-765
---
Angus: Hey Mae.
Mae: Hey Angus. How's it going?
Angus: You are like the third person I've seen today.
Mae: I know you've explained it but I have zero clue how this place stays open.
Angus: You'd be surprised.
Mae: Band practice later?
Angus: Gotta warm up my voice!
Mae: You growl real good.
Angus: Thanks. I'm a born growler.
Angus: When I was a baby I never cried
Angus: I just growled.
Mae: I think I cried nonstop until I was like 9
Mae: and then I cried all the time again starting at like 13...
Angus: You had a good three years of not crying in there!
Mae: They were really productive years.
Angus: Heh heh.
<<set $angus_done to 1>>
===
title: InitLevel_A3D1
tags: 
colorID: 5
position: 1976,-1064
---
<<show Angus>>
===
title: Angus_A3D1
tags: 
colorID: 5
position: 1981,-799
---
Angus: You up for band practice in a bit?
Mae: I guess. Weird night.
Angus: How so?
Mae: I'll tell you about it later.
<<set $angus_done to 1>>
===
title: InitLevel_A3D2
tags: 
colorID: 5
position: 2274,-1036
---
<<show Angus>>
===
title: InitLevel_A3D3
tags: 
colorID: 5
position: 2556,-1033
---
<<show Angus>>
===
title: InitLevel_A3D4
tags: 
colorID: 5
position: 2844,-1042
---
<<show Angus>>
===
title: Angus_A3D2
tags: 
colorID: 5
position: 2258,-800
---
Angus: So, I said I could like
Angus: take you up to the park.
Mae: you did indeed.
Angus: You ready to go?
-> Naw
-> Yeah
    [[InvestigationQuest]]
===
title: Angus_A3D3
tags: 
colorID: 5
position: 2540,-787
---
Angus: Hey Mae.
Mae: Hey dude.
Mae: Totally what's up.
Angus: Nothing.
Mae: You ok?
Angus: Yep.
Mae: ...ok
Angus: Sorry. Don't mind me.
<<set $angus_done to 1>>
===
title: Angus_A3D4
tags: 
colorID: 5
position: 2840,-793
---
<<if $did_angus_investigation_quest>>
    Angus: My legs are still sore from running from that dude.
    Mae: I'm just sore in general.
    Mae: and tired.
    Mae: in general.
    Angus: My life doesn't involve a lot of running.
    Mae: heh heh
    Mae: I feel like I should have
    Mae: some sort of clever comment to follow that up.
    Angus: You can get back to me.
    <<set $angus_done to 1>>
<<else>>
    Angus: So, I said I could like
    Angus: take you up to the park.
    Mae: you did indeed.
    Mae: I'm so tired.
    Angus: you gonna be ok?
    Mae: yeah, i'll be fine
    Angus: You up for this park trip?
    -> Naw
    -> Yeah
        [[InvestigationQuest]]
<<endif>>
===
title: InvestigationQuest
tags: 
colorID: 5
position: 2501,-439
---
<<set $angus_investigation_quest to 1>>

<<if $did_gregg_friendship_quest_4>>
	Mae: Wow. Bea let you have the car two nights in a row?
	Angus: yeah
	Mae: Even after the flat tire?
	Angus: Well, I mean, you can’t really do anything about that
	Angus: and I replaced the tire.
	Mae: Oh yeah.
	<<close>>
	<<wait 2>>
<<endif>>

<<if $did_gregg_investigation_quest>>
	Mae: Cool you could borrow her car, though.
	Mae: I had to ride on Gregg’s bike
	Mae: when we saw that ghost.
	Angus: Yeah Gregg needs to get his license.
	Mae: I fell off his bike.
	<<if $did_gregg_friendship_quest_3>>
		Mae: which I *also* did a few days before that
	<<endif>>
<<endif>>

<<if $completed_investigation_quests is 1 and $dom_friend_bea>>
    <<set $second_iq_and_dom_friend_bea to 1>>
<<else>>
    <<set $second_iq_and_dom_friend_bea to 0>>
<<endif>>

<<if $completed_investigation_quests is 0 or $second_iq_and_dom_friend_bea>>
	Mae: I’M EXCITED! \o/
	Angus: Are you?
	Mae: Yeah! We never get to hang out! _o_
	<<if $dom_friend_gregg>>
		Angus: Yeah, I guess huh?
		Mae: I’ve been hanging out with Gregg so much
		Mae: it’ll be nice to actually get to know you a little better!
		Angus: I’m pretty great.
	<<endif>>
	Mae: Ghost hunting with Angus!
<<endif>>

Angus: So you think the guy you saw was a ghost?
Mae: not a guy- a GHOST
Angus: right.
Mae: Why does no one believe me that it was a ghost?
Angus: Well, I mean… Ghosts don’t actually exist.
Mae: I’m pretty sure they do.
Angus: Well, I’d need evidence.
Mae: What counts?
Angus: A ghost walking up and saying hello
Angus: and me saying hey I’ve verified you are a dead person.
Mae: Could be a zombie or a frankenstein.
Angus: ok maybe if they’re made of ghosty stuff
Mae: Ghosty Stuff can be our podcast
Mae: about ghosts
Angus: *This episode of Ghosty Stuff brought to you by Donut Wolf*
<<if $did_gregg_friendship_quest_4>>
	Mae: No, I’m going to be sick.
	Angus: sorry.
<<endif>>

Mae: Welcome to Ghosty Stuff.
Mae: I’m Mae, and I saw ghosts, and this is Angus, who doesn’t believe me.
Angus: I’m Angus, and this is Mae. She’s a crazy person.
<<irisOut 1 wait>>
<<sectionTitle AngusIQ ParkEntrance>>
===
title: Angus_A2D3
tags: 
colorID: 3
position: 1405,-752
---
Angus: Hey Mae.
Mae: Hey Angus. How's videoland?
Angus: good, good.
Angus: start of the halloween rush.
Angus: got a pretty good horror section.
Mae: nice.
Angus: you got a favorite?
Mae: what, horror movie?
Angus: yeah.
    ->Scythe Sabboth
        Angus: ooh! italian!
        Mae: yeah. they don't mess around.
        Angus: pretty colors.
        Mae: pretty *everything.
    ->Black Oaks
        Angus: oh nice. witchy.
        Mae: yeah it's witchy as hell.
        Mae: and scary.
        Angus: yeah that one's like
        Angus: historical.
        Mae: the witchy version.
Mae: well, seeya later!
Angus: byeeee!
<<set $angus_done to 1>>
===
title: Angus_A2D4
tags: 
colorID: 3
position: 1655,-749
---
Mae: happy halloweeeeen!
Angus: ha ha hey. you too.
Mae: you feeling spooky?
Angus: oh extremely. can't you tell?
Mae: do your spooky face.
<<wait 1>>
Angus: :)
<<wait 2>>
Mae: angus you're just smiling.
Mae: and it's adorable, not scary.
Angus: :|
Angus: oh.
Mae: nice effort, though!
Angus: heh heh.
<<set $angus_done to 1>>
===
