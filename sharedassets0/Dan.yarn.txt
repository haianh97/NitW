title: DanInitLevel
tags: 
colorID: 0
position: -379,-289
---
<<if $act is 0 and $day is 0 and $Underhill is 1>>
    <<show Dan>>
<<elseif $act is 1 and $Underhill is 1 and $talked_to_dan_in_act_1 is 0>>
	<<show Dan>>
    <<trigger Dan CoffeeIdle>>
<<elseif $act is 2 and $day is 1 and $OlPickaxe>>
	<<show Dan>>
<<elseif $act is 2 and $day is 3 and $OlPickaxe>>
	<<show Dan>>
<<elseif $act is 3 and $day is 1 and $TowneCentreWest>>
    <<warp Dan DanBehindDiner>>
    <<show Dan>>
<<elseif $act is 4 and $day is 3 and $TowneCentreWest>>
    <<show Dan>>
    <<trigger Dan TacoSignIdle>>
<<elseif $act is 2 and $day is 4 and $OlPickaxe is 1 and $night>>
	<<show Dan>>
<<else>>
	<<hide Dan>>
<<endif>>

===
title: Dan
tags: 
colorID: 0
position: 1,183
---
<<if $act is 0 and $day is 0>>
	[[DanDemo]]
<<elseif $act is 2 and $day is 1>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA2D1_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA2D1_01]]
    <<endif>>
<<elseif $act is 2 and $day is 3>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA2D3_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA2D3_01]]
    <<endif>>
<<elseif $act is 3 and $day is 1>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA3D1_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA3D1_01]]
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA4D3_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA4D3_01]]
    <<endif>>
<<elseif $act is 1>>
	<<if $dan is 0>>
        <<set $talked_to_dan_in_act_1 to 1>>
        <<set $dan += 1>>
		[[Dan_00_00]]
	<<elseif $dan is 1>>
        <<set $dan += 1>>
		<<set $dan_done to 1>>
		[[Dan_00_01]]
	<<endif>>
<<endif>>

===
title: Dan_00_00
tags: demo2
colorID: 0
position: 291,499
---
<<set $met_dan to 1>>
Mae: Danny, right? #line:1e31fb
Dan: Yep. #line:173b22
Mae: I think you were a senior when I was a freshman? #line:aa7c41
Dan: Probably. #line:975937
Mae: How’s it going? #line:8e6335
Dan: Just lost my job.. #line:bb9b0e
Mae: Oh no! #line:34d338
Dan: They say construction’s always hiring #line:e85056
Dan: but it’s not. #line:f15d3a
Dan: In fact, it’s often laying off guys named Dan. #line:cf9f4e
Mae: What are you going to do? #line:0c4300
Dan: I’m open to suggestions. #line:394f7a
[[Don’t give up!|Dan_00_DontGiveUp]] #line:86a190
[[I’m sure *someone* in town is hiring!|Dan_00_Someone]] #line:61e2da
===
title: Dan_00_01
tags: demo2
colorID: 0
position: -47,468
---
Mae: I don’t have a job either. #line:4e663f
Dan: Aw man, sorry to talk your ear off about it. #line:cbb9f0
Dan: How are you holding up? #line:ea94b4
Mae: Good. Just dropped out of college and I’m living with my parents. #line:b07126
Dan: Oh. #line:e4bbc9
Dan: So you’re basically a teenager again. #line:a91e87
Mae: NO. #line:8cc110
Mae: I’m just an adult living with her parents. #line:7ab0a1
Dan: Oh ok so it’s not like you’re unemployed #line:a63fa1
Dan: it’s like you’re lacking a daytime hobby #line:dfad55
Dan: that pays money you don’t need for rent. #line:f6d174
Mae: Alright, I’m gonna get going. #line:541a24
Dan: Yeah. #line:58062b
===
title: DanDemo
tags: demo
colorID: 0
position: 465,171
---
<<if $dan is 0>>
	Dan: {width=2}Construction's always hiring, Rob!
	Dan: {width=2}Well, at least the coffee shop is still open.
	Dan: {width=2}Even if it's not hiring.
	<<set $dan to 1>>
	<<set $dan_done to 1>>
<<endif>>

===
title: Dan_00_DontGiveUp
tags: demo2
colorID: 0
position: 183,767
---
Dan: Well there’s always a chance someone will die #line:e4db66
Dan: And I can have their job. #line:2fb23b
Mae: Dan, you’re not gonna like #line:631b43
Mae: kill anyone, are you? #line:a51abb
Dan: Does it pay? #line:be25c9
===
title: Dan_00_Someone
tags: demo2
colorID: 0
position: 537,763
---
Dan: I’ve been out of work for a year before. #line:0a16d7
Dan: And this time I only lasted 6 months before being laid off. #line:89e77e
Dan: Do you know what that does to a resume? #line:c03c3e
Mae: Not really. #line:00278b
Dan: I have a zombie resume. #line:d4bfe1
Dan: It’s dead but somehow it’s still going all over the place. #line:0e4484
Mae: Oh. #line:7f148b
===
title: DanA2D1_00
tags: 
colorID: 3
position: -1677,266
---
// olpickaxe
Mae: how's it going dude?
Dan: got a job here. that's cool.
Mae: oh nice!
Dan: actually i'm lying. it sucks.
Mae: oh. geez.
===
title: DanA2D1_01
tags: 
colorID: 3
position: -1688,500
---
Dan: I had no idea there were so many kinds of hammers
Dan: and wrenches
Dan: and nails.
Dan: my god, the nails.
===
title: DanA2D3_00
tags: 
colorID: 3
position: -1401,266
---
// olpickaxe
Mae: hey dude.
Dan: hello.
Mae: how's working here going?
Dan: eh. i dunno.
Dan: it pays.
===
title: DanA2D3_01
tags: 
colorID: 3
position: -1403,503
---
Dan: I like refuse to unload all that rock salt by myself.
Dan: it's not even halloween yet.
===
title: DanA3D1_00
tags: 
colorID: 4
position: -1150,271
---
// tcw, behind diner
Mae: hey dude!
Dan: hey... you?
Mae: working here now?
Dan: yep. sucks.
Dan: but it pays.
Mae: question: how do they fit a kitchen in there?
Dan: barely.

===
title: DanA3D1_01
tags: 
colorID: 4
position: -1152,503
---
Mae: What do you do here?
Dan: Grillman and janitor.
Dan: i cook and mop.
Mae: at the same time?
Dan: yes.
===
title: DanA4D3_00
tags: 
colorID: 6
position: -883,273
---
// taco
Mae: so you're a taco dude now?
Dan: i mean i was hired to be a taco artisan.
Dan: but i guess that means holding this sign out in the snow.
Mae: it's not really snowing that hard.
Dan: it's snowing. there is snow in the sky, falling.
Mae: how are the tacos here?
Dan: good. i like tacos.
Mae: there is not one person on god's earth who doesn't love tacos.
Dan: bad news: my ex hates tacos.
Mae: good news: they're now your ex.
Dan: heh heh heh
Mae: have fun with the tacos.
Dan: eh. it pays.
===
title: DanA4D3_01
tags: 
colorID: 6
position: -881,505
---
Dan: TACO Buck!
Dan: Dine in, Dine out!
Dan: We deliver!
Dan: Please tip your delivery persons!
Dan: best tacos in all of this general region. pretty sure.
Dan: been missing the pizza place?
Dan: why not try a pizza taco?
Mae: oh wow. that sounds amazing.
Dan: watching your health?
Dan: try out all-lettuce taco!
Mae: no. i refuse.
Dan: get outta here kid i'm workin'.
===
title: DanIntro
tags: 
colorID: 0
position: -864,-204
---
Mae: Danny, right?
Dan: Yep. Well, Dan.
Mae: I think you were a senior when I was a freshman?
Dan: Probably.
<<set $met_dan to 1>>

<<if $act is 2 and $day is 1>>
    [[DanA2D1_00]]
<<elseif $act is 2 and $day is 3>>
    [[DanA2D3_00]]
<<elseif $act is 3 and $day is 1>>
    [[DanA3D1_00]]
<<elseif $act is 4 and $day is 3>>
    [[DanA4D3_00]]
<<endif>>
===
