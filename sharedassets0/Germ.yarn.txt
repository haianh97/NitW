title: GermInitLevel
tags: 
colorID: 0
position: -14586,-1905
---
<<if $TowneCentreWest>>
    <<if $act is 2 and $day is 1>>
        <<show Germ>>
        <<warp Germ GermSnackFalcon>>
    <<else>>
        <<hide Germ>>
    <<endif>>
<<elseif $Outskirts>>
    <<if $act is 2 and $day is 2 and $germ_done is 0>>
        <<show Germ>>
    <<elseif $act is 2 and $day is 3 and $germ_done is 0>>
        <<show Germ>>
        <<if $germ_friendship_quest_1>>
            <<warp Player MaeGerm>>
            <<flip Germ -1>>
        <<endif>>
    <<elseif $act is 2 and $day is 4>>
        <<show Germ>>
        <<warp Germ GermBBallCourt>>
    <<elseif $act is 3 and $day is 1 and $germ_done is 0>>
        <<show Germ>>
    <<elseif $act is 3 and $day is 2 and $germ_done is 0>>
        <<show Germ>>
        <<if $germ_friendship_quest_2>>
            <<warp Player MaeGerm>>
            <<flip Germ -1>>
        <<endif>>
    <<elseif $act is 3 and $day is 3 and $germ_done is 0>>
        <<show Germ>>
    <<elseif $act is 3 and $day is 4 and $germ_done is 0>>
        <<show Germ>>
        <<if $germ_friendship_quest_3>>
            <<warp Player MaeGerm>>
            <<flip Germ -1>>
        <<endif>>
    <<else>>
        <<hide Germ>>
    <<endif>>
<<elseif $Bridge>>
    <<if $germ_friendship_quest_2>>
        <<show Germ>>
        <<if $hobo_code_1>>
            <<warp Mae MaeInTunnel>>
            <<warp Germ GermInTunnel>>
            <<flip Mae -1>>
        <<else>>
            <<noFallThrough Collision:BridgeMiddle 0>>
            <<warp Mae MaeEnterQuest>>
            <<warp Germ GermEnterQuest>>
            <<cameraLookAt CameraBridgeStart>>
        <<endif>>
    <<else>>
        <<hide Germ>>
    <<endif>>
<<endif>>
===
title: Germ_A2D1_00
tags: demo2
colorID: 3
position: -17909,-1171
---
Mae: Hey... Jeremy? #line:37c669
<<set $met_germ to 1>>
Mae: Jeremy Warton? #line:5543e0
Germ: No one calls me that. #line:d6adf9
Mae: Germ, right? #line:b08616
Germ: Germ Warfare. #line:cba336
Mae: Oh, that stuck, huh? #line:652231
Germ: Yep. #line:685ff8
Mae: That's an ok name. #line:b9b012
Germ: Your name is Mae. #line:b808e4
Mae: Yep. #line:989856
Germ: That's an ok name. #line:1b2e75
Mae: OK. #line:88a1b0
<<unlockDoodle Germ1>>
===
title: Germ_A2D1_01
tags: demo2
colorID: 3
position: -17648,-1138
---
Mae: You were like a year behind me right? #line:12ed8f
Germ: 2 years. #line:561a17
Mae: So you're fresh out, huh? #line:9a5831
Germ: I hang out with Gregg sometimes. #line:807f36
Germ: We ride bikes. #line:5b9b99
Germ: I'm pretty good at it. #line:64bf4c
Mae: Ok. #line:f85a65
Germ: !!! #line:6bf45c
Mae: What? #line:e75a44
Germ: I saw a rabid possum the other day. #line:7572a2
Mae: Really? Where? #line:9c720f
Germ: It was crawling across the road. #line:822b66
Mae: Had it been hit by a car? #line:0193c4
Germ: Naw, just rabid. #line:f688e6
Mae: What happened to it? #line:8ec90a
Germ: Got hit by a car. #line:b1980e
Mae: Wow. #line:5cb1e1
Germ: It was like.. #line:5e3c2a
Germ: a poem. #line:ee3ac3
<<set $like_a_poem to 1>>
Mae: OK. #line:7a7316
===
title: Germ_A2D1
tags: 
colorID: 3
position: -17623,-1576
---
<<if $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A2D1_00]]
<<elseif $germ is 1>>
    <<set $germ += 1>>
    <<set $germ_done to 1>>
    [[Germ_A2D1_01]]
<<endif>>
===
title: Germ_A2D2_00
tags: 
colorID: 3
position: -17080,-1250
---
Mae: Hey Germ.
Mae: Whatcha doin?
Germ: Nothin.
Germ: Was hanging with a couple crusties before.
Mae: What?
Germ: Crust punks. Traveler kids.
Mae: Oh ok. Like from off the trains.
Germ: They know we don't got much cops.
Mae: I miss *all* the cool stuff.
Germ: One of them had a dog.
Germ: They mostly have dogs sometimes.
Germ: One time I seen a crusty missing a foot.
Mae: What happened?
Germ: Trains. Jumping on and off and falling between cars and stuff.
Mae: That's intense.
Germ: Hands, knees, legs, feet, whole arms.
Mae: We saw an arm the other day!
Mae: Do you think it was from a crusty?
<<set $crusty_clue to 1>>
Germ: Naw.
Germ: I mean.
Germ: I dunno.
Mae: Huh.
===
title: Germ_A2D2_01
tags: 
colorID: 3
position: -16760,-1293
---
Mae: So,
Germ: I'M HERE A LOT OF DAYS.
Mae: Ok, I was gonna say-
Germ: I have to go now.
Mae: Bye!
<<set $germ_done to 1>>

===
title: Germ_A2D2
tags: 
colorID: 3
position: -16949,-1563
---
<<if $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A2D2_00]]
<<elseif $germ is 1>>
    <<set $germ_done to 1>>
    [[Germ_A2D2_01]]
<<endif>>
===
title: Germ_A2D3_00
tags: 
colorID: 3
position: -16361,-1114
---
Mae: Hey Germ.
Germ: Heeeeeey.
Germ: Wanna hang out?
Mae: I dunno, supposed to hang with Gregg or Bea tonight.
Germ: Geez I'm not asking you to run away and get married.
-> No
    Germ: Ok bye!
    Mae: Bye!
    <<set $germ_done to 1>>
-> Yes
    <<set $germ_friendship_quest_1 to 1>>
    Mae: Ok, what do you wanna do?
    Germ: Follow me!
    <<irisOut 1 wait>>
    <<scene ParkingLot>>
===
title: Germ_A2D4_00
tags: 
colorID: 3
position: -15859,-1113
---
Mae: Hey Germ!
Germ: Hey Mae.
Crusty1: Hey! I'm Skamz.
Mae: Scams?
Crusty1: That's Jenna.
Crusty2: Hi!
Mae: Hi!
OldHobo: I'm Bill Don.
Germ: And I'm Germ!
Mae: I'm Mae.
<<close>>
<<wait 1>>
Mae: I completely forgot what I was going to say.

===
title: Germ_A2D4_01
tags: 
colorID: 3
position: -15607,-1088
---
Mae: Hey Germ, you coming to Harfest?
Germ: Eh. Maybe.
Mae: Are you guys going?
Crusty1: Yeah we might hop over
Crusty1: check out the part-tay.
Crusty1: I mean, y'all got ghosts here for real, right?
<<set $knows_legend_hobo to 1>>
Germ: Yeah.
Mae: What?
Germ: Little Joe, you know?
Mae: Yeah man. Local legend.
Crusty1: My man Tapeworm?
Crusty1: Out in Bright Harbor?
Crusty1: He told me he never got off in this town.
Crusty1: Because of the ghost!
OldHobo: Aw that's a load a' crap.
Crusty1: He sneaks into railyards and SNATCHES up the good and the pure.
OldHobo: Well you kids got nothin' a' fear then.
Crusty1: dammit old timer! we're pure as the air!
Mae: Alright well I guess I'll see some of you there!
Mae: Nice to meet you!
Crusty1: Same, little lady!
Mae: ...
Mae: Seeya Germ!
Germ: Seeya.
===
title: Germ_A2D4
tags: 
colorID: 3
position: -15635,-1447
---
<<if $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A2D4_00]]
<<elseif $germ is 1>>
    <<set $germ += 1>>
    [[Germ_A2D4_01]]
<<elseif $germ is 2>>
    [[Germ_A2D4_02]]
<<endif>>
===
title: Germ_A2D4_02
tags: 
colorID: 3
position: -15362,-1228
---
Germ:{emote=pentagram}
<<close>>
Germ:{emote=ghost}
<<close>>
===
title: Germ_A3D1_00
tags: 
colorID: 4
position: -14926,-1211
---
Mae: Hey.
Germ: Sup kid.
Germ: *sniff sniff*
Germ: You smell weird.
<<if $knows_council_bball is 0>>
	Mae: Why is my aunt's car here?
	Germ: That's your aunt?
	Mae: ...yeah...
	Germ: Oh weird that like makes you half cop?
	Germ: YOU'LL NEVER HALF CATCH ME, HALF COPPAAAAAH!!!
	Germ: HA HA HA HA HA HA HA HA HA HA
	Mae: ...
	Germ: I dunno what they're doing up there.
	Mae: Huh.
<<endif>>
Mae: You go to Harfest?
Germ: Nah. Over it.
Mae: Cool. I had kind of a weird time.
Mae: Weird bad time, actually.
Germ: It's a guaranteed weird bad time.
Mae: Yeah.
<<if $knows_legend_hobo is 1>>
	Mae: Are those people from yesterday still here?
	Germ: Dunno.
	Germ: We're not like... good friends or anything.
	Mae: Oh yeah. I don't know why I thought you'd know.
	Mae: I was wondering if they ever met Casey.
	Mae: Out there somewhere.
	Germ: I can ask. I used to ask.
	Mae: I just hate thinking about him out there all by himself.
	Germ: Aw, Casey's tough. He'll be fine.
	Germ: When you gotta get out, you gotta get out!
	Mae: yeah I guess
<<endif>>
Germ: Welp, see you at band practice!
Mae: Real talk: do you actually like watching us play?
Germ: Yeah! I mean you're not good but yeah.
Germ: Not my style.
Mae: What is your style?
Germ: Black metal, uh thrash crossover, hip hop.
Mae: I like Witchdagger! They're kinda metal.
Germ: Oh yeah like that doom shit, right?
Germ: DA-DA-DUUUUURGA!!!
Mae: Totally!
Germ: ... 
Mae: What?
Germ: You know one time a guy followed me home.
<<set $germ_followed to 1>>
Germ: Like, a guy from the tracks.
-> What? Ok whatever, I gotta get going.		
    Germ: Seeya.
-> What did you do?
    Germ: It was like some night back in the summer
    Germ: and out of the corner of my eye I could kinda see this dude
    Germ: take this turn out of the parking lot
    Germ: and start creepin' up the hill
    Germ: like 50 feet back.
    Germ: But like I didn't want to look right at him.
        ->aaagh...
        ->aaaaaaaaaagh...
    Germ: I took a detour through the woods and sat in a tree.
    Germ: It was getting dark but like
    Germ: I could see him standing just off the road
    Germ: waiting for me to come down.
        ->Oh my god.
        ->Holy shit.
    Mae: What did you do?
    Germ: After it got dark, I climbed down
    Germ: and got up the hill real quick.
    Mae: AAAAAAAAGH!!!
    Germ: Yeah it was a weird bad time.
    Mae: aaaaaagh....
    Mae: I am so upset right now.
    Germ: Ha ha I know right?
    Germ: Seeya!
    <<set $germ_followed to 2>>
[[WalkGermOut]]
===
title: Germ_A3D2_00
tags: 
colorID: 4
position: -14690,-1187
---
Mae: Hey.
Germ: Hey...
Mae: What?
Germ: Wanna hang out?
Mae: I got plans later...
Germ: Ok well I don't think you're gonna get kidnapped or anything.
-> Nah
    Germ: Cool cool.
    Germ: Seeya!
    <<set $germ_done to 1>>
-> Yeah sure!
    Germ: Hop on my bike!
    Mae: Really? Where is it?
    Germ: tied up behind the food donkey!
    Germ: I guess you could like run behind.
    Mae: Ok fine.
    <<set $germ_friendship_quest_2 to 1>>
    <<irisOut 1 wait>>
    <<scene Bridge>>
===
title: Germ_A3D3_01
tags: 
colorID: 4
position: -13941,-1318
---
Mae: what's traveling like?
Crusty3: s'good. s'good.
Crusty3: never could stay in one spot.
Mae: i have like the exact opposite problem
Crusty3: alright pardners. time for me to shove off!
Germ: See ya, Angel!
Mae: Stay safe!
Crusty3: adios!
<<close>>
<<walk Crusty3 BehindStairs>>
<<wait 2>>
<<hide Crusty3>>
<<set $crusty_3_done to 1>>
Germ: Well, that's over.
Mae: No fun?
Germ: No fun.
===
title: Germ_A3D4
tags: 
colorID: 4
position: -13209,-1455
---
<<if $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A3D4_00]]
<<elseif $germ is 1>>
<<endif>>
===
title: GermEnterParkingLot_A2D3
tags: 
colorID: 3
position: -16360,-893
---
<<wait 5>>
Germ: We're here!
Mae: We're in the parking lot.
Germ: Yep!
Mae: We walked for like 15 seconds. o>
Germ: It's like really big here.
Mae: I could throw a rock and hit where we were just standing.
Germ: You can see like all of the sky here.
Mae: oh wow you can
<<wait 5>>
Mae: ok so seriously we're just looking at the parking lot? _o_
Germ: sometimes
Mae: "sometimes"?
Germ: sometimes in the morning
Germ: if you're up on the hill back there
Germ: a whole buncha fog passes over the lots
Germ: and it's like an ocean
Germ: and there's a herd of deer that comes through
Germ: and it's like magic.
Mae: wow.
Germ: why did we ever need this much parking?
    ->railroads
        Mae: used to be like a big train station back here somewhere.
        Mae: back when people took trains.
        Mae: i mean like, pretty often.
        Mae: there was a restaurant.
    ->jobs
        Mae: people used to work at the glass factory
        Mae: and i guess some folks still work at the sewing place
        Mae: there was some other factory somewhere back here at some point too.
    ->stores
        Mae: there was the food donkey
        Mae: and the old second hand store
        Mae: and the shoe store
        Mae: there used to be a record store back here somewhere.
Germ: all gone now, though.
Mae: yep. just a lot of asphalt, weeds, and broken glass.
Germ: it's so pretty. i love it.
Mae: hm.
Germ: you know if they never do anything with this
Germ: in a bunch of years this is gonna be like
Germ: regular ground.
    ->wow.
    ->what's gonna happen to the parking lot?
        Mae: i mean the parking lot part of the parking lot.
Germ: Nature's old. It waits you out.
Mae: that's spooky.
<<wait 3>>
Germ: ok we're done!
Mae: what?
Germ: let's go!
//germ starts walking away//
Mae: hey!
<<irisOut 1 wait>>
<<scene Outskirts>>
===
title: GermEnterOutskirts_A2D3
tags: 
colorID: 3
position: -16363,-684
---
<<if $germ_friendship_quest_1>>
    Mae: So, I've been having-
    Germ: I have to go now!
    <<set $germ_done to 1>>
    Mae: Bye!
    <<set $germ_friendship_quest_1 to 0>>
    <<unlockDoodle GermFQ1>>
    <<set $did_germ_friendship_quest_1 to 1>>
    [[WalkGermOut]]
<<endif>>
===
title: Germ_A2D3
tags: 
colorID: 3
position: -16353,-1351
---
<<if $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A2D3_00]]
<<elseif $germ is 1>>
<<endif>>
===
title: Germ_A3D2
tags: 
colorID: 4
position: -14559,-1421
---
<<if $Bridge and $germ_friendship_quest_2>>
    [[Germ_Bridge]]
<<elseif $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A3D2_00]]
<<endif>>
===
title: Germ_A3D1
tags: 
colorID: 4
position: -14933,-1486
---
<<if $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A3D1_00]]
<<endif>>
===
title: Germ_A3D3_00
tags: 
colorID: 4
position: -14178,-1478
---
Mae: Hey.
Germ: Hey. Angel, this is my friend Mae.
Crusty3: 'Ey Mae. Whatsup.
Mae: cool name
Crusty3: gracias, much obliged
Germ: Angel's just in town between trains.
Germ: going west, next?
Crusty3: yeah, then south for the winter.
Crusty3: like a dirty birdie.
Crusty3: down to the gulf
Crusty3: lay in the sun
Crusty3: eat me some crawfish
Mae: actually Angel you might be able to answer a question:
Crusty3: shoot, bro
    ->have you heard anything about a ghost in town?
        Germ: Angel has indeed.
        Crusty3: we was just talking about it before you got here
        Mae: really?
        Crusty3: yeah, nobody ever stops here because of it
        Crusty3: but i ain't superstitious
        Crusty3: out on the west coast, i punched a cop
        Crusty3: and fought off his dog
        Crusty3: to keep a half pizza i found
        Mae: wow. finding pizza is a criminal offense?
        Crusty3: bro whatever they say is a criminal offense is a criminal offense
        Crusty3: but i digress
        Mae: ghost?
        Crusty3: yeah the dumb ones say the ghost grabs the good people
        Crusty3: i ain't good, so i got nothin' to fear
        Crusty3: you feel me?
        Mae: yeah.
        Germ: good by what standard?
        Crusty3: damned if i know, bro.
        Crusty3: but here's what i'll do:
        Crusty3: i don't believe in ghosts, but i know there's a god
        Crusty3: up there watching out for all their children down here
        Crusty3: and i'll say 7 prayers for you
        Crusty3: and that'll keep that nasty ghost at bay.
        Mae: Wow. Thanks.
        Mae: That is like, sincerely very kind of you.
    ->have you met anyone named Casey out on the rails?
        Crusty3: hm. lemme think.
        Crusty3: you know what he or she looked like?
        Mae: He. He looked kinda like me. A little taller. Orange-ish hair.
        Crusty3: Nah, man. I don't recollect.
        Crusty3: But I seen like, a thousand dudes out there.
        Mae: It's ok.
        Crusty3: I tell you what-
        Crusty3: I'll ask around, and if I find him
        Crusty3: or someone who's seen him,
        Crusty3: I'll say that Mae from Possum Springs is looking for him.
        Mae: Thanks. That means a lot. I owe you.
Crusty3: nah, nah
Crusty3: it's a cold world
Crusty3: run by the worst people
Crusty3: and good men die for god knows what reason
Crusty3: we gotta take on, like, duties for one another, ya know?
Mae: yeah.
Mae: thanks again.
===
title: Germ_A3D3_02
tags: 
colorID: 4
position: -13665,-1462
---
Germ: Something bad's coming.
Mae: I know.
Mae: It's a ghost, Germ. A real life ghost.
Germ: There aren't ghosts.
Germ: Ghosts aren't a thing.
Mae: how do you know?
Germ: my brother would have visited.
<<wait 2>>
Mae: geez.
Germ: Welp, dinner time!
Germ: See ya!
[[WalkGermOut]]
===
title: Germ_A3D3
tags: 
colorID: 4
position: -13958,-1822
---
<<if $germ is 0>>
    <<set $germ += 1>>
    [[Germ_A3D3_00]]
<<elseif $germ is 1>>
    <<set $germ += 1>>
    [[Germ_A3D3_01]]
<<elseif $germ is 2>>
    [[Germ_A3D3_02]]
<<endif>>
===
title: Germ_A3D4_00
tags: 
colorID: 4
position: -13196,-1163
---
Mae: Hey Germ.
Germ: Hey Mae.
Germ: Totally what's up?
Germ: Other than the obvious.
Germ: You seem... something too.
Mae: Do I look any different?
Germ: To me you do.
Mae: Thanks for your attention.
<<set $germ_done to 1>>
//[[Germ_A3D4_QuestCheck]]
===
title: GermEnterBridge_A3D2
tags: 
colorID: 4
position: -14417,-1039
---
<<if $germ_friendship_quest_2>>
    <<if $germ_bridge is 0>>
        <<walk Germ GermToBridge>>
        <<walk Mae MaeToBridge>>
        <<cameraLookAt CameraBridgeEdge 6>>
        Mae: you sure your bike’s gonna be safe?
        Germ: yeah. I don’t think anyone’s been in that shed for years
        Germ: sorry you fell off
        Mae: it’s cool. I feel so alive.
        Germ: scrapes are cool like that
        -> i skinned my elbow
        	Germ: i can like, ride you to the hospital
        	Germ: for this serious wound
            Mae: put the siren on
        -> i hit my head pretty hard
        	Germ: did you hurt your brain
        	Mae: naw, i got a thick skull
        	Germ: that’s cool
        <<close>>
        <<walk Germ GermOnBridge>>
        <<wait 1>>
        <<walk Mae MaeOnBridge>>
        <<cameraLookAt CameraBridgeMiddle 6>>
        <<waitForMove Germ>>
        //they walk out onto the bridge and Germ jumps up onto the railing.
        <<playOneShot event:/mae/footsteps/metal/footstep_metal_jump Germ>>
        <<jumpThroughUp Germ>>
        <<wait 2>>
        //We’ll need a Germ balancing idle here//
        Mae: So what are we doing here?
        Germ: do you trust me?
            ->sure
                Germ: really?
                Mae: yeah. i guess.
            ->never thought about it before
        Germ: ok
        Germ: bye
        <<close>>
        <<wait 1>>
        //Germ jumps off the bridge//
        <<jumpThrough Germ>>
        <<wait 1.5>>
        <<warp Germ GermOnBottom>>
        Mae: OH MY GOD GEEEERM!!!
        Mae: ...oh geez, dude...
        Germ: hey!
        Mae: WHAT THE HELL, MAN?
        <<resetRig Germ>>
        Germ: JUMP! It’s fine!
        Mae: oh for real?
        <<flip Germ -1>>
        <<cameraFollowPlayer 2>>
    <<endif>>
<<endif>>
===
title: Germ_Bridge
tags: 
colorID: 4
position: -14547,-731
---
<<if $germ_bridge is 1>>
    Mae: ok so...
    Germ: I started coming down here a few months ago
    Germ: to, like, just hang
    Mae: ok
    <<warp Rabies RabiesTunnelExit>>
    <<walk Rabies RabiesTunnelEnter>>
    Germ: and I noticed-
    Germ: oh! hey Rabies!
    <<playerFace 1>>
    <<lookForward Mae>>
    Mae: AAAGH!!
    Germ: Rabies is my sewer friend
    Mae: It’s name is Rabies?
    Germ: HIS name is rabies.
    Mae: His name is Rabies?
    Germ: I found him when they pulled that body out of here
    Germ: after that big flood a few months ago.
    Mae: oh neat.
    Germ: He was sad and wet and hungry so I brought him chips and ice cream
    Mae: sounds right
    Germ: and I wanted the other possums to not pick on him
    Germ: so i named him after something they’re all afraid of
    Mae: This is the best story ever.
    Germ: He’s so rad.
    Germ: HEY RABIES!
    Germ: SPEAK!
    <<playOneShot event:/bridge/possum_speak Rabies>>
    Rabies: HHHRRRRRGKK!!!
    Mae: oh my gaaaawd
    Germ: He’s gonna rule this whole ravine someday
    Germ: you be nice to Mae, ok Rabies!
    <<close>>
    <<walk Rabies RabiesTunnelExit>>
    Mae: I love him.
    Germ: you got any pets?
    <<if $times_fed_rats >= 2>>
    	Mae: I got some rats downtown.
    	Germ: sweet.
    <<else>>
        Mae: Not really...
        Germ: you should get one.
        Mae: my dad has a bird.
        Germ: that's a garbage pet.
        Mae: tell me about it.
    <<endif>>
    <<wait 2>>
    Mae: so there's like... weird stuff on the walls in here...
    Germ: ancient cave paintings.
    Mae: not new, at least.
    Germ: what do you think it means?
        ->if you sleep here you might drown
            Germ: true true.
            Mae: that's sad.
            Germ: yeah. but it's sad enough that like
            Germ: it gets too weird to be sad.
            Mae: i guess.
            Mae: just like
            Mae: sucks people are sleeping in here.
            Mae: not like we don't got vacant buildings.
            Germ: it's dumb.
        ->rabies is trash king
            Germ: it's a prophecy.
            Mae: written by ancient hobos.
    Germ: there's like
    Germ: all kinds of secret places like this around town.
    Germ: i'm like the only person to really map th-
    <<close>>
        <<trigger Rabies Candy>>
        <<warp Rabies RabiesTunnelExit>>
        <<walk Rabies RabiesTunnelEnter>>
        <<wait 2>>
        Germ: HEY RABIES!! WHATCHA GOT THERE???
        <<close>>
        <<waitForMove Rabies>>
        <<trigger Rabies SitUp>>
        <<playerFace 1>>
        Mae:{width=4} oh my gaaaaaaaaaaaaaaaawd!
        Mae: is that a Fiasco bar?
        Germ: you want me to unwrap that for you, guy?
        <<close>>
        <<trigger Rabies Unwrap>>
        <<wait 1>>
        Mae: HOLY EFFING CRAP!!!
        Germ: HE! IS! THE! BEST!
        Mae: You should take him home!
        Germ: No way. He’d pee all over everything
        Germ: and dig a hole in the wall and live there.
        Mae: oh yeah
        Germ: Rabies is wild. Let him be wild.
        <<close>>
        <<trigger Rabies Bite>>
        <<wait 2>>
        Mae: AAAAGGH LOOK HE’S EATING IT!!!
    [[GermBridgeEnding]]
<<endif>>
===
title: Landed
tags: 
colorID: 4
position: -14298,-814
---
<<flip Mae 1>>
Mae: dude you scared me to death!
Germ: why? You jump off big stuff all the time!
Mae: yeah but like... that’s in town. Nice flat ground.
Mae: not like i can just go jumping off cliffs and whatever
Germ: oh yeah i forgot you grew up in town
Mae: so what are we doing here?
Germ: i come here a lot.
Germ: wanted to show you something.
    ->you hang out at a sewer pipe?
        Germ: i mean, not exclusively
    ->are you showing me the sewer?
        Germ: i thought you'd be interested.
Germ: i saw like 10 rats down here once!
Mae: ok that’s pretty cool
<<close>>
<<walk Germ GermInTunnel 1>>
<<wait 5>>
<<set $germ_bridge to 1>>
===
title: GermEnterOutskirts_A3D4
tags: 
colorID: 4
position: -13197,-912
---
<<if $germ_friendship_quest_3>>
    Germ: Gotta go now.
    <<set $germ_done to 1>>
    Mae: Bye!
    <<unlockDoodle GermFQ3>>
    <<set $germ_friendship_quest_3 to 0>>
    <<set $did_germ_friendship_quest_3 to 1>>
    <<unlockAchievement He's_From_Somewhere>>
    [[WalkGermOut]]
<<endif>>
===
title: WalkGermOut
tags: 
colorID: 4
position: -13682,-173
---
<<close>>
<<walk Germ BehindStairs>>
<<set $germ_done to 1>>
===
title: GermEnterOutskirts_A3D2
tags: 
colorID: 4
position: -14123,-419
---
<<if $germ_friendship_quest_2>>
    Germ: See ya!
    <<set $germ_done to 1>>
    <<set $germ_friendship_quest_2 to 0>>
    <<unlockDoodle GermFQ2>>
    <<set $did_germ_friendship_quest_2 to 1>>
    [[WalkGermOut]]
<<endif>>
===
title: GermBridgeEnding
tags: 
colorID: 4
position: -14828,-642
---
<<irisOut 1 wait>>
<<scene Outskirts>>
===
title: UnusedGermAndRabiesStuff
tags: unused
colorID: 0
position: -15079,-638
---
        Mae: do you think that’s even remotely right?
        Germ: we’ll never know
        Mae: yeah i guess
        Germ: that’s like... a lot of life
        Mae: deep.
        Mae: so like, someone really died in here?
        Germ: yep, it was in the summer, and-
        <<close>>
        <<trigger Rabies Candy>>
        <<warp Rabies RabiesTunnelExit>>
        <<walk Rabies RabiesTunnelEnter>>
        <<wait 2>>
        Germ: HEY RABIES!! WHATCHA GOT THERE???
        <<close>>
        <<waitForMove Rabies>>
        <<trigger Rabies SitUp>>
        <<playerFace 1>>
        Mae:{width=4} oh my gaaaaaaaaaaaaaaaawd!
        Mae: is that a Fiasco bar?
        Germ: you want me to unwrap that for you, guy?
        <<close>>
        <<trigger Rabies Unwrap>>
        <<wait 1>>
        Mae: HOLY EFFING CRAP!!!
        Germ: HE! IS! THE! BEST!
        Mae: You should take him home!
        Germ: No way. He’d pee all over everything
        Germ: and dig a hole in the wall and live there.
        Mae: oh yeah
        Germ: Rabies is wild. Let him be wild.
        <<close>>
        <<trigger Rabies Bite>>
        <<wait 2>>
        Mae: AAAAGGH LOOK HE’S EATING IT!!!
        <<irisOut 1 wait>>
        <<scene Outskirts>>
===
title: Germ_A3D4_QuestCheck
tags: 
colorID: 4
position: -12954,-1170
---
<<if $did_germ_friendship_quest_1 and $did_germ_friendship_quest_2>>
	Germ: Do you want to come?
	Germ: Got a trampoline!
	-> Oh. Uh.
        Mae: Nah. I'm not really feeling well.
        Germ: Cool. Seeya!
        [[WalkGermOut]]
	-> Sure!
        Germ: Alright, hop on!
        <<set $germ_friendship_quest_3 to 1>>
        <<irisOut 1 wait>>
        <<scene GermHouse>>
<<else>>
    Germ: No prob!
    Germ: I gotta get home.
    Mae: Where do you live?
    Germ: Across the parking lots, up the hill.
    Germ: back in the woods.
    Mae: Huh. I've never been back there.
    Germ: I have!
    Mae: Yes you have.
    Germ: Seeya!
    [[WalkGermOut]]
<<endif>>
===
