title: Lori
tags: 
colorID: 0
position: 2478,1267
---
<<if $demo2>>
    <<if $lori is 0>>
        <<set $lori += 1>>
        [[LoriDemo2_00]]
    <<elseif $lori is 1>>
        <<set $lori += 1>>
        [[LoriDemo2_01]]
    <<elseif $lori is 2>>
        [[LoriDemo2_02]]
    <<endif>>
<<elseif $met_lori is 0>>
    <<if $act is 4 and $day is 3>>
        <<set $lori to 1>>
    <<endif>>
    [[LoriIntroConvo]]
<<elseif $act is 1>>
    [[A1.Emoticon]]
<<elseif $act is 2 and $day is 1>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A2D1.0]]
    <<elseif $lori is 1>>
        [[A2D1.Emoticon]]
    <<endif>>
<<elseif $act is 2 and $day is 2>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A2D2.0]]
    <<elseif $lori is 1>>
        [[A2D2.Emoticon]]
    <<endif>>
<<elseif $act is 2 and $day is 3>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A2D3.0]]
    <<elseif $lori is 1>>
        [[A2D3.Emoticon]]
    <<endif>>
<<elseif $act is 2 and $day is 4>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A2D4.0]]
    <<elseif $lori is 1>>
        [[A2D4.Emoticon]]
    <<endif>>
<<elseif $act is 3 and $day is 1>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A3D1.0]]
    <<elseif $lori is 1>>
        [[A3D1.Emoticon]]
    <<endif>>
<<elseif $act is 3 and $day is 2>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A3D2.0]]
    <<elseif $lori is 1>>
        [[A3D2.Emoticon]]
    <<endif>>
<<elseif $act is 3 and $day is 3>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A3D3.0]]
    <<elseif $lori is 1>>
        [[A3D3.Emoticon]]
    <<endif>>
<<elseif $act is 3 and $day is 4>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A3D4.0]]
    <<elseif $lori is 1>>
        [[A3D4.Emoticon]]
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    <<if $lori is 0>>
        <<set $lori to 1>>
        [[A4D3.0]]
    <<elseif $lori is 1>>
        [[A4D3.Emoticon]]
    <<endif>>
<<endif>>
===
title: LoriInitLevel
tags: 
colorID: 0
position: 1997,1219
---
<<if $night is 0 and $lori_day_done is 1>>
    <<hide Lori>>
<<else>>
    <<if $act is 1 and $TowneCentreEast and $met_lori is 0>>
        <<warp Lori LoriGround>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 3 and $day is 2 and $TowneCentreEast>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 2 and $day is 1 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 2 and $day is 2 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 2 and $day is 3 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 2 and $day is 4 and $TowneCentreEast>>
        <<warp Lori LoriGround>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 3 and $day is 1 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 3 and $day is 2 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 3 and $day is 3 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 3 and $day is 4 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $act is 4 and $day is 3 and $TowneCentreEast>>
        <<warp Lori LoriRoof>>
        <<show Lori>>
        <<sit Lori>>
    <<elseif $Tracks or $OverheadTracks>>
        <<show Lori>>
        <<flip Mae -1>>
    <<else>>
        <<hide Lori>>
    <<endif>>
<<endif>>
===
title: LoriIntroConvo
tags: 
colorID: 3
position: 1588,2031
---
<<if $act is 4 and $day is 3>>
    Lori: Hey killer.
    Mae: Geez! Who are you?
    Lori: Lori M.
    Mae: How do you know about the whole "killer" thing?
    Lori: You're kind of a local celebrity to some of us.
    Mae: ok.
    Mae: Have I seen you around somewhere before?
    Lori: I dunno. I'm up here most days.
    Mae: hm
    Lori: hm
    Mae: See ya.
    Lori: Later, killer.
    <<set $lori_done to 1>>
<<else>>
    Lori: Hey killer.
    Mae: Ugh. Don’t call me that. =_=
    Mae: Who are you?
    Lori: Lori M.
    Mae: How do you even know about the “Killer” thing?
    Lori: It was a big deal, even to us middle schoolers.
        ->Yeah well. That was a long time ago.
            Lori: Why did you do it?
            Mae: I don’t know.
            Lori: Did he have it coming?
            Mae: No.
            Lori: Our mothers told us not to talk to you.
            Lori: Because no one knew when you were gonna do it again.
        ->And what did the middle schoolers say
            Lori: You put him in the hospital.
            Lori: And no one knows why.
            Lori: Did he like have it coming or something?
            Mae: No.
            Lori: Our mothers told us not to talk to you.
            Lori: Because no one knew when you were gonna do it again.
    Mae: Well, say hi to your mom for me. She sounds lovely.
    Lori: Oh, she's gone.
    Mae: Oh. Sorry.
    Lori: It's fine.
    Mae: Yeesh. :\
    Mae: :| o_o 
<<endif>>

<<set $met_lori to 1>>
===
title: A2D1.0
tags: 
colorID: 3
position: 1837,2027
---
Mae: Hey.
Lori: Hey! It's you.
Mae: It's me.
Mae: I'm gonna sit. Legs are killing me.
Lori: Yeah it's a hike to get up here, right?
Mae: I'm just gettin' old. 20 years. :\
Lori: You're like ALL the way old.
Mae: How old are you? :|
Lori: 15 in February.
<<close>>
<<sit>>
Mae: I used to hang out here after school.
Lori: Really? Most kids don't come all the way up here.
Mae: Ha ha. That's why it's so nice. :) =_=
Lori: Better'n being at home!
Mae: o_o :|
Mae: Where do you live?
Lori: Chestnut street.
Mae: Oh ok. Out by the tracks.
Lori: Yeah... guess I'm not really "town".
Lori: I'm like
Lori: "tracks".
Mae: I live down on Maple.
Lori: Ooh! The rich neighborhood!
Mae: {emote=interrobang}
    ->What?
    ->Um... no.
Lori: Oh sorry.
Lori: I just meant it's nice...
Lori: *huff* *huff*
Lori: Nevermind it's ok I'm sorry I'm sorry
//lori goes into panic animation//
Mae: :(
Mae: It's ok! Calm down!
Lori: I'm sorry I get all weird sometimes I'm sorry
Lori: *huff* *huff* *huff* 
Mae: I'll forgive you if you stop apologizing!
Lori: *huff* *huff*
Lori: ok.
Mae: :|
//pause as transition back to lori normal//
Mae: The tracks are cool. Haven't been out there in a while.
Lori: The train shakes my room at night.
Mae: Yeah in the winter I can hear it. :)
Mae: One of my favorite noises.
Lori: Well you don't have to wake up with
Lori: *huff*
Lori: bits of ceiling on you.
Mae: Good point. :|
Mae: Alright
<<jump>>
Mae: I'm out! It was nice chatting!
Lori: I'm here most days.
Mae: Cool
Mae: See ya later! o/
Lori: Seeya killer.
Mae: Ugh. =_= _o_ 
Mae: o_o
<<set $can_do_lori_friendship_quest to 1>>
<<if $knows_mural and $talked_to_lori_about_mural is 0>>
    [[TalkAboutMural]]
<<endif>>
===
title: A2D1.Emoticon
tags: temp
colorID: 3
position: 2074,2027
---
Lori: {emote=crossbones}
===
title: A2D2.0
tags: temp
colorID: 3
position: 2320,2033
---
Mae: Hey
Lori: Hey
Mae: Gonna sit if that's cool.
Lori: It's cool.
<<close>>
<<sit>>
<<wait 2>>
Mae: Whatcha doing?
Lori: Drawing.
Mae: ok, whatcha drawing.
Lori: You know how in a movie
Lori: like a knife will go through someone's eye
Mae: whoa boy do i ever
Lori: I'm like designing an effect and makeup for that
Lori:{emote=eye_stab}
Lori: I'm working on a horror movie.
    ->You're gonna make a horror movie?
        Lori: No.
        Lori: Yeah.
    ->That's sooooo rad!
        Lori: ok maybe I'm not making it yet but
Lori: I mean someday I could like
Lori: direct it and write it and do the makeup.
Lori: I have a really good scream too.
Mae: Really?
Lori:{width=600} AAAAAAAAAAAAAAAAAAAAAAAAAAAAAGH!!!!
Lori: I want to like
Lori: go to horror movie school.
    ->I think they have those.
        Lori: Oh yeah they're really cool.
    ->They have those?
        Lori: Yeah!!!
Lori: I already know how to make fake blood and body parts.
Lori: Sorry, it's probably boring.
Mae: How in the world could that possibly be boring?
Lori: I know like all of them.
Mae: Body parts?
Lori: Horror movies.
Mae: Every one?
Lori: A lot of them.
Mae: cool cool cool.
Mae: alright, I gotta go.
<<jump>>
Lori: seeya!
Mae: later!
<<set $can_do_lori_friendship_quest to 1>>
<<if $knows_mural and $talked_to_lori_about_mural is 0>>
    [[TalkAboutMural]]
<<endif>>
===
title: A2D2.Emoticon
tags: temp
colorID: 3
position: 2565,2033
---
Lori: {emote=tombstone}
===
title: A2D3.0
tags: 
colorID: 3
position: 2818,2016
---
Mae: Hey kid. Whatcha up to?
Lori: Hey adult. Nothin'.
Mae: Sounds good.
<<if $can_do_lori_friendship_quest>>
    Lori: I was gonna go down to the tracks
    Lori: did you want to come?
    Lori: Sorry if that sounds boring
    Lori: You don't have to go.
    Lori: it's ok.
    Mae: ha ha geez let me answer before you decide I said no
    Lori: oh ok... did you want to go?
    -> Sure!
        Lori: oh good! yeah it'll be fun!
        Mae: let's rock
        <<irisOut 1 wait>>
        <<scene Tracks>>
    -> Actually I can't
        Lori: oh. oh okay.
        Lori: it's fine. it's fine.
        Lori: it'd be really boring anyway.
        Mae: Aw man I didn't mean to make you sad!
        Mae: I'm just busy today is all.
        Lori: it's cool it's cool it's cool
        Lori: see you later!
        Mae: ...see ya...?
<<else>>
    Lori: Probably gonna go hang by the train tracks.
    Mae: nice.
    Mae: see ya!
    Lori: bye!
<<endif>>
===
title: A2D3.Emoticon
tags: temp
colorID: 3
position: 3048,2004
---
Lori: {emote=crossbones}
===
title: A2D4.0
tags: temp blocking
colorID: 3
position: 3298,2009
---
Mae: You hype for Harfest?
Lori: Um. Harfest is pretty dumb, right?
Lori: I'm gonna watch a bunch of old Dracula movies from the 60s.
Mae: That actually sounds really cool.
Lori: It is. Gonna get a pizza too.
Lori: All to myself.
Mae: Wow. You have life way more figured out
Mae: than I did at your age.
Lori: really?
Lori: what were you doing then?
    ->hanging out in really embarrassing places online
    ->ruining my life
        Lori: oh
        Lori: sorry to bring it up!
        Mae: oh it's fine. just a thing that happened
        Lori: oh right. the thing.
        Mae: yup. that's my advice. 
        Mae: try not to have a thing.
        Lori: ok.
Mae: yep.
Lori: seeya!
Mae: later!
//lori walks left offscreen//
===
title: A2D4.Emoticon
tags: temp
colorID: 3
position: 3529,1982
---
Lori: {emote=tombstone}
===
title: A3D1.0
tags: 
colorID: 4
position: 1533,2803
---
Lori: Did you go to Harfest?
Mae: yeah. uh.
Mae: ended up in a play
Mae: because my friend Bea screwed it up
Lori: cool. i ate pizza and watched Bloodgorgon.
        ->cool
        ->bloodgorgon?
            Lori: Italian movie from like 1980
            Lori: lots of blood but it's like theatrical
            Lori: and stuff
            Lori: girl's boarding school
            Lori: buncha ghosts kidnapping them
            Lori: taking them to the-
            Mae: to the Bloodgorgon?
            Lori: yep.
Mae: ...
Lori: is something wrong?
Mae: nah. just.
Mae: think i saw a ghost or something last night
Mae: ugh I'm not sure
Lori: that sounds kind of awesome?
Mae: it was like, the opposite of awesome.
Lori: oh. sorry.
Mae: it's cool. i'll see you later.
Lori: good luck with your ghost problem!
Mae: thanks.
<<set $can_do_lori_friendship_quest to 1>>
<<if $knows_mural and $talked_to_lori_about_mural is 0>>
    [[TalkAboutMural]]
<<endif>>
===
title: A3D1.Emoticon
tags: temp
colorID: 4
position: 1765,2796
---
Lori: {emote=crossbones}
===
title: A3D2.0
tags: 
colorID: 4
position: 2006,2794
---
Mae: drawing something?
Lori: yeah drawing a bad guy for a movie
    <<close>>
    <<sit>>
    <<wait 2>>
Mae: have you like filmed anything ever?
Lori: i made a little movie with my phone
Lori: and i pirated some movie programs
Lori: it's real bad
    ->what's it about?
        Lori: um
        Lori: lady finds a window in her wall
        Lori: that wasn't there before
        Lori: and it's like looking at her backyard
        Lori: but it's always sunset
        Lori: and there's a guy standing there she can't quite see
        Mae: geez. that's actually kinda creepy
        Lori: really???
        Mae: i am creeped.
        Lori: oh thanks
        Lori: i thought it sucked
            ->where'd the idea come from?
                Lori: i like a lot of horror movies that are really gory
                Lori: but gory isn't always scary
                Lori: so i was thinking of what's scary
                Lori: and i dunno
                Lori: you're at home, but there's something new
                Lori: or maybe you just never noticed it before
                Lori: and there's someone strange very nearby
                Lori: and... i dunno
                Mae: ...
                Lori: what?
                Mae: nothing. creepy.
            ->so how did you film this?
                Lori: i shot it at my house
                Lori: i played both parts
                Lori: and some of it i animated on the computer
                Lori: it's extremely bad!
                Mae: I want to see it!
                Lori: no one will ever see it.
                Mae: do you still have it?
                Lori: yeah, i keep it like a secret shame.
                Mae: that should be the title.
                Lori: damn.
    ->i play bass real bad.
        Lori: how long have you been playing?
        Mae: enough to be real, real bad.
<<jump>>
Mae: alright! i'm off.
Lori: see ya!
Mae: later!
<<set $can_do_lori_friendship_quest to 1>>
<<if $knows_mural and $talked_to_lori_about_mural is 0>>
    [[TalkAboutMural]]
<<endif>>
===
title: A3D3.0
tags: 
colorID: 4
position: 2465,2795
---
Mae: Hey kid. Whatcha up to?
Lori: Hey adult. Nothin'.
Mae: Sounds good.
<<if $can_do_lori_friendship_quest>>
    Lori: was gonna go down to the tracks
    Lori: did you want to come?
    -> Sounds good, actually.
        Lori: oh good good good
        Lori: let's go
        Mae: totally let's
        <<irisOut 1 wait>>
        <<scene Tracks>>
    -> Actually I can't
        Mae: I'm like... tired right now.
        Lori: right now?
        Mae: Like, at this point in my life.
        Mae: maybe some other time.
        Lori: it's cooooool.
        Mae: See ya!
        Lori: bye!
<<else>>
    Lori: Gonna go hang at the tracks.
    Mae: Don't get hit or anything.
    Lori: i've lived next to the tracks all my life
    Lori: Only dorks and drunks get hit by trains.
    Mae: that's... harsh.
    Lori: it's a harsh world
    Mae: ...
    Mae: well, see ya!
    Lori: bye!
<<endif>>
===
title: A3D2.Emoticon
tags: temp
colorID: 4
position: 2236,2793
---
Lori: {emote=tombstone}
===
title: A3D3.Emoticon
tags: temp
colorID: 4
position: 2707,2797
---
Lori: {emote=crossbones}
===
title: A3D4.0
tags: 
colorID: 4
position: 2933,2796
---
Lori: You okay?
<<close>>
<<sit>>
Mae: Yeaaah.
Mae: I'm like
Mae: only half here right now
Mae: i'm just... tired.
Lori: are you sick or something
Mae: i don't even know anymore
Lori: you should get some cold medicine
Lori: and some crackers and ginger ale
Lori: and lay in bed and watch bad movies.
    ->thanks for the advice, doc
        Lori: don't mention it.
    ->i feel like that won't fix this one
        Lori: one time i got pneumonia
        Lori: and i had to go to school anyway
        Mae: isn't that contagious or something?
        Lori: i dunno.
Mae: =_=
//trigger hud2 drunk cam//
Mae: hhmmm hmmmm hmmm
Mae: hmmmm mmm mmmmm
Mae: -_-
Lori: Mae? you ok?
Mae: mm mmmm mmmmm
Lori: MAE!!!
//fade out//
Mae: o_o
Mae: ugh. sorry. spaced out there for a sec.
Lori: you should like
Lori: see a doctor
Mae: i'll get around to it.
Mae: thanks.
Lori: uh... no problem...
<<jump>>
Mae: alright well
Mae: i'll probably see you later.
Lori: probably?
Mae: nothing's certain i guess
Lori: ...ok
Mae: see ya
===
title: A3D4.Emoticon
tags: temp
colorID: 4
position: 3148,2790
---
Lori: {emote=tombstone}
===
title: A4D3.0
tags: 
colorID: 6
position: 3413,2792
---
Lori: Hey!
Lori: You ok?
<<close>>
<<sit>>
Mae: Hey.
Lori: I heard you were like in the hospital or something?
Mae: ha ha. yeah.
Lori: what for?
Mae: fell into a ravine while running through the woods at night
Lori: why?
Mae: um. chased by a death cult of conservative uncles.
Lori: alright. you don't have to tell me.
<<if $did_lori_friendship_quest_1>>
    <<wait 2>>
    Lori: so you're really ok?
    Mae: yeah. just been a weird few days.
    Lori: ok. i got worried.
    Mae: aw. i'm super tough.
    Mae: nothing's gonna get me anytime soon.
    Lori: oh good. that's good.
    <<wait 2>>
    <<if $did_lori_friendship_quest_2>>
        <<if $knows_mural>>
            Mae: I wonder if they'll ever figure out who messed up the dudes.
            Lori: the dudes?
            Mae: the mural. In the tunnel.
            Lori: oh, I did that
        <<else>>
            Lori: So we're friends, right?
            Mae: yeah.
            Lori: ok.
            Lori: i messed up the mural.
        <<endif>>
        Mae: WHAT?!
        Mae: Why?
        Lori: I don’t know!
        Mae: THAT'S NOT A REASON?!
        Lori: Why do you care so much?
            ->it's The Dudes!
                Mae: You don't mess with the Dudes like that!
            ->The Dudes! the Dudes are sacred!
                Lori: Why?
        Mae: They're like a part of this place!
        Lori: This entire place is royally messed up!
        Lori: And nobody cares!.
        Lori: I wanted someone to notice things aren’t ok.
        Lori: This town is like a horror movie that never stops
        Lori: and it’s not even a good one!
        Mae: yeah, it might suck!
        Mae: but it’s /our/ horror movie
            ->and that's kind of cool, right?
            		Lori: yeah but…
                    <<wait 2>>
            		Mae: yeah. i know.
                    Mae: i have the same thing inside of me
                    Lori: the thing
                    Mae: the thing
                    Lori: i feel like no one cares
                    Lori: everything just... sucks forever.
                    Lori: there's thing thing coming for us
                    Lori: happening to us
                    Mae: yeah but like
                    Mae: this isn't advice i'm good at taking, but
                    Mae: sometimes things are big enough and bad enough
                    Mae: that they're unkillable
                    Mae: but, ya know, life goes on
                    Lori: yeah. that's not really comforting.
                    Mae: heh heh.
                    Mae: well, I'm still working out a better pep talk for myself too
                    Lori: ha ha ha. fair enough.
            	->what kind of movie would you want it to be?
            		Lori: I don’t know.
            		Lori: Romantic Comedy.
            		Lori: Everyone lives in clean shiny places there
            		Lori: and they’re all pretty
            		Lori: and they kiss.
            		Mae: But they’re horrible to sit through.
            		Mae: and you end up hating every person onscreen
                    Lori: …
            		Lori: … yeah.
            		Lori: they’re always pretty hateable.
            		Mae: it’s ok to hate them
            		Mae: they’re not real people
            		Lori: Yeah. Fictional people don’t mind.
            		Mae: I meant the actors.
            		Lori: me too.
            Mae: Ya know
            Mae: Does your family do stuff for the holidays?
            Lori: My dad’s usually away on thanksgiving
            Lori: but he’s home for Longest Night.
            Mae: You should come do thanksgiving at my house
            Lori: Oh no. No no no.
            Mae: Come on.
            Mae: We have 4 chairs and there’s only 3 of us.
            Mae: that other chair usually just has junk on it.
            Lori: No no no. I can’t.
            Mae: Why not? 
            Lori: *huff huff*
            Lori: It’s fine.
            //Lori goes into panic attack mode//
            Lori:{width=8} itsfineitsfineitsfineitsfine
            Mae: hey hey ok. we can talk about it later.
            Lori: *huff huff huff*
            Lori: Thank *huff* you *huff*
            Mae: You ok? Can I do anything?
            Lori: No it’s *huff* cool I just need to *huff* sit here and be
            Lori: *huff*
            Lori: quiet for awhile.
            Mae: Ok. I’ll leave you alone then!
            Mae: See you tomorrow probably.
            Lori: Mae?
            Mae: yep!
            Lori: Thanks for *huff* hanging out me.
            Mae: No prob, kid.
            Mae: try not to like, burn something down in the meantime.
            Lori: ha ha ha
            Lori: *huff*
            Mae: see ya!
        <<unlockAchievement Horrorshow>>
        <<jump>>
    <<endif>>
<<else>>
    <<wait 3>>
    Lori: you... you wanna hang out sometime?
    Mae: isn't that what we're doing?
    Lori: i mean like we could watch horror movies
    Lori: you could meet my sister
    Lori: she's like almost your age
        ->what's her name? i probably know her from school.
            Lori: Stevie Meyers. But you don't know her.
            Lori: she beat up someone and had to go to a different school.
            Mae: oh cool. we might get along.
            Lori: ha ha probably.
            Lori: she's mean sometimes but she's cool.
            Mae: Sounds good.
        ->i don't think i know her
            Lori: you don't.
            Lori: she's not home all the time
            Lori: but I can like... plan it
            Lori: it can be a party
            Lori: it's gonna be great it's gonna be great it's gonna be great
            Mae: sounds like it's gonna be great
    <<jump>>
    Mae: I gotta get going.
    Mae: It's been a weird past couple of days.
    Lori: Weird's good.
    Mae: I guess. Weird's just what happens.
    Lori: see you around.
    Mae: later, kid.
    Lori: later, killer.
<<endif>>
===
title: A4D3.Emoticon
tags: temp
colorID: 6
position: 3676,2793
---
Lori: {emote=crossbones}
===
title: TalkAboutMural
tags: 
colorID: 0
position: 3125,2268
---
Mae: You see someone messed up the mural in the tunnel?
Lori: yeah ha ha pretty funny
Mae: ...
Lori: i mean very weird and bad
Mae: any idea who did it?
Lori: nope.
Lori: lots of suspects though
Mae: yeah... whole town fulla suspects
<<set $talked_to_lori_about_mural to 1>>
===
title: A1.Emoticon
tags: temp
colorID: 0
position: 1715,1779
---
Lori: {emote=tombstone}
===
title: EnterTowneCentreEastFromTracks_A2D3
tags: 
colorID: 0
position: 2828,2240
---
Lori: seeeeeeyaaaaa!
===
title: EnterTowneCentreEastFromTracks_A3D3
tags: 
colorID: 0
position: 2442,3033
---
Lori: laaaaaaateerrrrrrrrrssssss{width=8}
===
title: LoriDemo2_00
tags: demo2
colorID: 2
position: 1129,1828
---
Lori: Hey killer. #line:11cc47
Mae: Ugh. Don’t call me that. =_= #line:7742cb
Mae: Who are you? #line:e3f2a8
Lori: Lori M. #line:642058
Mae: How do you even know about the “Killer” thing? #line:83c4ea
Lori: It was a big deal, even to us middle schoolers. #line:5fc494
[[Yeah well. That was a long time ago.|Lori_LongTimeAgo]] #line:fc9fb2
[[And what did the middle schoolers say?|Lori_MiddleSchoolers]] #line:b83ed9
===
title: Lori_LongTimeAgo
tags: demo2
colorID: 2
position: 917,2152
---
Lori: Why did you do it? #line:0ca106
Mae: I don’t know. #line:aee15b
Lori: Did he have it coming? #line:c2bfab
Mae: No. #line:74bbd1
Lori: Our mothers told us not to talk to you. #line:9894b1
Lori: Because no one knew when you were gonna do it again. #line:55ea64
[[Lori_00_End]]
===
title: Lori_MiddleSchoolers
tags: demo2
colorID: 2
position: 1246,2143
---
Lori: You put him in the hospital. #line:f7a0d8
Lori: And no one knows why. #line:6bac83
Lori: Did he like have it coming or something? #line:b7fe54
Mae: No. #line:74f6ce
Lori: Our mothers told us not to talk to you. #line:3c8295
Lori: Because no one knew when you were gonna do it again. #line:09aa43
[[Lori_00_End]]
===
title: Lori_00_End
tags: demo2
colorID: 2
position: 1118,2447
---
Mae: Well, say hi to your mom for me. She sounds lovely. #line:c80d1a
Lori: I will. #line:f0cd56
Mae: Yeesh. :\ #line:021c81
Mae: :| o_o #line:9522f9
===
title: LoriDemo2_01
tags: demo2
colorID: 3
position: 440,1788
---
Mae: I'm gonna sit. My legs are tired. #line:970a9d
Lori: Yeah it's a hike to get up here, right? #line:71f0f4
Mae: I'm just gettin' old. 20 years. :\ #line:dc84ab
Lori: You're like ALL the way old. #line:8808ac
Mae: How old are you? :| #line:83d25d
Lori: 16 in February. #line:a73c78
<<close>>
<<sit>>
Mae: I used to hang out here after school. #line:ba5b6d
Lori: Really? Most kids don't come all the way up here. #line:cca6f6
Mae: Ha ha. That's why it's so nice. :) =_= #line:2db058
Lori: Better'n being at home! #line:c3b44c
Mae: o_o :| #line:e9eae9
Mae: Where do you live? #line:8448e0
Lori: Chestnut street. #line:2e0b44
Mae: Oh ok. Out by the tracks. #line:889080
Lori: Yeah... guess I'm not really "town". #line:0c9148
Lori: I'm like #line:455e76
Lori: "tracks". #line:ca56ab
Mae: I live down on Maple. #line:0aa43c
Lori: Ooh! The rich neighborhood! #line:43988c
Mae: {emote=interrobang} #line:293c04
<<close>>
    ->What? #line:33a308
    ->Um... no. #line:648086
Lori: Oh sorry. #line:635a37
Lori: I just meant it's nice... #line:4ed887
Lori: Nevermind it's ok I'm sorry I'm sorry #line:5bbbe8
Mae: :( #line:ffc8b9
Mae: It's ok! Calm down! #line:ed8b26
Lori: I'm sorry I get all weird sometimes I'm sorry #line:54352d
Mae: I'll forgive you if you stop apologizing! #line:1b52fb
Lori: ok. #line:20fef1
Mae: :| #line:752649
Mae: The tracks are cool. Haven't been out there in a while. #line:4ecfaf
Lori: The train shakes my room at night. #line:5b0586
Mae: Yeah in the winter I can hear it. :) #line:b671d7
Mae: One of my favorite noises. #line:c8b9f6
Lori: Well you don't have to wake up with bits of ceiling on you. #line:8aaebe
Mae: Good point. :| #line:dbba2d
Mae: Alright #line:849859
<<jump>>
Mae: I'm out! It was nice chatting! #line:2b0efd
Lori: I'm here most days. #line:1b8333
Mae: Cool #line:ba6063
<<if $knows_mural is 1 and $asked_lori_mural is 0>>
    Mae: Speaking of nothing, did you see the tunnel got hecked up? #line:cbedba
    Lori: Yeah, weird right? #line:283a0c
    Lori: Town council and that cop pretty much chased everyone out earlier. #line:33ec8c
    Mae: I was wondering where all the kids were. #line:81c19a
    Lori: Heh heh. They're all older than me. #line:33b574
    Lori: My sister hangs out with them. #line:46eb89
    Mae: <o> -_- #line:b3f214
        ->Maybe I should ask your sister about it. #line:fd18fa
            Lori: I don't think she knows anything. #line:2676aa
            Mae: Yeah, you're probably right. #line:f77845
            Mae: Any idea where she hangs out? #line:518a6c
            Lori: um... she's in the tunnel a lot... #line:dfdecd
            Lori: when there isn't a cop hanging out. #line:99b00a
            Mae: Fair. #line:328a17
        ->Any idea who did it? #line:82aea3
            Lori: Maybe that one guy? Steve? #line:0e96bc
            Mae: Steve? #line:8dac24
            Lori: Yeah. Like he's out of school #line:2c4cc4
            Lori: but like still comes to school parties. #line:8409a4
            Lori: Hits on girls in my class. #line:b9b151
            Mae: =_= _o_ #line:66263f
            Mae: Oh god. Steve Scriggins. #line:b153fa
             <<set $suspect_steve to 1>>
            Lori: Yeah. Him. #line:0cb166
            Mae: Ok. That makes sense. #line:4e8e02
            Lori: Any idea where he hangs out? #line:9b1b39
            Mae: I dunno. I just got back into town a few days ago. #line:294359
    Mae: Thanks, kid. o_o #line:77996d
    <<set $asked_lori_mural to 1>>
<<endif>>
Mae: See ya later! o/ #line:bfd689
Lori: Seeya killer. #line:81e28a
Mae: Ugh. =_= _o_ #line:69a449
Mae: o_o #line:45be52
<<unlockDoodle LoriM>>
===
title: LoriDemo2_02
tags: demo2
colorID: 3
position: 681,1852
---
Lori: {emote=music} #line:df953e
===
