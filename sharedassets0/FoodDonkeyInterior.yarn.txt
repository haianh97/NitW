title: EnterLevel
tags: 
colorID: 3
position: -1545,1363
---
<<if $act is 2 and $day is 1>>
    <<if $gregg_friendship_quest is 1>>
        <<show Gregg>>
        <<show SteveScriggins>>
        <<setCanInteract Gregg 0>>
        <<setCanInteract SteveScriggins 0>>
        <<walk Gregg GreggWalkIn>>
        <<walk Mae MaeWalkIn wait>>
        <<playerCanRun 0>>
        <<playerCanJump 0>>
        [[Intro]]
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    [[LastDayScene]]
<<else>>
    <<set $wont_open_food_donkey_basement_door_1 to 0>>
<<endif>>
===
title: InitLevel
tags: 
colorID: 0
position: -1618,1131
---
<<hide Gregg>>
<<hide SteveScriggins>>

<<set $arcade_machine_1_done to 1>>
<<set $arcade_machine_2_done to 1>>
<<set $arcade_machine_3_done to 1>>
===
title: PickRobotMascot0
tags: 
colorID: 3
position: -867,1849
---
Gregg: Aw yeah. Froggy robot.
SteveScriggins: Finally. Goddamn.
Mae: Oh shut up Steve.
<<set $robot_head to 0>>
[[ArcadeMachinePicked]]
===
title: PickRobotMascot1
tags: 
colorID: 3
position: -600,1853
---
Gregg: Yeah! Robot piggy!
SteveScriggins: Finally. Goddamn.
Mae: Oh shut up Steve.
<<set $robot_head to 1>>
[[ArcadeMachinePicked]]
===
title: PickRobotMascot2
tags: 
colorID: 3
position: -322,1856
---
Gregg: Cool cool. Mecha-bunn.
SteveScriggins: Finally. Goddamn.
Mae: Oh shut up Steve.

<<set $robot_head to 2>>
[[ArcadeMachinePicked]]
===
title: Gregg
tags: 
colorID: 3
position: -653,933
---
<<if $gregg_friendship_quest_1 is 0>>
    <<stopDialogue Gregg>>
    [[CowHeadConvo]]
<<elseif $gregg_friendship_quest_1 is 1>>
    <<if $food_donkey_key_ring > 0>>
        <<stopDialogue Gregg>>
        [[DoorUnlock]]
    <<elseif $gregg_fq1_look is 0>>
        <<stopDialogue Gregg>>
        [[DoorLocked]]
    <<elseif $gregg_fq1_look is 1>>
        <<set $gregg_fq1_look to 2>>
        [[GreggLook1]]
    <<elseif $gregg_fq1_look is 2>>
        <<set $gregg_fq1_look to 3>>
        <<setCanInteract Gregg 0>>
        [[GreggLook2]]
    <<endif>>
<<elseif $gregg_friendship_quest_1 is 2>>
    <<stopDialogue Gregg>>
    Mae: Oh god. These guys.
    Mae: I was both terrified and super into these things
    Mae: when they'd put them out front.
    Mae: They moved!
    Gregg: They look so tiny when they're in pieces.
        ->I dunno man they're still pretty big.
            Mae: God, these things were actually gigantic when they had them out.
        ->We're smaller in pieces too.
            Gregg: Too real.
            Gregg: I hope you get chopped up by a chopper.
            Mae: I hope you get chopped up by a blunt chopper.
            SteveScriggins: You two are weird.
    Gregg: ok, we're stealing one of these
    Mae: is it even stealing?
    Gregg: it's better if we think of it as stealing.
    Mae: cool
    <<close>>
    <<wait .5>>
    Gregg: aaaagh.
        -> aaagh?
        -> what?
    Gregg: aaaaaaaaaagh.
        -> are you ok?
        -> dude, what?
    Gregg: {width=300}aaaaaaaaaaaaaaaaaaaaaaaaaagh!!!
        -> DUDE! WHAT!
        -> STOP SCREAMING!!!
    Gregg: I can't pick! They all look great!
    SteveScriggins: You get one. That's the deal. Hurry up.
    Mae: Oooh let me pick! Let me pick! :)
    <<close>>
    <<run Gregg Gregg3 -1 wait>>
    Gregg: sure.
    Mae: :|
    <<set $gregg_friendship_quest_1 to 3>>
    <<set $robot_head_choice_enabled to 1>>
<<elseif $gregg_friendship_quest_1 is 3>>
    <<if $gregg_fq1_try_decide is 0>>
        <<set $gregg_fq1_try_decide += 1>>
        [[TryToDecide00]]
    <<elseif $gregg_fq1_try_decide is 1>>
        <<set $gregg_fq1_try_decide += 1>>
        [[TryToDecide01]]
    <<elseif $gregg_fq1_try_decide is 2>>
        [[TryToDecide02]]
    <<endif>>
<<endif>>

===
title: ArcadeMachinePicked
tags: 
colorID: 3
position: -607,2238
---
<<close>>
<<wait 1>>
SteveScriggins: You know what? I ain't carrying nothin'.
SteveScriggins: See ya ladies.
<<close>>
<<walk SteveScriggins SteveScriggins1>>
Mae: Steeeeeeeve!?
SteveScriggins: Get found dead, punks!
SteveScriggins: Hweh heh heh.
<<close>>
<<wait 2>>
<<hide SteveScriggins>>
Gregg: He says punks like it's a bad thing.
Mae: I don't care.
Mae: He's scum and a criminal and anyway I think he's been bad news from the start.
Gregg: Mm. Yes.
Mae: What deal did you make with him?
Gregg: He's allowed to shoplift whatever he wants from the Snack Falcon while I'm working.
Mae: See? Criminal all the way.
Gregg: Some people...
<<close>>
<<wait 1>>
Mae: So should we like smash a window to get this out of here?
<<close>>
<<set $robot_crate to 1>>
<<irisOut 1 wait>>
<<scene CarryingRobotMascot>>
===
title: NotSure
tags: 
colorID: 3
position: -318,1575
---
Gregg: Let me know when you're ready.
===
title: Intro
tags: 
colorID: 3
position: -1896,1678
---
Mae: Oh god. What is that smell?
SteveScriggins: Glue.
Mae: Oh no. Is that-
<<walk SteveScriggins SteveWalkIn>>
SteveScriggins: [wave]Sup ladies.[/wave]
Mae: =_=
<<vo Mae dissapointed>>
Mae: ugh. :(
Mae: Effing Steve Scriggins.
SteveScriggins: *FFFFFFFTTTTT!!*
Mae: Did you just actually make a fart noise?
SteveScriggins: Hweh heh heh.
SteveScriggins: You ladies lost or something?
Gregg: Deal's a deal, Steve.
SteveScriggins: I ain't forgot.
Gregg: Where is it then?
SteveScriggins: In the back.
Mae: I remember you, you know.
SteveScriggins: I bet [wave]everyone[/wave] remembers you.
    ->That teacher sue you yet?
        Mae: Or at least beat you up in a parking lot?
        SteveScriggins: Which teacher?
        Mae: The one you locked in the closet during Longest Night break.
        SteveScriggins: Pssh. That guy can't even go in small rooms now.
        SteveScriggins: He was always a wuss, just needed someone to bring it out.
        Mae: ... O_O
        Gregg: ... O_O
    ->What are you doing in here?
        SteveScriggins: Your mom.
        Mae: Great Steve.
        SteveScriggins: All the ways.
        Mae: Cool.
        SteveScriggins: I'm your new dad.
        Mae: Steve, you sincerely could not get with my mom.
        SteveScriggins: Is that a challenge?
Mae: o_o :|
Gregg: o_o
Gregg: ok well
Gregg: can we get on with this?
<<close>>
<<run SteveScriggins SteveScriggins1 -1>>
<<run Gregg Gregg1>>
<<wait 3>>
<<setCanInteract Gregg 1>>
<<runDialogue Gregg GreggScrigginsBG1>>

===
title: GetKeyRing
tags: 
colorID: 3
position: 264,380
---
<<stopDialogue Gregg>>
Mae: No wonder this place went under.
Mae: People leaving keys all over the place!
Mae: Don't mind if I do.
<<fadeOut 0>>
<<playOneShot event:/food_donkey/key_pickup Mae>>
<<wait 1>>
<<set $food_donkey_key_ring to 1>>
<<set $got_food_donkey_key_ring to 1>>
<<setPlayerInteractLocator Gregg MaeDoorConvo>>
<<warp Gregg GreggDoor2>>
<<flip Gregg 1>>
<<clearAltBodyRig Gregg>>
<<setAnimInt Gregg kickingDoor 1>>
<<setAnimInt SteveScriggins kickingDoor 1>>
<<warp Mae MaeFoundKey>>
<<flip Mae 1>>
<<fadeIn 1 wait>>
-> Found some keys!
    <<close>>
	<<wait 2>>
	Mae: Guys?
-> I am the keymaster!
	Mae: I will unlock your ass! \o/ 
	SteveScriggins: Yeah I heard that about you!
Mae: _o_
<<setCanInteract Gregg 1>>
===
title: RobotMascot0
tags: 
colorID: 3
position: 118,1162
---
Mae: Ol' greenie here.
Gregg: I used to have dreams about this thing.
SteveScriggins: Sex dreams
SteveScriggins: Probably.
SteveScriggins: Best you could get.
SteveScriggins: Probably.
Mae: ...
Gregg: Do frogs even have sex?
Mae: Nah, they got like eggs or something.
SteveScriggins: ...
SteveScriggins: You two are really, really dumb.
<<set $robot_head_0_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: RobotMascot1
tags: 
colorID: 3
position: 408,1165
---
Mae: Pretty cool pig.
Gregg: Yeah pretty alright.
Gregg: Didn't he used to hold like sausage links or something?
Mae: Yeah! That's messed up!
SteveScriggins: Eatin' his own kind.
Mae: or her own kind.
SteveScriggins: Whatever, PC-police.
Mae: PC yourself into traffic Steve.
Gregg: ANYWAY.
<<close>>
<<wait .5>>
Mae: Cannibal pig, man.
Gregg: Cannibal pig.
<<set $robot_head_1_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: RobotMascot2
tags: 
colorID: 3
position: 655,1160
---
Mae: God. I remember these being less horrifying.
Gregg: They were tall then. Couldn't see their faces.
SteveScriggins: I gotta look at you ladies' faces.
SteveScriggins: Hweh heh heh.
<<lookAt Mae SteveScriggins>>
Mae: -_-
Gregg: I think this one's kinda cute.
SteveScriggins: Aw, he thinks it's cute.
Mae: Steve you are a disease.
Mae: o_o
<<set $robot_head_2_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: Checked2ndRobotMascot
tags: 
colorID: 3
position: 412,1539
---
SteveScriggins: GEEZ. Why does she get to pick?
SteveScriggins: Why is this such a thing?
SteveScriggins: This is taking forever!
Mae: :( \o/
Mae: [shake=.05]SCRIGGINS! BITE MY ENTIRE ASS![/shake]
SteveScriggins: WHAT?!
Mae: [shake=.05]GO LEGALLY BRAIN DEAD[/shake]
Mae: [shake=.05]FROM LACK OF OXYGEN[/shake]
Mae: [shake=.05]DUE TO CHOKING ON MY ENTIRE ASS![/shake]
SteveScriggins: I... I... what?
Gregg: Classic.
Mae: [shake=.05]THAT'S RIGHT, STEVE! CLASSIC![/shake]
SteveScriggins: [shake=.05]OH MY GOD CAN WE JUST GET THIS DONE ALREADY[/shake]
Mae: :| _o_
===
title: DecisionPoint
tags: 
colorID: 3
position: -610,1588
---
Mae: Ok kids we're gonna go with...
[[Actually, I'm not sure yet...|NotSure]]
<<if $robot_head_0_done is 1>>
    [[Frog Head|PickRobotMascot0]]
<<endif>>
<<if $robot_head_1_done is 1>>
    [[Pig Head|PickRobotMascot1]]
<<endif>>
<<if $robot_head_2_done is 1>>
    [[Rabbit Head|PickRobotMascot2]]
<<endif>>
===
title: TryToDecide00
tags: 
colorID: 3
position: -957,1288
---
Gregg: Ugh... which one?
Gregg: I'm bad at decisions that actually mean something.
Mae: You picked out the tuxes we wore to junior prom.
Gregg: Ok yeah... I'm pretty good at it.
Mae: Remember how Tim Whitley rode that giant tractor into homecoming?
Gregg: Totally.
Gregg: So... did you pick?
-> Dude gimme time.
    Gregg: Ugh fine.
    Mae: Dude, is this serious business?
    Gregg: This is serious business.
    Mae: Ok then let's treat it like serious business.
    Gregg: I'm hungry though.
    Mae: Uuuugh me too.
    Gregg: We should eat later.
    Mae: Yeah probably.
    SteveScriggins: I DO HAVE places to be, you know!	
-> I totally did.
    Gregg: Oh cool
    [[DecisionPoint]]
===
title: TryToDecide01
tags: 
colorID: 3
position: -621,1275
---
Gregg: Which one are we going with?
-> [shake=.05]YOU GIVE ME A SEC.[/shake]
    Mae: \o/
    <<dilate Mae .5 .25>>
    Gregg: DUDE. CHILL.
    <<dilate Mae 1 .5>>
    Mae: _o_
-> Um... ok. I think I got it.
    Gregg: Sweeeeet.
    [[DecisionPoint]]

===
title: TryToDecide02
tags: 
colorID: 3
position: -339,1279
---
Gregg: Anything?
-> Nope!
-> Yep!
    Gregg: YAY!
    [[DecisionPoint]]
===
title: GreggScrigginsBG1
tags: 
colorID: 3
position: -308,360
---
Gregg: {emote=hotdog}
SteveScriggins: {emote=hotdog}
[[GreggScrigginsBG1]]
===
title: CowHeadConvo
tags: 
colorID: 3
position: -1149,1050
---
Gregg: So this still works sometimes?
SteveScriggins: Eh.
SteveScriggins: You know Beth Holstead?
Gregg: Yeah.
SteveScriggins: She was in here like 6 months ago and she swears it talked.
Gregg: Whoa.
SteveScriggins: Didn't even have to push the button.
SteveScriggins: But I dunno how much stake I’d put in it.
SteveScriggins: You know Beth.
Gregg: Kinda?
SteveScriggins: There’s a lot of glue in here.
Gregg: I guess.
<<set $gregg_friendship_quest_1 to 1>>
<<setInteractDir Gregg -1 0>>
<<walk SteveScriggins SteveScrigginsDoor1>>
<<walk Gregg GreggDoor1>>
<<runDialogue Gregg GreggScrigginsBG2>>
===
title: GreggScrigginsBG2
tags: 
colorID: 3
position: -72,373
---
<<waitForMove SteveScriggins>>
SteveScriggins: {emote=cloud_rain}
<<close>>
<<wait 1>>
[[GreggScrigginsBG2]]
===
title: DoorLocked
tags: 
colorID: 3
position: -1144,801
---
SteveScriggins: [shake=.05]GODDAMMIT!!![/shake]
Mae: What?
SteveScriggins: Someone locked the effin’ door!
SteveScriggins: Goddammit I bet it was Beth.
SteveScriggins: Or Levy.
SteveScriggins: Or the train trash.
Gregg: So...
SteveScriggins: AAAAAAAAAAAARGH
SteveScriggins: They better not have stolen anything.
Mae: ...from the abandoned grocery store...
Mae: ...with the unlocked doors…
Mae: What’s behind this door anyway?
SteveScriggins: Could probably bust it open...
Gregg: Or I could go get my lockpick stuff.
Mae: I bet there’s like a key or something.
SteveScriggins: Whatever.
<<setAnimInt SteveScriggins kickingDoor 1>>
<<close>>
<<run Gregg GreggLook1>>
<<wait 3>>
<<stopMoving Gregg>>
<<warp Gregg GreggLook1>>
<<setAltBodyRig Gregg GreggRummageRig>>
<<playerCanRun 1>>
<<playerCanJump 1>>
<<set $gregg_fq1_look to 1>>
===
title: GreggLook1
tags: 
colorID: 3
position: -1108,506
---
Mae: What are you doing?
Gregg: Trying to find something to help bust down the door.
Mae: Cool. I’m looking for a key because I’m smarter.
Gregg: Uh huh. But I get to hit things.
Mae: ...
Mae: crap.
===
title: GreggLook2
tags: 
colorID: 3
position: -817,445
---
Gregg: How much glue did they need in here?
Mae: Dude you gotta stick stuff to stuff to run a successful business.
Gregg: Well, apparently not.
Gregg: What are you doing?
Mae: Gonna check that office up there.
Gregg: Cool. Don’t like slice yourself to death on the glass.
Mae: Dumb death.
Gregg: Dumb death.
===
title: DoorUnlock
tags: temp
colorID: 3
position: -228,847
---
<<setPlayerInteractLocator Gregg>>
Mae: Yo idiots stop kicking before you break your femurs or something.
Gregg: I’m not an idiot.
Mae: Yeah sorry hey idiot and Gregg stop kicking.
Mae: I got keys.
<<setAnimInt SteveScriggins kickingDoor 0>>
<<setAnimInt Gregg kickingDoor 0>>
<<walk Gregg GreggDoor1 1>>
<<flip SteveScriggins -1>>
SteveScriggins: Where’d you get them?
Mae: Office. Up there.
SteveScriggins: No wonder this place went under.
SteveScriggins: Leaving keys all over the place!
Mae: That was exactly what I thought.
<<walk SteveScriggins SteveScrigginsDoor2 1>>
<<walk Mae GreggDoor2>>
Mae: I’m upset that we thought like the exact same words.
SteveScriggins: I thought it better.
<<lookRight Mae>>
Mae:{emote=food_donkey_key}
<<fadeOutMusic 5>>
<<close>>
<<playOneShot event:/food_donkey/key_door Mae>>
<<wait 1.1>>
<<playOneShot event:/food_donkey/door_open Mae>>
<<wait 1>>
<<tell BasementDoor Unlock>>
<<wait 1>>
<<walk SteveScriggins SteveScriggins2>>
<<wait 1.5>>
<<setInteractDir Gregg 1 0>>
<<lookNearestMae>>
->Why are we doing crimes with this guy?
	Gregg: Robbers can’t be choosers.
	<<walk Gregg Gregg2 -1>>
	Mae: They really can.
	SteveScriggins: You know I can hear you, right?
->Ugh. Why did it have to be Steve?
	Gregg: He had an in.
	<<walk Gregg Gregg2 -1>>
	Mae: What in?
	SteveScriggins: I broke the front door.
<<playMusic food_donkey_faces 3>>
<<set $gregg_friendship_quest_1 to 2>>
===
title: LastDayScene
tags: 
colorID: 6
position: -1420,1661
---
<<if $food_donkey_last_scene is 0>>
    <<set $food_donkey_last_scene to 1>>
    <<fadeIn 1>>
    <<warp Mae MaeMallardRats1>>
    <<run Mae MaeMallardRats2 wait>>
    <<lookLeft Mae>>
    Mae: :) \o/ [wave]My babies!!![/wave]
    <<lookUp Mae>>
    Mae: My little rat babies...
    <<lookNearest Mae>>
    Mae: Well, not so little anymore. 
    Mae: You've made it! You've broken free!
    Mae: U_U It's a [wave]miracle![/wave]
    <<lookUp Mae>>
    Mae: _o_ I'm just gonna enjoy this moment for a bit. o_o
    Mae: Don't mind me...
    <<fadeOut 2 wait>>
<<else>>
    <<fadeOut .0001 wait>>
<<endif>>
<<warp Mae MaeSitUpTop>>
<<sit3>>
<<lookNearest Mae>>
<<setSortingLayerName PlayerBG5>>
<<wait .5>>
<<fadeIn 2 wait>>
<<waitForInteractOrJump>>
<<fadeOut 2 wait>>
<<scene Outskirts left>>
===
