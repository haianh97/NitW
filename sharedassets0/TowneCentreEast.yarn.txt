title: Monument.1
tags: 
colorID: 0
position: 27126,-6230
---
Mae:{locator=monument}I think I have a great-great-uncle or something on this.
Mae:{locator=monument}Yep, there you go. Anselm Borowski.
<<wait .25>>
Mae:{locator=monument}"Anselm?"
Mae:{locator=monument}Even in life, he was tragic.
===
title: Monument.2
tags: 
colorID: 0
position: 27366,-6209
---
Mae:{locator=monument}A possum on top, of course. Always, always a possum.
===
title: Monument.3
tags: 
colorID: 0
position: 27652,-6155
---
Mae:{locator=monument}We like our war monuments in Possum Springs.
Mae:{locator=monument}There are 2 really ancient ones over in Olde Possum Springs.
Mae:{locator=monument}Three in town proper.
Mae:{locator=monument}And I think they put a new one up on Pill Hill.
Mae:{locator=monument}There's one up by Possum Leap, but that one's spooky.
Mae:{locator=monument}I haven't been up there since that party we're not going to think about.
<<end>>
===
title: VeryTopAct2
tags: 
colorID: 3
position: 26072,-7221
---
Mae: Years ago I thought when I was 20 I'd never be able to climb this high up anymore.
Mae: Shows what you know, little Mae.
Mae: ...
<<unlockDoodle RoofcatIsCorrect>>
<<end>>
===
title: Windmill
tags: demo2
colorID: 0
position: 27868,-6471
---
Mae: I think these are supposed to be weather stations? #line:d433ba
Mae: Pioneer Scouts made them when they weren't playing football #line:23c9f1
Mae: or learning about [wave]MANHOOD[/wave]. #line:70c096
Mae: These are the manliest of miniature windmills for sure. #line:1107b1
<<set $windmill_done to 1>>
===
title: BlockedEast
tags: 
colorID: 0
position: 27195,-6480
---
<<if $act is 0 or $demo2>>
    [[BlockedEast_Demo2]]
<<else>>
    Mae: My god.
    Mae: What kind of weather did I miss?
    Mae: This is nuts.
    Mae: Oh well, wasn't going to church anyway.
    <<unlockDoodle FallingApart>>
<<endif>>
===
title: InitLevel
tags: 
colorID: 0
position: 28717,-8557
---
<<setCanInteract Donna 0>>

<<if $act is 0 and $day is 0>>
	<<show Donna>>
	<<show Gregg>>
    Gregg: =_=
	<<trigger Gregg Sit ShowGnome>>
	<<warp Gregg GreggRoof Sit>>
    <<setPlayerInteractLocator Gregg GreggRoofTalk>>
    <<setCanInteract Donna 1>>
    <<hide Lori>>
    <<hide TZEmployee1>>
    <<hide TZEmployee2>>
    <<show Bruce>>
<<else>>
    <<hide Gregg>>
    <<hide Bruce>>
<<endif>>

<<if $act is 1 and $day is 2>>
    <<show PoleWorker>>
    <<hide Donna>>
    <<hide TZEmployee1>>
<<else>>
    <<hide PoleWorker>>
<<endif>>

<<if $act is 1 and $day is 3>>
    <<show PoleWorker>>
    <<hide Donna>>
    <<hide TZEmployee2>>
<<else>>
    <<hide PoleWorker>>
<<endif>>

<<if $act is 2 and $day is 1 and $demo2>>
    //<<sit Bea>>
<<endif>>

<<if $act is 2 and $day is 4>>
    <<show Molly>>
<<else>>
    <<hide Molly>>
<<endif>>

<<if $act is 2 and $day is 4 and $towne_centre_east_city_council_a2d4_done is 0>>
    <<show Council1>>
    <<show Council2>>
    <<show Council3>>
    <<show Council4>>
<<else>>
    <<hide Council1>>
    <<hide Council2>>
    <<hide Council3>>
    <<hide Council4>>
<<endif>>

<<set $taxi_stop to 1>>

<<if $demo2>>
    <<show Bea>>
<<else>>
    <<hide Bea>>
<<endif>>

<<if $act is 3 and $day >= 2 and $did_birdland_quest is 0>>
    <<show Saleem>>
<<else>>
    <<hide Saleem>>
<<endif>>

<<if $act is 4 and $day is 3 and $janitor_done is 0>>
    <<show Janitor>>
<<else>>
    <<hide Janitor>>
<<endif>>
===
title: Travis
tags: 
colorID: 3
position: 27227,-6772
---
<<if $act is 2 and $day is 1>>
	Travis: You suck, Mae.
	Mae: ...
	Travis: Yeah, you heard me.
	Mae: ......
	Travis: You suck.
	Mae: You are the WORST.
	<<set $travis to 1>>
<<elseif $act is 2 and $day is 2>>
	<<if $travis < 20>>
		Travis: My car got trashed.
		Mae: Maybe if you weren't such a jerk...
		Travis: Wait what? Where's this coming from?
		<<set $travis to 20>>
	<<elseif $travis is 20>>
		Mae: Maybe it was... carma.
		Travis: Like Karma?
		Mae: N-no... You don't. Oh.
		<<set $travis to 21>>
	<<elseif $travis is 21>>
		Travis: Nice.
	<<endif>>
<<endif>>

===
title: CautionSign
tags: 
colorID: 0
position: 27818,-6778
---
Mae: CAUTION: Do not jump on street lights.
Mae: Do not run on power lines.
Mae: Do not leap between roofs.
Mae: You could die.
Mae: Thank you!
===
title: Gregg_A0D0
tags: demo
colorID: 0
position: 28594,-6813
---
<<camera lookOffset 0 0>>
<<camera lookZoom -1>>
[[GreggRoof]]
===
title: GreggRoof
tags: demo
colorID: 0
position: 28955,-6475
---
Mae: So... um... what happened?
Gregg: Snuck into the elevator
Gregg: but when I got out here the window shut behind me.
Mae: Why were you coming up here to begin with, dude?
Mae: You're not exactly cut out for jumping around roofs.
Gregg: I guess I'm just not feeling it today
Gregg: so I thought I'd throw something off the roof.
<<wait .25>>
Gregg: Oh yeah, I also stole a garden gnome.
Mae: Just having one of your off-days?
Gregg: I guess.
<<close>>
<<wait .5>>
Mae: I was thinking before of what would happen
Mae: if I burned myself alive in my bedroom.
Gregg: ok. :| o_o
Mae: I was thinking that I'd totally haunt here forever.
Mae: Just really bum everyone out.
Gregg: Oh dude I'd bite everyone in the ass.
Gregg: They'd be like what the hell, who just bit my ass???
Gregg: [shake=.04]RAWR ASSBITE!!!!![/shake] D:
Gregg: :|
Mae: I was thinking about being here forever.
Mae: Or being anywhere forever.
Mae: Like a dracula.
Gregg: If I was gonna haunt someplace forever, I don't know if it'd be Possum Springs.
Gregg: This town sucks. This town is a dracula.
Gregg: Tell you what:
Gregg: You go down, ride the elevator back up and let me back in.
Gregg: Then we can borrow Bea's car and go to Donut Wolf.
Mae: I dunno... Bea and I aren't really...
Gregg: No it's cool, me and angus borrow it all the time.
[[GreggRoofGnomeDecision]]

===
title: GreggRoofGnomeDecision
tags: demo
colorID: 0
position: 29314,-6420
---
Mae: Sweet dude. Did you want to throw this gnome?
Gregg: I dunno. What do you think?
[[Naw dude. This gnome's partying with us tonight.|GreggRoofNoToss]]
[[Obviously. This isn't even a question.|GreggRoofToss]]

===
title: GreggRoofToss
tags: demo
colorID: 0
position: 29643,-6469
---
<<close>>
<<musicFadeOut 4>>
<<mood Gregg Neutral>>
<<trigger Gregg ThrowGnome>>
<<wait 3.9>>
<<trigger Gregg HideGnome>>
<<playSound CarSounds GnomeHit>>
<<shake 0.05 0.05 2>>
<<wait .5>>
<<playSound CarSounds Alarm loop>>
<<wait 1.5>>
Gregg: Yep, that did it. I feel better.
Mae: Good dude.
Gregg: Can we get off this roof now?
Mae: I am never gonna let you live this down.
Gregg: Hey asscat, you want Donut Wolf or not? -_-
Mae: {width=4}[shake=.06]AWWOOOOOOOOOOOOOO!!!![/shake] \o/
Gregg: [shake=.04]RIDE THE CHARIOT![/shake] D: \o/
Mae: [shake=.04]To Donut Hell!!!![/shake] \o/ :D
<<fadeOutSounds CarSounds 4>>
<<set $gnome_tossed to 1>>
<<musicFadeOut 2>>
<<fadeOut 2 wait>>
<<sectionTitle LaterAtTheDonutWolf PawsModeDonutWolf01>>

===
title: GreggRoofNoToss
tags: demo
colorID: 0
position: 29464,-6126
---
Gregg: You got lucky, gnome.
Mae: Stop talking. Donut Wolf.
Gregg: Get going already!
Mae: {width=4}[shake=.06]AWWOOOOOOOOOOOOOO!!!![/shake] \o/ :)
<<set $gnome_tossed to 0>>
<<musicFadeOut 2>>
<<fadeOut 2 wait>>
<<sectionTitle LaterAtTheDonutWolf PawsModeDonutWolf01>>
===
title: TZEmployeeAuto
tags: demo2
colorID: 0
position: 28086,-6225
---
<<if $act is 1 and $day is 1>>
    [[TZAutoA1D1]]
<<elseif $act is 2 and $day is 1>>
    [[TZAutoA1D1]]
<<endif>>
===
title: TZEmployeeEmoticonLoop
tags: demo2
colorID: 0
position: 28356,-6289
---
TZEmployee2:{emote=coffee}
Donna:{emote=cigarette}
[[TZEmployeeEmoticonLoop]]
===
title: PoleWorker_00_00
tags: 
colorID: 2
position: 27843,-7167
---
Mae: Whatcha doing up there?
PoleWorker: streetlight got tore off in that big storm a few days back
PoleWorker: don’t help that idiots keep jumping on it
Mae: That’s a shame.
PoleWorker: spread the word to the other kids
Mae: Adults.
PoleWorker: Oh ho ho I don’t think so.
PoleWorker: Adults got jobs and better things to do anyway.
Mae: Ah.
PoleWorker: Anyway youse tell those other kids to lay off.
PoleWorker: Gonna get themselves or somebody else killt!
===
title: PoleWorker
tags: 
colorID: 2
position: 28007,-7476
---
<<if $pole_worker_sequence is 0>>
    <<if $pole_worker is 0>>
        <<set $pole_worker += 1>>
        [[PoleWorker_00_00]]
    <<elseif $pole_worker is 1>>
        <<set $pole_worker += 1>>
        <<set $pole_worker_done to 1>>
        [[PoleWorker_00_01]]
    <<endif>>
<<endif>>
===
title: PoleWorker_00_01
tags: 
colorID: 2
position: 28223,-7187
---
Mae: You know when they're fixing the steps?
Mae: I desperately need to get to church.
PoleWorker: Tomorrow, I think.
Mae: The vampires can't go on sacred ground.
PoleWorker: I ain’t seen no vampires around here.
PoleWorker: 'cept maybe the government
PoleWorker: Suckin' us dry!
Mae: You've taken over my vampire story.
Mae: I’m leaving.
PoleWorker: Watch out for 'em bloodsuckers, kid.
Mae: ADULT!!!!
===
title: SeeGregg
tags: 
colorID: 0
position: 29095,-6966
---
<<camera extraLookOffset 0 2>>
<<alwaysLookAt Mae Gregg>>
<<wait .25>>
Mae: o/
<<lookAt Gregg Mae>>
Mae: Hey Gregg!
Mae: _o_
Gregg: {medianLocator,align=middle}Hey Maedae
Mae: Whatcha doin up here, dude?
Gregg: {medianLocator,align=middle}Got locked out.
Mae: Ok, I'm coming up. Just hang tight.
<<addToJournal GreggStuckOnRoof>>
<<unlockDoodle Gnome 2>>
<<camera extraLookOffset 0 0>>
===
title: Bruce
tags: 
colorID: 0
position: 28794,-7126
---
<<if $bruce is 0>>
    Mae: Um... hey there. You ok?
    Bruce: ...
    Mae: You from around here?
    Bruce: No.
    Mae: You from off the train, then?
    Bruce: Got sick, had to stop. Everyone knows not to stop in Possum Springs anymore.
    Mae: On account of our stringent vagrancy policy?
    Bruce: Some hold their breaths when we roll through town.
    Mae: Uh huh? So what are you doing up here?
    Bruce: Sleeping. Keeping off the streets.
    Mae: Can't be very warm.
    Bruce: It's a lot colder in the west end of town. Best keep high and eastward.
    Mae: Ok then, we'll I'll be going...
    Bruce: At dusk I have to make it to the tracks and hop anything out of here.
    Bruce: But truth is, I'm too afraid to leave this roof.
    Mae: Afraid of what?
    Bruce: "Don't get off in Possum Springs." I knew that.
    Bruce: I got off here. I screwed up.
    Mae: What are you afraid of?
    Bruce: ...
    Mae: Hey listen, dude, I mean if you want to hang out up here I don't care.
    Mae: Just don't like steal anything or eat anyone's pet or whatever.
    Mae: I mean hey, we all have hard times, and
    Bruce: I don't want to talk to you anymore.
    Bruce: Please go away.
    <<set $bruce to 1>>
<<elseif $bruce is 1>>
    Bruce: ...
<<endif>>
===
title: Pee
tags: 
colorID: 0
position: 28510,-7352
---
Mae: I thought he stopped doing the peeing thing.
Mae: Something must be pretty wrong.
===
title: Teen1_Demo
tags: demo
colorID: 0
position: 27507,-7042
---
<<if $towne_centre_east_teen_1 is 0>>
	Mae: 'sup!
	<<set $towne_centre_east_teen_1 += 1>>
    <<set $towne_centre_east_teen_1_walk_away to 1>>
<<elseif $towne_centre_east_teen_1 is 1>>
	Mae: You guys are still in highschool, right?
	Teen3: Yeah probably.
	Mae: Man, me and Gregg totally set that place on fire once! :)
	Teen1: You didn't burn it down, though.
	Mae: :|
    Mae: No, but I mean...
	Teen2: So the story here is that you didn't burn the school down.
	Teen1: That's easy to do.
	Teen3: Look at us. We're also not burning down the school.
	Teen2: But you don't see us bragging about it.
	Mae: I'm not bragging! :(
	Teen1: Oh ok then. What did you want?
	Mae: Nothing! Geez!
    Mae: :|
	<<unlockDoodle Teens 1>>
	<<addToJournal TeensAreTheWorst>>
	<<set $towne_centre_east_teen_1 += 1>>
<<elseif $towne_centre_east_teen_1 is 2>>
	Teen1: {emote=pentagram}
	<<set $towne_centre_east_teen_1 += 1>>
<<elseif $towne_centre_east_teen_1 is 3>>
	Teen1: {emote=crossbones}
	<<set $towne_centre_east_teen_1 += 1>>
<<elseif $towne_centre_east_teen_1 is 4>>
	Teen1: {emote=heartbreak}
	<<set $towne_centre_east_teen_1 to 2>>
<<endif>>
===
title: Teen1
tags: 
colorID: 0
position: 27515,-7345
---
<<if $act is 0 and $day is 0>>
    [[Teen1_Demo]]
<<endif>>
===
title: Teen1WalkAway
tags: 
colorID: 0
position: 27117,-7105
---
Teen1: Yo, you know Gregg, right?
Mae: He's my best friend.
Teen2: [wave]Aw.[/wave]
Teen3: Dude shut up.
Teen1: Yeah I think he's stuck on the roof.
Mae: Wait what?
Teen2: Yeah I think he went up there and got locked out.
Teen3: He looks so sad.
Teen1: I was gonna call my mom to tell her to call the fire department, but...
Mae: Why is he up on the roof?
Teen2: Pssh. I don't know, and I'm not his [color=EC4D67][wave]best friend[/all].
Teen3: You are such a dick.
<<addToJournal TodoGetGreggOffRoof>>
<<unlockDoodle FindGregg>>
<<set $towne_centre_east_teen_1_walk_away to 2>>
===
title: SteveScriggins
tags: demo
colorID: 0
position: 29798,-7336
---
<<if $steve_scriggins is 0>>
    SteveScriggins: {width=2} Huh. Didn't know you were [color=999999]back.[/all]
    [[Geez, does everyone know?{width=1.5}|ScrigginsDoesEveryoneKnow]]
    [[Do I know you?{width=.7}|ScrigginsDoIKnowYou]]
<<elseif $steve_scriggins is 1>>
    [[ScrigginsLoop]]
<<endif>>
===
title: ScrigginsLoop
tags: demo
colorID: 0
position: 30208,-7332
---
SteveScriggins: Got any copper?
===
title: ScrigginsDoesEveryoneKnow
tags: demo
colorID: 0
position: 29706,-7040
---
SteveScriggins: No shame in quittin', quitter. ¯\_(ツ)_/¯
Mae: Oh god, you're Steve Scriggins.
Mae: I didn't know you were still in town.
SteveScriggins: Yeah, strippin' copper.
Mae: Ok
SteveScriggins: We have big bonfire in the field behind my brother's house every weekend
SteveScriggins: to burn off all the building scrap stuff that isn't copper.
Mae: Um
SteveScriggins: Because of all the stuff in it the fire burns like 10 times hotter.
Mae: ...
SteveScriggins: You can't get near it without dying!
SteveScriggins: You should come sometime!
Mae: no.
SteveScriggins: ok.
<<set $steve_scriggins to 1>>
<<addToJournal ScrigginsCopper>>
===
title: ScrigginsDoIKnowYou
tags: demo
colorID: 0
position: 30032,-7039
---
SteveScriggins: Steve Scriggins
Mae: I.. I'm not sure...	
SteveScriggins: You may remember my handiwork, I locked that [color=999999]teacher...[/color]
Mae: ...in the supply closet...
SteveScriggins: IN THE [color=999999]SUPPLY CLOSET[/color]
Mae: ...before Christmas break. He was in there for
SteveScriggins: [wave]TWO WEEKS![/wave]
Mae: He ate spanish workbooks and drank glue to survive. :\
SteveScriggins: [shake=.05]YEAH HE DID![/shake]
Mae: :|
<<addToJournal ScrigginsDumb>>
<<unlockDoodle Scriggins 1>>
<<set $steve_scriggins to 1>>
[[Why did you do that?|ScrigginsWhyDidYouDoThat]]
[[Didn't he die from that?|ScrigginsDidntHeDie]]
===
title: ScrigginsWhyDidYouDoThat
tags: demo
colorID: 0
position: 29874,-6734
---
SteveScriggins: ¯\_(ツ)_/¯
SteveScriggins: I'm a [color=999999][wave]menace![/all]
===
title: ScrigginsDidntHeDie
tags: demo
colorID: 0
position: 30187,-6703
---
SteveScriggins: Nope, he walked into an open sewer!
Mae: Oh my god really?
SteveScriggins: Excellent death!
Mae: Wow, yeah. Excellent death.
SteveScriggins: Inspiring. I hope I go out like that or in a shootout with the cops.
Mae: Those are both very attainable goals.
SteveScriggins: ...[wave]EXCELLENT[/wave] [color=999999]death...[/color]
===
title: BlockedWest
tags: 
colorID: 0
position: 26943,-6475
---
<<if $blocked_west is 0>>
    Mae: I saw a weird possum over there last week. Still filled with such dread.
    Mae: I could go visit Angus at work or hit the Snack Falcon, but I dunno.
    Mae: They're over there.
    Mae: With the possum.
    <<set $blocked_west += 1>>
<<else>>
    Mae: Is there anything more depressing than the vacant ruins of the party barn?
    Mae: I'm too depressed to go any further.
<<endif>>
===
title: TriggerSeenEnough_A2D4
tags: 
colorID: 3
position: 29042,-7354
---
Mae: Aw!!! everything is so great and harfesty!!!
Mae: which is basically just a less spooky halloween?
Mae: whatever!
Mae: i just wanna head home and get ready for tonight!!!!
Mae: i could just do that...
Mae: or i could wander a bit...
Mae: take in the harfesty air...
Mae: well, i'll get home when i do.
<<set $harfest_prep to 1>>
===
title: TriggerCityCouncil_A2D4
tags: 
colorID: 3
position: 29165,-7724
---
<<if $towne_centre_east_city_council_a2d4 is 0>>
    <<set $towne_centre_east_city_council_a2d4 to 1>>
    Council2: That ferris wheel looks like a nightmare.
    Council3: Thanks, Colleen. That's helpful.
    Council1: What's wrong with it?
    Council2: I don't trust giant load-bearing machines you can assemble and disassemble in one day.
    Council1: It's the Hit Record! We've always had the Hit Record at Harfest!
    Council4: Yeah. Since 1984.
    Council3: Maybe that's a clue that we should retire it.
    Council4: SHUT UP ANDREW.
    Council2: Ok everyone let's just take a step back here and cool down.
    Council1: WE. ARE. GOING. TO. HAVE. THE. HIT. RECORD. AT. HAR. FEST. TO. NIGHT!!!
    Council2: Fine then. I'll send all lawsuits your way.
    Council1: BRING IT COLLEEN.
<<endif>>
[[CityCouncilLoop_A2D4]]
===
title: CityCouncilLoop_A2D4
tags: 
colorID: 3
position: 29498,-7740
---
Council1:{emote=spiral}
Council2:{emote=cloud_lightning}
Council3:{emote=gun}
Council4:{emote=cash}
===
title: Window_Angry_A2D1
tags: 
colorID: 3
position: 30609,-6253
---
note: these play without you needing to advance or respond
WindowAngry: HEY! GET DOWN OFFA THERE!!!
WindowAngry: Hey!!!!
===
title: Window_Angry_A2D3
tags: 
colorID: 3
position: 30614,-6007
---
note: these play without you needing to advance or respond
WindowAngry: HEY! GET DOWN FROM THERE!
WindowAngry: YOU HEAR ME???!!
===
title: Window_Angry_A3D1
tags: 
colorID: 4
position: 30618,-5752
---
note: these play without you needing to advance or respond
WindowAngry: HEY! I'M GONNA CALL THE COPS!!!
WindowAngry: YOU LISTENING TO ME?!?!?
===
title: Window_Angry_A3D3
tags: 
colorID: 4
position: 30614,-5513
---
note: these play without you needing to advance or respond
WindowAngry: You're gonna break your neck!!!
WindowAngry: And I won't lift a finger to help!!!
WindowAngry: Hey!!!!
===
title: StairLanding
tags: 
colorID: 3
position: 32249,-7362
---
Mae: Hm. Looks like that streetlight could hold me...
===
title: Monument_Act1.1
tags: 
colorID: 2
position: 27076,-5851
---
Mae:{locator=monument}I think I have a great-great-uncle or something on this.
Mae:{locator=monument}Yep, there you go. Anselm Borowski.
<<wait .25>>
Mae:{locator=monument}"Anselm?"
Mae:{locator=monument}Even in life, he was tragic.
===
title: Monument_Act1.2
tags: 
colorID: 2
position: 27374,-5850
---
Mae:{locator=monument}We like our war monuments in Possum Springs.
Mae:{locator=monument}There are 2 really ancient ones over in Olde Possum Springs.
Mae:{locator=monument}Three in town proper.
Mae:{locator=monument}And I think they put a new one up on Pill Hill.
Mae:{locator=monument}There's one up by Possum Leap, but that one's spooky.
Mae:{locator=monument}I haven't been up there since that party we're not going to think about.
<<end>>
===
title: Monument_Act2.2
tags: demo2
colorID: 3
position: 27375,-5580
---
Mae:{locator=monument}I wonder if Gregg still has that helmet from World War 1... #line:89cfa9
Mae:{locator=monument}With the bullet hole in it... #line:bfc7b8
<<end>>
===
title: Monument_Act2.1
tags: demo2
colorID: 3
position: 27067,-5579
---
Mae:{locator=monument}I've never been to war. #line:561e36
Mae:{locator=monument}Not like the recruiter didn't try, though. #line:a9820b
Mae:{locator=monument}Hard. #line:97b518

===
title: Monument_Act3.1
tags: 
colorID: 4
position: 27068,-5298
---
Mae:{locator=monument}War is some bullshit.
Mae:{locator=monument}You hear me, statue?
Mae:{locator=monument}I guess you were drafted.
Mae:{locator=monument}ugh.
Mae:{locator=monument}you died before you got to enjoy the statue they made of you.
Mae:{locator=monument}it's garbage.
===
title: Monument_Act3.2
tags: 
colorID: 4
position: 27376,-5286
---
Mae:{locator=monument}they can't make me go to war.
Mae:{locator=monument}i'll go underground.
Mae:{locator=monument}i'll join some sort of group that's against war and stuff.
Mae:{locator=monument}we'll blow up war stuff and sabotage other... war stuff.
Mae:{locator=monument}i'll live in the shadows for decades.
Mae:{locator=monument}no address.
Mae:{locator=monument}always on the run.
Mae:{locator=monument}living in a trailer in the desert.
Mae:{locator=monument}it'll be awesome.
<<end>>
===
title: ReminderEastStairs
tags: 
colorID: 3
position: 30551,-7965
---
Mae: Wow! Construction's finally done!
Mae: Stairs is open for business!
Mae: wonder what's going on up there?
<<set $reminder_east_stairs_done to 1>>
===
title: TZAutoA1D1
tags: demo2
colorID: 0
position: 28182,-5916
---
<<if $tz_employees is 0>>
    <<set $tz_employees to 1>>
    Donna: What? #line:eb1a51
    TZEmployee2: And where exactly are you planning on going? #line:f5d94a
    Donna: I dunno. Ham Panther? #line:79c5b7
    TZEmployee2: But honey you're only gonna get minimum there. #line:1e6139
    TZEmployee2: And I hear they're murder.. #line:c31a79
    Donna: *THIS* place is murder. #line:4e6c26
    Donna: I don't think I can handle one more incoming call. #line:06228a
    Donna: I wake up sometimes because I hear that *BING* noise. #line:b88667
    TZEmployee2: Nowadays I just take a sleeping aid. Works wonders. #line:d82dd4
    Donna: We shouldn't have to drug ourselves to get our sleep back from this job! #line:9dab4c
    TZEmployee2: Well, Ham Panther isn't gonna give you a bonus. #line:5f1001
    TZEmployee2: Ham Panther doesn't even have a leaderboard. #line:337022
    Donna: But it isn't calls. #line:aa9f78
    Donna: It isn't sales. #line:409727
    Donna: I wouldn't be  waiting for that *BING*. #line:558160
    TZEmployee2: I don't know, honey... #line:73b8de
    TZEmployee2: Maybe you'll have a good shift today and get on the leaderboard #line:97aa38
    TZEmployee2: And we'll see how you feel then? #line:772dc2
    Donna: I guess. But it won’t change anything. #line:7c1979
<<else>>
    [[TZEmployeeEmoticonLoop]]
<<endif>>
===
title: Bea_A2D1
tags: demo2
colorID: 3
position: 28966,-7979
---
<<if $bea is 0>>
    <<set $bea += 1>>
    [[Bea_A2D1_00]]
<<elseif $bea is 1>>
    <<set $bea += 1>>
    <<set $bea_done to 1>>
    [[Bea_A2D1_01]]
<<endif>>
    
===
title: Bea_A2D1_00
tags: demo2
colorID: 3
position: 29218,-8061
---
Mae: Hey Bea. #line:4d49e8
Bea: Yyyyo. #line:a932fc
Mae: Sorry about last night. #line:7f76f5
Mae: I get woozy on cold medicine. #line:775889
Mae: So I don't really know what I expected to happen. #line:ae4daa
Bea: Sorry for what I said. #line:f21747
Mae: You know, I don't really remember it much. #line:3f81dc
Mae: So whatevs. #line:87517a
Bea: Hooray for alcohol. #line:79f993
Mae: New town motto. #line:794735

===
title: Bea_A2D1_01
tags: demo2
colorID: 3
position: 29491,-8052
---
Mae: Band practice later? #line:c5f5bc
Bea: Ha ha. Yep. #line:6763a1
Mae: I'm amazed it's still a thing. #line:db253f
Mae: We were barely even a band in highschool. #line:f09f3b
Bea: Eh. It's fun. Get to hang out with Angus. #line:5f492f
Mae: How did you even end up doing it? #line:7e3b21
Bea: When Casey left town, Angus told me that Gregg was really sad. #line:b4bc92
Bea: So I volunteered to be the new drummer. #line:b2ab24
Mae: Well, "drummer." #line:961a43
Mae: It's a laptop, Bea. #line:6f5e46
Bea: It sure is, Mae. It sure is. #line:b64069
===
title: NextToCat.1
tags: demo2
colorID: 0
position: 28040,-8201
---
Mae: Hey cat. #line:60b9f0
Mae: How’d you get up here? #line:a3b5e9
Mae: You put in all of this effort just to sleep? #line:380fbd
Mae: I have rarely related more to an animal. #line:ae3645
===
title: NextToCat.2
tags: demo2
colorID: 0
position: 28028,-7990
---
Mae: Here, at the end of the world: #line:7523b8
Mae: a sleeping cat. #line:f059cf
===
title: NextToCat.3
tags: demo2
colorID: 0
position: 28004,-7779
---
Mae: Hey cat. #line:c6211d
Mae: When I was little I never thought I’d be able to get up here. #line:d07089
Mae: And you’re like half the size of little me. #line:a54c4e
Mae: You win. #line:070d48
<<set $next_to_cat_done to 1>>
<<unlockDoodle RoofcatIsCorrect>>
===
title: TopOfPartyBarn.1
tags: demo2
colorID: 0
position: 28301,-8179
---
Mae: Takes a lot of parties to keep a barn open. #line:f22571
Mae: I don’t know how it even survived as long as it did. #line:4899f5
===
title: TopOfPartyBarn.2
tags: demo2
colorID: 0
position: 28299,-7952
---
Mae: I don’t think Gregg got permission to have band practice here. #line:da04bc
Mae: We are so gonna get evicted. #line:5c3822
Mae: Then the barn shall once again be silent. #line:ee821c
<<set $top_of_party_barn_done to 1>>
===
title: BlockedEast_Demo2
tags: demo2
colorID: 0
position: 27458,-6445
---
Mae: I am not climbing these stairs today. Don't need to go to church. #line:6ba35b
Mae: No gods, no masters, no effing stairs today please. #line:7e80c0
===
title: Saleem
tags: 
colorID: 4
position: 31351,-7747
---
<<if $saleem_sequence is 0>>
	Mae: Hey!
	Saleem: Hello.
    Mae: I'm Mae!
    Saleem: Hello Mae, I'm Saleem.
	Mae: Are you a busker? Are you busking?
	Saleem: What’s busking?
	Mae: I don’t know.
	Mae: I think it’s when you play outside and people give you money.
	Saleem: Are you gonna give me money?
	Mae: No.
	Saleem: then I’m not busking.
	Mae: Cool.
	<<set $saleem_sequence += 1>>
<<elseif $saleem_sequence is 1>>
	Mae: Still up here?
	Saleem: I mean, I left and went to work and so forth since we met.
	Mae: Hm
	Mae: is that a fiddle?
    Saleem: it's a violin.
    Mae: that's like the same thing, right?
    Saleem: it's a violin, i assure you.
	<<set $saleem_sequence += 1>>
<<elseif $saleem_sequence is 2>>
	Mae: Has anyone complained?
	Saleem: About what?
	Mae: You playing.
	Saleem: I don’t think anyone cares, honestly.
    Saleem: or they like it.
    Saleem: i'm fine either way.
	<<set $saleem_sequence += 1>>
<<endif>>

<<if $saleem_sadie_sequence is 0 and $sadie_sequence is 1>>
	Mae: I just met someone else playing by themselves
	Saleem: Really?
	Mae: Yeah. Across town in the abandoned building by the Clik Clak.
    Mae: they had one of those big saxophones.
	Saleem: Hm. 
    Mae: I just thought it was an interesting coincidence.
    Saleem: Small town.
	<<set $saleem_sadie_sequence += 1>>
<<elseif $saleem_sadie_sequence is 1 and $sadie_sequence is 2>>
	Mae: You know that woman I mentioned across town?
	Mae: I think she’s looking for people to play with.
	Saleem: really?
	Mae: Yeah. Just saying.
	Mae: I told her I’d mention it.
	Saleem: I’ll keep it in mind.
    Mae: I'm not saying you *HAVE* to go play with her or anything-
    Saleem: I get it.
    Mae: I don't want to make this weird,
    Saleem: It'll be a lot less weird when you stop making it weird!
	<<set $will_complete_birdland_quest to 1>>
	<<set $saleem_sadie_sequence += 1>>
<<endif>>

<<set $saleem_done to 1>>
===
title: AngryWindowDude
tags: 
colorID: 0
position: 27524,-7911
---
<<set $angry_window_dude to 1>>
<<animationTrigger WindowAngry GetAngry>>
<<wait .5>>
WindowAngry: HEY! GET DOWN FROM THERE!
===
title: Janitor_A4D3
tags: 
colorID: 6
position: 32770,-7571
---
Mae: Hey. It’s you.
<<wait 2>>
Janitor: yep yep.
Janitor: For now at least.
Mae: You know, I never got your name.
Janitor: I’m the Janitor.
Janitor: I clean up, do fixits.
Janitor: Whatever needs done.
Mae: Oh. Ok.
Janitor: So what’s it been like being home again?
Janitor: Things like you remember ‘em?
    ->Kinda.
    ->No.
Mae: I don’t know.
Mae: I don't even remember today.
Janitor: Sounds like you been through something.
Mae: How did you know?
Janitor: Call it experience.
Janitor: I swear some mornings I feel every bit as old as these trees.
Mae: You know, something big did happen
Mae: and I feel like I should have woken up today
Mae: and like, have learned something.
Mae: But I don’t know if I learned anything, really.
Janitor: Weeeell,
Janitor: in my experience the big things don’t teach you anything
Janitor: but they make you something
Janitor: and sometimes you gotta wait a while and see what come of it.
Mae: huh.
<<wait 2>>
Janitor: Welp, done an honest day’s work.
Janitor: quittin’ time!
Janitor: Smelters game tonight!
Mae: See ya around.
Janitor: Not if I see you first, Mae.
Mae: ha ha
Janitor: *wheeze* haw haw haw!
//janitor walks offscreen to the left//
<<close>>
<<walk Janitor JanitorOffLeft wait>>
Mae: wait! How do you know my name?
<<wait 2>>
//giant astral-god-looking bird flys up and into the sky in the bg//
//Mae: oh my god
//Mae: he was-
//Mae: finally some part of this makes sense.
//<<close>>
//<<walk Janitor JanitorBackOn wait>>
//janitor walks back onscreen interrupting//
//Janitor: HO-LEE JA-MOLEY
//Janitor: you see that big bird?
//Janitor: That’s a Great Nor’Easter King Crane!
//Janitor: Must be late to the migratin’.
//Janitor: Haw! Wait til the fellers at Miller’s hear about this!
//Mae: Ha ha ha ha.
//Mae: oh my god. This week.
//<<walk Janitor JanitorOffLeft wait>>
//Janitor: You said it.
<<hide Janitor>>
//janitor walks offscreen to the left//
<<set $janitor_done to 1>>
<<unlockDoodle Janitor1>>
===
title: FrontOfPartyBarnA2D2.1
tags: 
colorID: 0
position: 30669,-7105
---
Mae: it looks so sad and empty when we’re not playing in it.
<<end>>
===
title: FrontOfPartyBarnA3D2.1
tags: 
colorID: 0
position: 30664,-6860
---
Mae: i can’t believe they just leave the band stuff in there
Mae: what if someone steals it?
Mae: what if the building falls down?
Mae: i should start taking gregg’s bass home.
Mae: just ya know… in case.
<<end>>
===
title: VeryTopAct3
tags: 
colorID: 4
position: 26291,-7214
---
Mae: Hm. used to sometimes see other people up here.
Mae: is this not a thing anymore?
Mae: am i just old and out of touch?
Mae: whatever. it's awesome.
Mae: i'm gonna put up a tent and camp out up here.
<<wait 2>>
Mae: ...
Mae: would definitely roll off and die.
<<unlockDoodle RoofcatIsCorrect>>
<<end>>
===
title: VeryTopAct4
tags: 
colorID: 6
position: 26508,-7203
---
Mae: Won't be able to climb this soon.
Mae: once it gets icy.
Mae: enjoy it while the weather lasts, mae.
<<unlockDoodle RoofcatIsCorrect>>
<<end>>
===
title: Molly_A2D4
tags: 
colorID: 3
position: 28735,-8244
---
Mae: hey aunt mall-cop.
Molly: mall cops don't carry guns, mae.
Mae: geez!
Mae: i guess you need that gun for... what are you doing?
Molly: blocking off the road.
Mae: oh yeah. wouldn't want to do that without a gun.
Molly: you know how many times i've shot someone?
Mae: no. tell me now.
Molly: once. 
Mae: you kill 'em?
Molly: no.
Mae: so you're saying you don't really need it, then?
Mae: you should carry a battle axe.
Mae: *THAT* would be impressive.
Molly: i'll look into it.
<<set $molly_done to 1>>
===
