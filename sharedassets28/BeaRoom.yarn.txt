title: Fight
tags: 
colorID: 3
position: 607,237
---
<<warp Bea BeaLayRig>>
<<setAltBodyRig Bea BeaLayRig>>
<<warp Mae MaeEnter>>
<<set $bea_friendship_quest_3 to 1>>
Mae: Did you guys just move in?
Bea: 10 months ago.
Mae: Why aren’t you unpacked?
<<close>>
<<wait 2>>
-> Pretty cool record player.
    Mae: as far as record players go.
    Bea: It was my mom’s.
    Mae: I don’t have any records.
    Mae: I don’t get records.
    Bea: My mom was into them.
    Bea: Ha ha. They were retro when she was a my age.
    Mae: They’re retro now.
    Bea: That’s how retro works.
-> Your old room was like...
    Bea: Bigger?
    Mae: Messy. Really messy.
    Bea: Eh. I was a slob when I was a kid.
    Mae: Guess the boxes take care of that problem.
    Bea: No, mom yelled it out of me.
    Mae: MOMS, amirite?
    Bea: U are rite.
<<close>>
<<wait 2>>
Mae: So I’ve got a question.
Bea: Mmhmm.
-> What exactly does your dad *DO* at the store?
    Bea: These days? Not much.
    Bea: Uh. Some administrative stuff. 
    Bea: He hires the repair guys...
    Bea: He sometimes goes out on jobs.
    Bea: That’s kind of his whole area.
    Bea: But that's about it.
-> Your dad’s kinda up and down, huh?
    Bea: He has good days and bad days.
    Bea: Often in the same day.
    Mae: ha ha. Gregg's always been like that too.
    Mae: just more... uh... zany about it.
    Bea: well Gregg is probably bipolar
    Mae: gee thanks, doctor bea.
    Bea: whereas my dad is having a years-long breakdown.
    Mae: You run the whole store now, right?
    Bea: More or less.
Mae: Yeah but it’s still his name on everything.
Bea: Yep.
Mae: And meanwhile you’re running basically everything.
Bea: Yep.
Bea: And! And! working there every day too.
Bea: Yep.
Mae: I just think you shouldn’t.
Bea: Ok.
    ->I just think you're like selling yourself out.
        Bea: I’m sorry, what?
    ->I would probably just quit.
        Mae: or like, take over the store.
Mae: Like, you not doing anything about it just reinforces-
Bea: Is this really the road you want to go down with this?
    ->I’m saying you should stand up for yourself or just like... quit!
        Bea: "Quit"?
        Bea: Really? Just quit.
        Bea: You are seriously detached from reality.
    ->I’m saying it’s not right that you just roll over and take it.
        Bea: "Take it?"
        //shake text here//
        Bea: "TAKE IT"?!?!?!
Mae: You’re doing all the work and he’s getting all the credit!
Bea: oh go to hell.
    ->WHAT?!
        Bea: you heard me.
    ->WHY?!
        Bea: RAAAAAAGH.
Mae: what is your problem?
Bea: here's some reality for you:
Bea: You know Creek? Repair Guy Creek at the shop?
    -> No?
        Bea: You’ve seen him.
        Bea: Talks a lot. Worked for us since forever.
    -> ...yeah?
        Bea: yeah you met him when we were kids.
Bea: When I was 15 he asked my dad if he could teach me how to drive.
Bea: And my dad said no, and then told me not to be alone with him.
    -> What the?
        Bea: Yeah. Exactly.
    -> I don't follow.
        Bea: he's a creep, Mae. 
Bea: And you know what? HE still works for us.
Mae: !!!
Mae: Why haven’t you fired him?!?!
<<fadeOutAmbientSFX _AmbientSFX:music 10>>
<<playAmbientSFX _AmbientSFX:record_static 10>>
Bea: I can't fire people!
Bea: And even if I could, he’s on the crew.
Bea: And frankly he’s our best guy. Practically heads up the repair team.
Bea: Since my dad’s barely doing anything!
Bea: And Creek’s got a family. And he needs the work.
-> That’s disgusting! 
    Mae: It’s not your responsibility to-
    Bea: I KNOW!
    Bea: DON’T YOU THINK I KNOW THAT?!
-> I’m gonna be sick.
    Bea: YEAH! ME TOO!
    Bea: I’M REALLY FAMILIAR WITH THAT FEELING.
Mae: I’m just saying if you keep letting these dudes off the hook-
Bea: OFF THE HOOK?!
Mae: Like, you're stronger than this, Bea.
Mae: You're like... badass and shit.
Bea: Y-YOU...
<<close>>
<<animationTrigger BeaLayRig GetUp>>
<<wait 2>>
Bea: "BADASS?"
Bea: MY MOM DYING WAS NOT VERY "BADASS."
Bea: ME SITTING IN THIS ROOM RIGHT NOW IS NOT VERY "BADASS."
Mae: ok sorry, i-
Bea: You are such a shithead, you know that?
    -> Aaaaargh!
        Bea: "Aaaaargh?" "Aaaaargh" what?!
    -> What the hell?
       Bea: "uuughhuhuhg what the hell?!"
Bea: A lot of time folks can’t just "choose" to do
Bea: whatever it is you decree to be the right thing!
Bea: A lot of times people do the things they do
Bea: because they can’t do anything else!
    -> You ALWAYS have a choice.
        Bea: says the child-woman with no job
        Bea: no rent
        Bea: no responsibilities
        Bea: and no consequences for your stupid actions
    -> You can ALWAYS choose.
        Bea: choose what?
        Bea: honestly. tell me.
        Bea: what is my choice here?
            ->you can just leave! i dunno!
                Bea: I... I...
                Bea: y-you
            ->tell your dad you're not gonna take this!
                Bea: tell him what????
                Bea: what is your magic solution
                Bea: that *I* need to do
Bea: I am seriously going to punch your goddamn lights out.
Bea: You come in here and start telling me how *I’M* screwing things up?
Bea: You want to come in here and say
Bea: “oh yeah just ditch your dad,
Bea: who is both getting old
Bea: AND can barely get off the couch half the time?”
<<if $grocery_quality is 1>>
    Bea: [wave]Oh, but "good dinner", Mae![/wave]
<<endif>>
Bea: You want to come in here and say
Bea: "oh yeah just start this big drama
Bea: and make your life WAY more stressful?"
-> It’s just not right is what I’m saying!
    Bea: Yeah. It isn’t. Nothing’s right.
    Bea: The whole effing world isn’t right.
-> That is NOT what I’m saying!
    Bea: Oh, what are you saying, then?
    Mae: AAAARGH I don’t know anymore!
Bea: like gregg is probably off doing god knows what wacky shit right now
Bea: i'm here making dinner and doing payroll.
Bea: You know like *nothing* about my life.
Bea: You just know what I “should” do.
Bea: And you’re the last person I’d ask for advice.
Mae: Ok ok I’m sorry.
Bea: Yeah, you’re sorry. Everyone's sorry.
Bea: But you don’t get it.
<<if $did_bea_friendship_quest_1 is 1 and $did_bea_friendship_quest_2 is 1>>
    Mae: I thought we were having fun!
    Mae: We went to the mall! We got stuck in a basement!
    Bea: Yeah and that was ALL you did those days.
    Bea: I’d worked a good 6 hours before you even woke up probably
    Bea: and came home each night after you and I were done
    Bea: to pay bills and figure out how we’re making next payroll
    Bea: While dad slept on the couch.
    Bea: This isn’t fun.
<<elseif $did_bea_friendship_quest_1 is 1>>
    Bea: You’re just like the other night, but all the time.
    Bea: Running around messing with people and taking whatever you want.
    Bea: While the whole mall is falling apart around you.
    Mae: I thought you had a good time!
    Bea: ...
<<elseif $did_bea_friendship_quest_2 is 1>>
    Bea: It’s like last night
    Bea: you get to play around in garbage and break shit and everything works out.
    Bea: While I’m stuck in the corner fixing the furnace.
    Mae: But I helped!
    Bea: ...
<<endif>>
<<close>>
<<wait 2>>
Mae: I should go.
Bea: You should go.
<<set $bea_dad_done to 0>>
<<set $bea_friendship_quest_3 to 1>>
<<irisOut 1 wait>>
<<unlockDoodle BeaFQ3>>
<<unlockDoodle DadBeaFQ3>>
<<scene BeaApartment>>
===
title: EnterLevel
tags: 
colorID: 0
position: 355,219
---
[[Fight]]
===
