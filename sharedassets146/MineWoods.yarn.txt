title: InitLevel_A3D4
tags: 
colorID: 4
position: 255,-1236
---
<<setCanInteract Bea 0>>
<<setCanInteract Gregg 0>>
<<setCanInteract Angus 0>>

<<hide Eide>>

<<flip Mae -1>>
<<groupFollow>>

<<playerCanRun 0>>

<<playMusic mine_woods_trek 10>>
===
title: Trigger1
tags: 
colorID: 4
position: 264,-747
---
Mae: How have I never been back up here?
<<setGenerateFollowPoints Bea 0>>
<<moveLocator BeaReturn Bea>>
<<walk Bea Bea1>>
Bea: Fences work?
Gregg: Nah. People are up here all the time.
<<close>>
<<setGenerateFollowPoints Gregg 0>>
<<moveLocator GreggReturn Gregg>>
<<walk Gregg Gregg1>>
Gregg: I came here when I was like 12.
Mae: what's up here?
<<lookAt Mae Bea>>
<<close>>
Gregg: just old junk and trees and animals.
<<wait 2>>
<<walk Gregg GreggReturn -1>>
<<wait 4>>
Angus: what is it, Bea?
<<wait 2>>
Bea: nothing.
<<waitForMove Bea>>
<<waitForMove Gregg>>
Mae: Nothing?
<<close>>
<<walk Bea BeaReturn -1>>
<<waitForMove Bea>>
<<waitForMove Gregg>>
Bea: I was praying.
Mae: woooooooooooow. o> :) O_o
Angus: really?
Bea: yeah. let's go.
<<lookNearest Mae>>
Mae: _o_ :| o_o 
<<setGenerateFollowPoints Bea 1>>
<<setGenerateFollowPoints Gregg 1>>

===
title: Trigger2
tags: 
colorID: 4
position: 277,-496
---
Mae: Wow.
<<lookLeft Mae>>
<<wait 2>>
Bea: Gotta put all this stuff somewhere I guess.
Mae: You remember this from when you were 12, Gregg?
Gregg: Yeah. I was scared of it.
Angus: How'd they even get all of this back up in here?
<<wait 2>>
Mae: *shudder*
Bea: what's up?
Mae: nothing.
Mae: just like
Mae: all this old, old stuff out here
Mae: it's here every night
Mae: when you go to sleep.
Bea: so?
Mae: ~~
Mae: it's out here. in the dark.
Mae: -_- :(
Mae: so old
<<setAnimint Mae headShaking 1>> 
Mae: so old so old so old
<<wait 2>>
Gregg: :(
Gregg: dude! hey mae! 
<<wait 2>>
<<setAnimint Mae headShaking 0>>
Mae: _o_ :| =_=
Mae: I'm fine.
Gregg: :|
Bea: we should go back. this is stupid.
<<wait 2>>
Mae: no. it's fine.
Mae: ghost hunt. woo. fun.
Mae: o_o
<<lookNearest Mae>>
<<fadeOutMusic 10>>
===
title: MinerScene
tags: 
colorID: 4
position: 284,-199
---
Mae: what the-
Gregg: huh?
Bea: oh my god...
<<setAnimInt Miner3 state 1>>
<<warp Miner3 MinerGround1>>
<<close>>
<<cameraLookAt MinerSceneCamera 3>>
<<cameraZoom -10>>
<<wait 3>>
Miner3: NO! NO!
Miner2: ah hell he’s awake already
Miner2: anyone got any juice?
Miner3: no. no. guys,
Miner2: aw, HELL.
Miner3: guys… guys… wait...
Miner1: Lurv, you knew this was gonna happen.
Miner1: you knew it the moment you screwed this whole thing up
Miner1: it ain’t like you weren’t on the other side of this before
Miner3: it was a *MISTAKE*!
Miner3: i owned up to it!
Miner1: Lurv, this *is* you ownin’ up to it
Miner3: aren’t i allowed to make one mistake?
Miner1: ONE MISTAKE????
Miner1: YOU LEFT A GODDAMN [shake=.05]*ARM*[/shake] IN FRONTA THE CLICK CLAK!
Miner1: cuz of you, god knows what’s gonna happen to this town!
Miner3: what was I supposed to do???
Miner1: IT WAS YOUR SCREW UP!
Miner2: ugh. his leg’s stuck.
Miner1: AND IT WASN'T LIKE YOU COULDN'T A' CALLED ANY OF US!
Miner3: i panicked, ok?! i was embarrased!
Miner1: i sure wish you'd called me, Lurv.
Miner1: you think any of us want to do this?
Miner3: guys. We can figure something out.
Miner1: nothing to figure out.
Miner3: but... b-but I got family!
Miner1: Lurv, don't insult us.
Miner1: you know you don’t gotta worry bout Jeanie and the girls.
Miner1: they will be completely taken care of.
Miner1: They’ll never want for nothin’.
Miner2: he’s not gonna budge with that leg stickin’ straight out
Miner1: aw, HELL.
Miner3: D-dave! Rick! Bennie! Guys, you know me!
Miner1: alright Dave, you hold 'im.
Miner1: i’m sorry, Lurv.
Miner1: you know we got no choice
Miner1: and i’d appreciate it if you stopped laying a guilt trip on us.
//camera pulls up to obscure action below//

<<playOneShot event:/minewoods/miner_struggle Miner3>>
<<cameraLookAt LookAwayCamera 3>>
<<wait 1>>
<<setAnimInt Miner1 stoop 1>>
<<setAnimInt Miner2 stoop 1>>
<<wait 1>>
Miner3:{locator=Miner3LookawayBubble}[shake=.05]no no no no[/shake]
Miner1:{locator=Miner1LookawayBubble}HOLD 'IM STILL
Miner3:{locator=Miner3LookawayBubble}[shake=.07]NOOOO![/shake]
//horrible bone cracking noise//
<<setAnimInt Miner1 stoop 0>>
<<setAnimInt Miner2 stoop 0>>
<<wait 1>>
<<playOneShot event:/minewoods/miner_struggle Miner3>>
<<wait 1>>
Miner3:{locator=Miner3LookawayBubble, width=800}[shake=.1]AAAAAAAAAAAAAAAAAAAAAAAAAAAGGGHHH!!!![/shake]
Miner1:{locator=Miner1LookawayBubble}HOLD STILL! THIS’LL GO EASIER IF-
//horrible bone cracking noise//
<<wait 1>>
<<warp Miner3 MinerGround2>>
<<playOneShot event:/minewoods/miner_struggle Miner3>>
<<wait 1>>
Miner3:{locator=Miner3LookawayBubble, width=800}[shake=.1]AAAAAAAAAAAAAAAAAAAAAGGGHHH!!![/shake]
Miner2:{locator=Miner2LookawayBubble}alright. he’s free.
<<setAnimInt Miner3 state 2>>
// camera goes back //
<<playOneShot event:/minewoods/miner_struggle Miner3>>
<<cameraLookAt MinerSceneCamera 2>>
<<wait 2>>
Miner1: Lurv.
Miner3:{width=800}[shake=.1]AAAAAAAAAAAAGH!!![/shake]
Miner1: LURV.
Miner1: ain’t noone can hear you this far out.
Miner1: you’re just puttin’ yourself to more anguish, an-
Miner3: [shake=.12]AAAGH!!! AAAAARGGHH!!! AAAH![/shake]
Miner1: I… aw HELL, let’s get this over with.
Miner1: wish you’d just make this easy, Lurv.
Miner2: ugh god his bone's stickin' out.
Mae:{locator=MaeOffscreenBubble}*gasp*
<<playOneShot event:/minewoods/mae_discovered Mae>>
Miner2: the hell?
Mae:{locator=MaeOffscreenBubble}[shake=.05]SHIT.[/shake]
Miner1: Mae? Mae Borowski?
Angus:{locator=MaeOffscreenBubble}[shake=.05]RUN!!!!!![/shake]
//wipe to run scene//
<<cameraFollowPlayer 3>>
<<fadeOut 1 wait>>
<<scene MineWoodsRunAway>>
===
title: InitLevel_A4D2
tags: 
colorID: 5
position: 523,-1015
---
<<set $death_walk to 1>>
<<playIdleAnimationPlayer>>
<<hide Miner1>>
<<hide Miner2>>
<<hide Miner3>>
<<hide Gregg>>
<<hide Bea>>
<<hide Angus>>
<<show Eide>>
<<flip Mae -1>>
<<warp Mae MaeStart>>

<<setCanInteract Bea 0>>
<<setCanInteract Gregg 0>>
<<setCanInteract Angus 0>>
===
title: FallDown
tags: 
colorID: 5
position: 533,-752
---
<<close>>
<<cameraLookAt MinerSceneCamera 3>>
<<cameraZoom -10>>
<<wait 3>>
// characters fall down from off screen
Mae: Falling down from offscreen.
<<close>>
<<warp Mae MaeGround>>
Mae: Phew, made it.
<<close>>
<<irisOut 1 wait>>
<<scene MineEntrance left>>
===
title: Eide
tags: 
colorID: 5
position: 537,-491
---
Mae: What are you?
<<close>>
<<wait 2>>
Mae: Are you actually a ghost?
Mae: Or just some
Mae: guy?
<<close>>
<<wait 2>>
Mae: What is this thing in my head?
Mae: Are you doing this?
Mae: Why is this happening to me?
<<close>>
<<wait 2>>
Mae: Talk!!!!
<<close>>
<<wait 2>>
Mae: Fine. Let’s do this.
<<playerCanMoveRight 0>>
===
title: EideApproach
tags: 
colorID: 5
position: 542,-222
---
<<tell GreggSideBoltShootEide Fire>>
<<wait 2>>

<<show Bea>>
<<show Gregg>>
<<show Angus>>

<<setAnimInt Gregg crossbow 1>>

<<if $dom_friend_bea>>
    <<warp Bea Char1EideApproach>>
    <<warp Gregg Char2EideApproach>>
<<else>>
    <<warp Gregg Char1EideApproach>>
    <<warp Bea Char2EideApproach>>
<<endif>>
<<warp Angus Char3EideApproach>>

<<if $dom_friend_bea>>
    <<walk Bea Char1NearEide>>
    <<walk Gregg Char2NearEide>>
<<else>>
    <<walk Gregg Char1NearEide>>
    <<walk Bea Char2NearEide>>
<<endif>>
<<walk Angus Char3NearEide>>

<<waitForMove Bea>>
<<waitForMove Gregg>>
//gregg is using Gregg_walk_crossbow, when he stops play Gregg_bod_Crossbow_Down2Up and hold on Gregg_bod_Crossbow_up//
<<setAnimInt Gregg crossbow 2>>

<<if $dom_friend_bea>>
    Gregg: Boom.
    Bea: Get away from her you piece of shit.
    Angus: You're outnumbered asshole.
    Eide: AAARGH. You shot me!
    Bea: And we can all use both of our arms.
    Bea: I will personally disassemble you.
    Gregg: whoa geez bea
<<else>>
    Bea: Nice shot.
    Gregg: I wasn’t aiming for his shoulder!
    Eide: AAARGH. You effing shot me!
    Gregg: Hey asshole! Get away from her!
    Angus: Three of us, one of you.
    Gregg: And none of us got bolts sticking out of us.
    Gregg: I am gonna *@$%ing  %*$@**$%*@*$%@ you!!!
    Bea: yeesh
<<endif>>
Eide: AAAAAAAARGH.
<<close>>

<<playOneShot event:/minewoods/eide_scamper_away Eide>>
<<run Eide EideRunAway>>
Eide: {width=5}[wave][shrink]AAAAARROoOoOoOOoooo[/all]
<<close>>
<<waitForMove Eide>>
<<hide Eide>>
<<wait 2>>
//play Gregg_bod_Crossbow_Up2Down and hold on Gregg_bod_crossbow_down//
<<setAnimInt Gregg crossbow 1>>

<<if $dom_friend_bea>>
	Bea: You ok?
		->where did...
		->what are...
	//bea walks up to mae//
	Bea: You are such an idiot.
	Bea: What were you thinking, coming out here alone?
		->i- i had a dream...
			Mae: you were all dead
			Mae: out here in the woods
			Mae: and it was because of me
			Bea: oh, you had a dream?
			Bea: is that how we’re making decisions now?
				->after all that’s happened... yeah?
				->i don’t know anymore
		->this is all because of me...
			Bea: oh, I’m sorry, are you the one putting on a dumb costume
			Bea: and following me around
			Bea: and kidnapping people maybe?
			Bea: is that your fault now?
			Mae: He... it... it’s in my dreams
	Bea: hey
	Bea: kiddo
	Bea: we’re out here
	Bea: outside your head
	Bea: whatever’s going on in there, i believe you
	Bea: but i’m not gonna let you wander off into the woods
	Bea: to die.
			->no, but-
				Bea: oh shut up.
				Bea: would you let me do this?
				Mae: oh, that’s cheating, putting it that way...
			->ok
	Bea: we’re all in this now
	Bea: and even if we weren’t
	Bea: i’d still be out here with you
	Gregg: me too. Obviously.
	Angus: clearly. Clearly we all would be.
	Bea: listen, I’m all the family my dad has left
	Bea: i for damn sure am not dying out here
	Bea: and you’re
	Bea: god, you’re the closest thing I have to a sister
	Bea: i don’t want you to be gone.
	Bea: so you’re not dying either.
	Mae: *cough*
	<<close>>
	<<wait 2>>
	Mae: that sounds ok
	Mae: by me
<<else>>
	Gregg: You ok dude?
		->where did...
		->what are...
	//gregg walks up to mae using Gregg_walk_crossbow ending in Gregg_bod_crossbow_down//
	Gregg: hey jerk
	Gregg: why the hell are you out here by yourself?
		->i- i had a dream...
			Mae: you were all dead
			Mae: out here in the woods
			Mae: and it was because of me
			Gregg: what does that even mean?
			Gregg: since when are your bad dreams in charge of me?
				->after all that’s happened… yeah?
				->i don’t know anymore
		->this is all because of me...
			Gregg: Oh yeah dude! all your fault!
			Gregg: you definitely made these dudes creep around
			Gregg: following us and like kidnapping someone maybe
			Gregg: yeah you did it with your effing mind control powers!
			Gregg: dude! come OFF it!
			Mae: He... it... it’s in my dreams
	Gregg: dude.
	Gregg: DUDE.
	Gregg: you don’t get to do this ok
	Gregg: i don’t get everything that’s going on with you
	Gregg: and I’m not saying it’s not happening
	Gregg: i believe you, ok? I do.
	Gregg: but you don’t get to die on me
	Gregg: you are not gonna do that to me, ok?
			->but dude, i-
				Gregg: I am not gonna lose you
				Gregg: ok? You can’t make me lose you.
				Gregg: Would you let me go like this?
				Mae: Dude... that’s cheating...
			->ok
	Gregg: We’ve been best friends since forever
	Gregg: you big dumb idiot.
	Gregg: and even if this was somehow all in your head
	Gregg: which it isn’t
	Gregg: i would still back you
	Gregg: to the actual god’s honest end.
	Bea: geez. Me too.
	Angus: clearly. Clearly we all would be.
	Gregg: I shot a dude for you! Come on!
	Gregg: and if we keep talking like this I’m gonna cry, and
	Mae: gregg
	Mae: *cough*
	<<close>>
	<<wait 2>>
	Mae: thank you
<<endif>>

Angus: Well, he’s wounded.
Angus: Sounded  like he went back up to the mine.
Bea: Yeah. What do we do?
Gregg: Follow him I guess.
	->no... i have to go alone
	->you all wait here. I’ll go
Bea: ...
Gregg: ...
Angus: ...
Gregg: anyway
Bea: yeah anyway
Angus: if he’s up in the mine we might be able to corner him and
Gregg: tie him up?
Bea: or something.
Angus: can you walk, mae?
Mae: i... i think so
Mae: might be a little slow
Angus: i’ll help you if you need it
Mae: thanks big guy

<<if $dom_friend_bea>>
	Bea: alright. Let’s go do something really stupid.
<<else>>
	Gregg: alright let’s go die or something but hopefully not
<<endif>>
<<close>>
<<fadeOut 1 wait>>
[[ByMineEntrance]]
===
title: ByMineEntrance
tags: 
colorID: 5
position: 539,69
---
<<warp Angus AngusByEntrance>>
<<warp Bea BeaByEntrance>>
<<warp Gregg GreggByEntrance>>
<<warp Mae MaeByEntrance>>
<<flip Angus -1>>
<<flip Bea -1>>
<<flip Mae 1>>
<<flip Gregg -1>>
<<cameraLookAt MinerSceneCamera 0>>
<<wait 1>>

<<fadeIn 1 wait>>
//set gregg to Gregg_bod_crossbow_down//
Angus: gate’s open
Bea: oh geez
Gregg: what?
Bea: we could like.. block it shut or something.
Gregg: aw dang
Gregg: you’re right
Mae: no...
-> he... it... whatever...
	Mae: it’s in there
	Mae: i have to go down there
-> i have to go down in there
Bea: well then
Angus: looks like all of his friends are gone
Bea: just one guy
Gregg: just one guy
Mae: just one... something...
//play gregg_bod_crossbow_down2up and hold on Gregg_bod_crossbow_up//
<<setAnimInt Gregg crossbow 2>>
Gregg: *bang bang*
//<<playerCanMoveRight 1>>
<<fadeOut 1 wait>>
<<scene MineEntrance>>
===
title: EnterLevel_A3D4
tags: 
colorID: 4
position: 265,-995
---
Bea: haven't been on a night hike in a long time.
Gregg: gotta listen for owls.
Mae: why?
Gregg: owls are cool.
Angus: alright.
Angus: i got work in the morning.
Angus: let's go catch us a ghost.
===
