title: InitLevel
tags: 
colorID: 0
position: -1847,539
---
<<if $act is 2 and $day is 4 and $did_harfest_play is 0>>
    <<show Crusty1>>
    <<show Crusty2>>
    <<show OldHobo>>
<<endif>>

<<if $act is 3 and $day is 1>>
    <<if $trigger_molly_a3d1_done is 0>>
        <<show Molly>>
    <<endif>>
<<else>>
    <<hide CopCar>>
<<endif>>

<<if $act is 3 and $day is 3 and $crusty_3_done is 0>>
    <<show Crusty3>>
<<endif>>

<<if $act is 2 and $day is 1 and $gregg_friendship_quest is 1>>
    <<show Gregg>>
    <<warp Mae GreggFQ1Mae0>>
    <<warp Gregg GreggFQ1Gregg0>>
    <<cameraLookAt GreggFQ1CameraStart>>
    <<playerFace -1>>
<<endif>>

<<if $act is 3 and $day is 4>>
    <<if $walking_up_to_the_woods>>
        <<show Angus>>
        <<show Bea>>
        <<show Gregg>>

        <<warp Angus WalkUpStartAngus>>
        <<warp Bea WalkUpStartBea>>
        <<warp Mae WalkUpStartMae>>
        <<warp Gregg WalkUpStartGregg>>
    <<endif>>
<<endif>>
===
title: EnterLevel_A2D1
tags: 
colorID: 3
position: -1256,735
---
<<if $gregg_friendship_quest is 1>>
    [[GreggFQ1]]
<<endif>>
===
title: GreggFQ1
tags: 
colorID: 3
position: -1015,680
---
<<fadeIn 2 wait>>
<<wait 1>>

<<walk Mae GreggFQ1Mae1>>
<<walk Gregg GreggFQ1Gregg1 1>>
<<wait 1>>
Gregg: And they found him out in the back of the junkyard!
<<close>>
<<wait .5>>

<<waitForMove Gregg>>

Mae: That's upsetting. 
Gregg: He'd been there for a while.
Mae: This is depressing. :\ =_=
Gregg: Really? 
Gregg: I just thought it was like super raaaawwww.:)
Mae: No, I mean the abandoned Food Donkey. o_o
Gregg: Yeah I know, right? It was a shock. :|
<<rotateHead Mae -10 .5>>
Mae: I think in 2nd grade we colored pictures for thanksgiving. :| o>
Mae: and they were like Food Donkey themed.
<<lookUp Mae>>
Mae: and they put them up on these windows.
Gregg: I remember that!
<<lookNearest Mae>>
Gregg: It was a donkey dressed up as a turkey!
<<lookOut Mae>>
<<dilate Mae .85 1>>
Mae: Yeah! Horrifying! :<
Gregg: Really, really horrible.
Mae: :| _o_
<<close>>
<<cameraLookAt GreggFollow 1>>
<<rotateHead Mae 0 .5>>
<<wait 2>>
<<lookNearest Mae>>
<<dilate Mae 1 .5>>

<<walk Gregg GreggFQ1Gregg2 1>>
<<wait .5>>
<<walk Mae GreggFQ1Mae2 1>>
<<extraZoom -5 7>>
<<cameraLookAt GreggFQ1Focal 7>>
<<lookLeft Gregg>>
Mae: What did they do with all the food?
Gregg: Donkey-turkeys got 'em.
Mae: Donkturks.
<<lookNearest Gregg>>
Gregg: Durnkeys.
<<close>>

<<waitForMove Mae>>
<<wait .5>>
<<lookRight Gregg>>
Mae: So what are we doing here?
Gregg: CRIMES. =_=
Mae: Ok I'm like double-in now. :) 
Gregg: I've been livin' too clean, Mae.
Mae: Damn right. =_= <o>
Gregg: We run this town.
Mae: Why don't we go in? o_o :|
Gregg: O_O D: \o/
Gregg: HEY!!!!!
<<close>>
<<wait 1>>
Mae: What the hell are you doing? _o_
Gregg: o_o :|
Gregg: I think I... yeah cool I got his attention.
Mae: What?
<<close>>

<<wait 1>>
Gregg: _o_
Gregg: Aw yeah here he comes.
Mae: Who?
Gregg: You don't know him I don't think.
Mae: Try me.
<<close>>

<<trackAllSpeechBubblesToCharacters 0>>
<<speechBubbleDistanceOverride 0>>

<<irisOut 1 wait>>
<<scene FoodDonkeyInterior>>
===
title: TriggerMolly
tags: 
colorID: 0
position: -1483,452
---
Molly: What were you doing out here last night?
    -> I saw someone kidnap someone. Like, a kid.
    -> What were YOU doing out out here last night?
        Molly: Vagrants. Reports of.
        Molly: That's why (waves hand at council)
        Molly: all this.
        Molly: might've just been someone saw you out here.
        Molly: Your turn
        Mae: I saw a kidnapping.
Molly: Tell me what you saw again?
Mae: Someone in a big old coat with someone over their back.
Molly: Uh huh.
Molly: And you chased them down here?
Mae: Yeah and I got to the fence and they were up in the woods.
Molly: Alright. Thank you, Mae.
Mae: Ugh. I knew you were just gonna brush this off.
Molly: Mae, there's been no missing person reports.
Molly: That *tends* to happen when a kid goes missing from something like Harfest.
Molly: Hell, I got 6 calls about kids out too late.
Molly: There would be a report.
Mae: I'M REPORTING!! This! This right here! This is a report!
Molly: Mae, there's no break in the fence.
Mae: So?
Molly: So unless someone could jump this thing...
Molly: while carrying a teenager...
Molly: who hasn't been reported as missing...
Mae: ...
Molly: Any theories, Mae?
Mae: ...
    -> No.
    -> They could've gone around the fence!
        Molly: I dunno Mae,
        Molly: this fence goes out along the parking lots
        Molly: almost to the tracks.
        Molly: For you to be right behind them,
        Molly: and then to be miraculously on the other side of the fence?
        Mae: UUUUUUUUURGH.
Molly: I was running around half the night
Molly: getting after teenage bullshit
Molly: and people scared of clotheslines and noises.
Mae: I- you- y-you- aaaargh!!!
Mae: Are you *TRYING* to kick off a horror movie?
Mae: No one believes the girl who saw the ghost!
Mae: Well, I have bad news for you-
Mae: THE COP ALWAYS DIES.
Molly: I'm sorry, we're talking about a “GHOST” now?
Mae: It must've gone through the fence??
Molly: Really, Mae?
Mae: No! Yes! I don't know!
Mae: I'm really tired.
Molly: Yeah that's kinda what I thought.
<<close>>
<<walk Molly MollyToRight wait>>
Mae: Will you at least check the woods?
Molly: Mae, me and Dan McConnell were up there this morning
Molly: because of what YOU said last night.
Molly: From the fence up to the old mine.
Molly: We found a couple teenagers on a gross mattress,
Molly: we saw some worried-looking deer,
Molly: and one deer hunter out too early.
Mae: That can't be it.
Molly: That is it.
<<close>>
<<walk Molly MollyOffRight wait>>
<<hide Molly>>
<<hide CopCar>>
Mae: THE COP ALWAYS DIES!
===
title: PreInitLevel
tags: 
colorID: 0
position: -1851,225
---
<<hide Gregg>>
<<hide Molly>>
<<hide Crusty1>>
<<hide Crusty2>>
<<hide Crusty3>>
<<hide OldHobo>>
===
title: EnterLevel_A2D4
tags: 
colorID: 3
position: -673,389
---
<<if $did_harfest_play>>
    <<hide Germ>>
    <<warp Mae MaeRight>>
    [[Harfest]]
<<else>>
<<endif>>
===
title: Harfest
tags: 
colorID: 3
position: -469,705
---
<<playMusic after_kidnapping>>
<<run Mae MaePastFoodDonkey wait>>
Mae: *pant pant*
Mae: where the hell did he go?
<<playOneShot event:/harfest/chain_fence_clang ChainFenceSFX>>
Mae: Shiiiit.
<<close>>
<<set $harfest_block_right to 1>>
===
title: TriggerLeftSide
tags: 
colorID: 3
position: -207,586
---
//camera pulls back/pans up so you can see the miner with the kid draped over his
//shoulder. there’s a solid few seconds of frozen poses here as the wind blows
//leaves around etc//
<<playMusic eide>>
<<cameraLookAt CameraLookAtEide 3>>
<<wait 10>>
//a flashing pops on behind Mae and the camera pulls back down to normal view,
//losing the guy on the hill//
<<cameraLookAt CameraBackToMaeAfterEideHill 3>>
//<<extraZoom 2 3>>
<<wait .5>>
<<close>>
<<warp Molly MollyOffRight>>
<<show Molly>>
<<walk Molly MollyToRight wait>>
Molly: Mae.
Molly: Go home.
<<hide EideHarfestStand>>
Mae: There's a guy up there on the hill!
Mae: he stole some kid?
Mae: he looked like a ghost!
//molly shines a light up//
Molly: a ghost, huh?
Mae: Or something like that?
//molly shines light back at mae//
Molly: Mae, you shouldn't be out here
Molly: doing whatever it is you're doing.
Mae: NO! NO ONE SHOULD BE OUT HERE!
Mae: But there's this guy-
Molly: I'll drive you home, Mae.
Mae: NO!
Molly: MAE! NOW.
<<close>>
<<irisOut 1 wait>>
<<set $harfest_costume to 0>>
<<scene BlackoutWoods>>
===
title: EnterLevel_A4D2
tags: 
colorID: 5
position: 443,111
---
<<if $night and $boss_convo>>
    [[FromWellWoods]]
<<elseif $night>>
    [[LimpIn]]
<<endif>>
===
title: FromWellWoods
tags: cut
colorID: 5
position: 690,408
---
Mae: We're saying goodbye to each other after the well woods.
Angus: Yes.
Gregg: Yes.
Bea: Yes.
<<close>>
<<irisOut 1 wait>>
<<scene MaeHouse left>>
===
title: LimpIn
tags: 
colorID: 5
position: 337,398
---
<<set $death_walk to 1>>
<<playIdleAnimationPlayer>>
<<cameraLookAt CameraInFrontOfFoodDonkey 0>>
<<warp Mae MaeLimpInFrom>>
<<walk Mae MaeLimpInTo wait>>
<<cameraFollowPlayer 2>>
===
title: MidwayThroughBasketBallCourt
tags: 
colorID: 5
position: 306,636
---
<<fadeOut 1 wait>>
<<scene MineWoods left>>
===
title: EnterLevel_A3D4
tags: temp
colorID: 4
position: -12,165
---
<<if $walking_up_to_the_woods>>
    <<walk Angus WalkUpEndAngus>>
    <<walk Bea WalkUpEndBea>>
    <<walk Mae WalkUpEndMae>>
    <<walk Gregg WalkUpEndGregg>>
    Mae: We're talking about walking to the woods.
    Bea: Talking and walking.
    Angus: Yes we are.
    Gregg: Indeed.
    <<set $walking_up_to_the_woods to 0>>
    <<fadeOut 1 wait>>
    <<scene MineWoods>>
<<endif>>
===
title: FarLeftFence.1
tags: 
colorID: 0
position: -939,-258
---
Mae: how did he get over this thing?
Mae: i can’t jump that. No way he could have.
Mae: there’s no hole.
Mae: how did he do it?
===
title: FarLeftFence.2
tags: 
colorID: 0
position: -697,-261
---
Mae: i mean i guess he could really be a ghost
Mae: i think he’s a ghost
Mae: ugh.
Mae: … what’s out there?
<<end>>
===
